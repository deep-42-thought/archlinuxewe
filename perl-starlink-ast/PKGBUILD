# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-starlink-ast'
pkgver='1.05'
pkgrel='1'
pkgdesc='Interface to the Starlink AST library'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-astro-fits-cfitsio' 'perl-tk') # 'perl-tk-zinc')
makedepends=('perl-test-deep' 'perl-test-number-delta' 'perl-module-build')
url='https://metacpan.org/release/Starlink-AST'
_distdir="Starlink-AST-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/T/TJ/TJENNESS/${_distdir}.tar.gz")
sha512sums=('9f1353c139cbecad4b468dbac73d27af53784ed0979fcd6dcb57277e1eae0d1f0d32d18dc4cf8d072b86e0534759c979e386ade809f75b0866a6afe953f00b31')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    perl Build.PL
    perl Build
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    perl Build test
  )
}

package() {
  cd "$srcdir/$_distdir"
  perl Build install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
