# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Leonid B <leonid dot bloch at esrf dot fr>
# Upstream contact: silx at esrf dot fr
pkgname=python-pyfai
_pkgname=pyFAI
pkgver=0.19.0
pkgrel=6
pkgdesc="Fast Azimuthal Integration in Python."
arch=('i686' 'pentium4' 'x86_64')
url="http://www.silx.org"
license=('GPLv3' 'BSD' 'MIT')
depends=(
  'python>=3.8'
  'python<3.9'
  'python-bash_kernel'
  'cython'
  'python-fabio'
  'python-h5py'
  'python-ipympl'
  'python-matplotlib'
  'python-numexpr'
  'python-numpy'
  'python-opengl'
  'python-pyqt5'
  'python-scipy'
  'python-silx')
makedepends=(
  'python-setuptools')
checkdepends=(
  'python-pyopencl'
  'xorg-server-xvfb'
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/silx-kit/${_pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('32960083e23977de860aad58b502814b8d2bf2045207c4065d4db7ac89e832fce7bb3e41a2110b62a903db40460e5490d0f25f0d79968b0005f8122539591639')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

check() {
  return
  cd "${srcdir}/${_pkgname}-${pkgver}"
  xvfb-run python setup.py test
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1
  install -D LICENSES.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D copyright "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
}
