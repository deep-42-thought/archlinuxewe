# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Leonid B <leonid dot bloch at esrf dot fr>
# Upstream contact: silx at esrf dot fr
pkgbase=python-pyfai
pkgname=(python2-pyfai python-pyfai)
_pkgname=pyFAI
pkgver=0.16.0
pkgrel=1
pkgdesc="Fast Azimuthal Integration in Python."
arch=('i686' 'x86_64')
url="http://www.silx.org"
license=('GPLv3' 'BSD' 'MIT')
_deppy2=(
  'python2>=2.7'
  'python2<2.8'
)
_deppy=(
  'python>=3.7'
  'python<3.8'
)
_depends=(
  'cython'
  'python-fabio'
  'python-h5py'
  'python-matplotlib'
  'python-numexpr'
  'python-numpy'
  'python-scipy'
  'python-silx')
_makedepends=(
  'cython'
  'python-setuptools')
_optdepends=()
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/silx-kit/${_pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('5ffe6d4b3416750a12caf08b29907cffbae07d5d7bbe793f376a0c548ba1a240828e4d4f2b6602d0921a01d143a1149ded428dc348764ab0f4e64fae2a2f98c1')

makedepends=(
  "${_deppy[@]}"
  "${_deppy2[@]}"
  "${_depends[@]}"
  "${_depends[@]//python/python2}"
  "${_makedepends[@]}"
  "${_makedepends[@]//ython/ython2}"
)

prepare() {
  cp -r "${_pkgname}-${pkgver}" "py2-${_pkgname}-${pkgver}"
  mv "${_pkgname}-${pkgver}" "py-${_pkgname}-${pkgver}"
}

build() {
  cd "${srcdir}/py2-${_pkgname}-${pkgver}"
  python2 setup.py build

  cd "${srcdir}/py-${_pkgname}-${pkgver}"
  python setup.py build
}

package_python2-pyfai() {
  depends=(
    "${_deppy2[@]}"
    "${_depends[@]//python/python2}"
  )
  optdepends=(
    "${_optdepends[@]//python/python2}"
  )
  cd "${srcdir}/py2-${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -D LICENSES.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D copyright "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
  find "${pkgdir}/usr/bin" -maxdepth 1 -type f -printf '%f\n' | \
    while read -r f; do
      mv "${pkgdir}/usr/bin/${f}" "${pkgdir}/usr/bin/python2-${f}"
      sed -i '1 s/python/python2/' "${pkgdir}/usr/bin/python2-${f}"
    done
}

package_python-pyfai() {
  depends=(
    "${_deppy[@]}"
    "${_depends[@]}"
  )
  optdepends=(
    "${_optdepends[@]}"
  )
  cd "${srcdir}/py-${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1
  install -D LICENSES.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D copyright "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
}
