# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor:chimeracoder <dev@chimeracoder.net>

pkgname='perl-ppi-html'
pkgver='1.08'
pkgrel=11
pkgdesc="Generate syntax-hightlighted HTML for Perl using PPI"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl>=5.36'
  'perl<5.37'
  'perl-css-tiny>=1.10'
  'perl-params-util>=0.05'
  'perl-ppi>=0.990')
makedepends=(
  'perl-extutils-makemaker>=6.42'
  'perl-pathtools>=0.80'
  'perl-test-simple>=0.47'
  'perl-module-install')
url='http://svn.ali.as/cpan/trunk/PPI-HTML'
source=("http://search.mcpan.org/CPAN/authors/id/A/AD/ADAMK/PPI-HTML-${pkgver}.tar.gz")
sha512sums=('84a873c580173764fb534dc36e0868dec3371ef67663e021b93229e6d142caf81408da3c4dbe205332a0a2deb3378ee82f3d37f77186ae8bd4067f3ad928dc79')
_distdir="PPI-HTML-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
