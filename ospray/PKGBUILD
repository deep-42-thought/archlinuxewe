# Contributor:  <gucong43216@gmail.com>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=ospray
pkgver=1.4.0
pkgrel=1
pkgdesc="A Ray Tracing Based Rendering Engine for High-Fidelity Visualization"
arch=('x86_64')
url="http://www.ospray.org/"
license=('Apache')
depends=('ispc' 'intel-tbb' 'embree-isa' 'libgl' 'glu' 'imagemagick'
         'libxinerama' 'libxcursor' 'glfw-x11' 'openmpi')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ospray/OSPRay/archive/v$pkgver.tar.gz")
sha512sums=('9877b29e24f8b4b66b6e6fb74c13df598f2f8053e8f32ea298ac91314ff9fed933d85e4394df268d2c28c4bcb09dfc559cfcbd1cda79cd8c38cfde29e8be0659')

prepare() {
  cd "$srcdir"

  [[ -d ${pkgname}-build ]] && rm -r ${pkgname}-build
  mkdir ${pkgname}-build

}

build() {
  cd "$srcdir/${pkgname}-build"

  export embree_DIR=/usr

  # MAX_ISA is detected in aur/embree-isa in build-time
  # Building may fail with community/embree
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DOSPRAY_MODULE_BILINEAR_PATCH:BOOL=ON \
        -DOSPRAY_MODULE_MPI:BOOL=ON \
        -DUSE_IMAGE_MAGICK:BOOL=ON \
        ../${pkgname}-${pkgver}
  make
}

package() {
  cd "$srcdir/${pkgname}-build"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
