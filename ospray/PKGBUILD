# Contributor:  <gucong43216@gmail.com>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=ospray
pkgver=1.4.1
pkgrel=1
pkgdesc="A Ray Tracing Based Rendering Engine for High-Fidelity Visualization"
arch=('x86_64')
url="http://www.ospray.org/"
license=('Apache')
depends=('ispc' 'intel-tbb' 'embree-isa' 'libgl' 'glu' 'imagemagick'
         'libxinerama' 'libxcursor' 'glfw-x11' 'openmpi')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ospray/OSPRay/archive/v$pkgver.tar.gz")
sha512sums=('1968f8d3526daa955b07e41ca9242db849ecf11bbe61fde45ca07ad95552a93d23472772bdb7d30df1f334c2ba4882e0e4b766ba451acf1b0be3b90ceea31603')

prepare() {
  cd "$srcdir"

  [[ -d ${pkgname}-build ]] && rm -r ${pkgname}-build
  mkdir ${pkgname}-build

}

build() {
  cd "$srcdir/${pkgname}-build"

  export embree_DIR=/usr

  # MAX_ISA is detected in aur/embree-isa in build-time
  # Building may fail with community/embree
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DOSPRAY_MODULE_BILINEAR_PATCH:BOOL=ON \
        -DOSPRAY_MODULE_MPI:BOOL=ON \
        -DUSE_IMAGE_MAGICK:BOOL=ON \
        ../${pkgname}-${pkgver}
  make
}

package() {
  cd "$srcdir/${pkgname}-build"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
