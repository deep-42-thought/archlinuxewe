# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Mirko Scholz
# Contributor: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Knut Franke <Knut.Franke ad gmx dot de>
# Contributor: Olaf Leidinger <leidola at newcon dot de>
# Contributor: Robal <nigrantis.tigris at gmail dot com>
# Contributor: Alexander <chronischzuspaet at gmail dot com> Kempen

# see https://svnweb.freebsd.org/ports/head/science/scidavis/Makefile?view=markup

_pkgname=scidavis
pkgname=scidavis-qt5
pkgver=2.4.0
_commit=${pkgver}
_commit_liborigin=4788f5bf674f93dab26c01bed84813e53f259c86
_commit_qwtplot3d=fce8eb0034827f0e0f86a36c075ef648343ac7cf
pkgrel=75
pkgdesc="Application for Scientific Data Analysis and Visualization, fork of QtiPlot. Qt5 port"
arch=('i686' 'pentium4' 'x86_64')
url="http://scidavis.sourceforge.net/"
license=('GPL')
makedepends=('boost' 'cmake' 'qt5-tools'
'sip4' 'python-sip4'
'qwt5-qt5>=5.2.3.1-2'
)
_pinned_dependencies=(
  'gcc-libs=12.1.0'
  'glibc>=2.33'
  'glu=9.0.2'
  'gsl=2.7.1'
  'libglvnd=1.4.0'
  'muparser=2.3.3'
  'python=3.10.4'
  'qt5-base=5.15.4+kde+r144'
  'qt5-svg=5.15.4+kde+r10'
  'zlib=1:1.2.12'
)
depends=('gsl' 'glu' 'mesa' 'muparser>=2.3.2' 'shared-mime-info'
'desktop-file-utils' 'hicolor-icon-theme'
'python-pyqt5'
'python-pyqt5-sip'
'qt5-svg'
'python' 'python-scipy'
"${_pinned_dependencies[@]}")
conflicts=('scidavis')
source=(
  "${_pkgname}-${pkgver}.tar.gz::https://github.com/highperformancecoder/scidavis/archive/${_commit}.tar.gz"
  "liborigin-${_commit_liborigin}.tar.gz::https://github.com/SciDAVis/liborigin/archive/${_commit_liborigin}.tar.gz"
  "qwtplot3d-${_commit_qwtplot3d}.tar.gz::https://github.com/SciDAVis/qwtplot3d/archive/${_commit_qwtplot3d}.tar.gz"
)
sha512sums=('9797daaf3239176eb8fc346fb8e579af965cfe0881e9048c77258f1165135a124aea3ec52434cc23e117ba2077313437da65f5c694f2d8a54d312997264d9318'
            '8f32eff3f54c2ad459b1d66a843d4c9e8641f850beb3b38a38ec18d70df916f870f2ebddac688d7eed48099c6acba2fe56995ef9bd989ba971c12b732e0588da'
            '5623d7745614267914a98e1da21a75706b51c624c7465d2770f8429b5a0947a77827c0247029bc1fb7e84d414913c7bd671740b25e07fc24d75e2d242e436d14')

prepare() {
  cd "${_pkgname}-${_commit}"
  rm -rf 3rdparty/liborigin
  mv ../liborigin-${_commit_liborigin} 3rdparty/liborigin
  rm -rf 3rdparty/qwtplot3d
  mv ../qwtplot3d-${_commit_qwtplot3d} 3rdparty/qwtplot3d
  mkdir -p build
}

build() {
  cd "${_pkgname}-${_commit}/build"
  cmake ..  -DSEARCH_FOR_UPDATES=OFF -DDOWNLOAD_LINKS=OFF -DORIGIN_IMPORT=ON -DSCRIPTING_PYTHON=ON \
            -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${_pkgname}-${_commit}/build"
  make INSTALL_ROOT="${pkgdir}" DESTDIR="${pkgdir}" install
}
