# Maintainer:  Erich Eckner <arch at eckner dot net>
# Contributor: Eric Biggers <ebiggers3 at gmail dot com>

pkgname=fscrypt-git
pkgver=0.2.5.49.gd5b8bdc
pkgrel=1
pkgdesc='A tool for managing Linux filesystem encryption'
arch=('x86_64')
url='https://github.com/google/fscrypt'
license=('Apache')
makedepends=('git' 'go')
depends=('pam')
provides=('fscrypt')
source=('git+https://github.com/google/fscrypt.git' 'pam_config')
sha512sums=('SKIP'
            '56f443cd2fbed9c164693c800cd8dc08bb35b88bb9c912fb35eb43cc6c6bc3b966d22d9810f43580d12b9faa8d753419a06336f08c47a0e602c48a103a705299')

pkgver() {
	cd "${srcdir}/fscrypt"
	git describe --tags --long | sed 's/^v//' | tr - .
}

prepare() {
	export GOPATH="${srcdir}/go"
	mkdir -p "${GOPATH}/src/github.com/google"
	ln -sf "${srcdir}/fscrypt" "${GOPATH}/src/github.com/google/"
}

build() {
	export GOPATH="${srcdir}/go"
	cd "${GOPATH}/src/github.com/google/fscrypt"
	make
}

package() {
	cd "${srcdir}/fscrypt"
	make PREFIX="${pkgdir}/usr" install
	install -Dm644 README.md "${pkgdir}/usr/share/fscrypt/README.md"
	install -Dm644 ../pam_config "${pkgdir}/etc/pam.d/fscrypt"
}
