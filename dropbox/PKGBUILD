# Maintainer:  Erich Eckner <arch at eckner dot net>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Tom < tomgparchaur at gmail dot com >
# Contributor: David Manouchehri <d@32t.ca>

# skip pin-dependencies -- we do not build, so "recompiling" will do nothing

pkgname=dropbox
pkgver=126.4.4618
pkgrel=3
pkgdesc="A free service that lets you bring your photos, docs, and videos anywhere and share them easily."
arch=("i686" "pentium4" "x86_64")
url="https://www.dropbox.com"
license=(custom)
_pinned_dependencies=(
  'gcc-libs=11.1.0'
  'glibc>=2.31'
  'libdrm=2.4.107'
  'libffi=3.3'
  'libfontconfig.so=1'
  'libfreetype.so=6'
  'libglib-2.0.so=0'
  'libglvnd=1.3.3'
  'libgthread-2.0.so=0'
  'libice=1.0.10'
  'libicudata.so=69'
  'libicui18n.so=69'
  'libicuuc.so=69'
  'libsm=1.2.3'
  'libx11=1.7.2'
  'libxcb=1.14'
  'libxcomposite=0.4.5'
  'libxdamage=1.1.5'
  'libxext=1.3.4'
  'libxfixes=6.0.0'
  'libxml2=2.9.10'
  'libxmu=1.1.3'
  'libxrender=0.9.10'
  'libxshmfence=1.3'
  'libxslt=1.1.34'
  'libxxf86vm=1.1.4'
  'mesa=21.1.4'
  'qt5-base=5.15.2+kde+r210'
  'qt5-declarative=5.15.2+kde+r29'
  'qt5-webkit=5.212.0alpha4'
  'zlib=1:1.2.11'
)
depends=('dbus' "${_pinned_dependencies[@]}")
optdepends=(
    'ufw-extras: ufw rules for dropbox'
    'perl-file-mimeinfo: opening dropbox folder on some desktop environments'
    'xdg-utils: for "Launch Dropbox Website" and file manager integration'
)
conflicts=("dropbox-experimental")
options=('!strip')

source=("dropbox.png" "dropbox.desktop" "terms.txt" "dropbox.service" "dropbox@.service")
source_i686=("https://clientupdates.dropboxstatic.com/dbx-releng/client/${pkgname}-lnx.x86-${pkgver}.tar.gz")
source_pentium4=("https://clientupdates.dropboxstatic.com/dbx-releng/client/${pkgname}-lnx.x86-${pkgver}.tar.gz")
source_x86_64=("https://clientupdates.dropboxstatic.com/dbx-releng/client/${pkgname}-lnx.x86_64-${pkgver}.tar.gz")
sha512sums=('b1a2ca11479c9f243c0368d79b36ef87910311af2dd126a3291438083544ed10a640143a58e73be1d27bf016c114e668ea504ed6eed6955370bfcac309e5fb7d'
            '690c934012a897771ed82115912fc49a81c9b1ba7ac6a6067c733b4693c9c112b1e4c808319068a2adce7b38c2cf23d3befb8ab41259b1c5280cbe8ee468c15c'
            '73db250803f4af26eca0730749b93cb23e3fd55e3645b3d8dbdb7b1959c4732674a456fe6ff2e65940caf82c6d197c00ae1ac805c4e88a1aadfd8a2a1160f2c7'
            'edb11b5a07b6a1512a5345d8ce476562fbfd9ec5642f36315a0969a2583f0e6ce371d39d25063af3734f9527ad53abdc29278c046abc2921222777af6f693a4f'
            '2d9e37272f401a65a6e1ffe3f6bcf2034cb09cc6c282145272c0e3e27f3f3518f7df488adb6368ee48f04ae89bfb0d4e77a9e4411d93d315c41366592b3abbac')
sha512sums_i686=('4ca715e5309dd2dcec53721c8be93a3ad3898550edc42f6da90e99818593f812eb59040a9df5fe88517970893d6769cd2e39961ebc5c1c73de5df22e128bd02c')
sha512sums_pentium4=('4ca715e5309dd2dcec53721c8be93a3ad3898550edc42f6da90e99818593f812eb59040a9df5fe88517970893d6769cd2e39961ebc5c1c73de5df22e128bd02c')
sha512sums_x86_64=('6c2d52bfbb53512ab4a35851dba2116f66387760e71b0744f863024599a68fa5fa948b5a5747cd45628da53cadae29a5239bb6d2412ec2f11a8b73e2e6975f15')
                                                
package() {
	if [ "${CARCH}" = 'i686' ] || [ "${CARCH}" = 'pentium4' ]; then
		_source_arch=x86
	else
		_source_arch="${CARCH}"
	fi

	install -d "$pkgdir"/opt
	cp -R "$srcdir"/.dropbox-dist/dropbox-lnx.$_source_arch-$pkgver "$pkgdir"/opt/dropbox

	find "$pkgdir"/opt/dropbox/ -type f -exec chmod 644 {} \;
	chmod 755 "$pkgdir"/opt/dropbox/dropboxd
	chmod 755 "$pkgdir"/opt/dropbox/dropbox

	install -d "$pkgdir"/usr/bin
	ln -s ../../opt/dropbox/dropbox "$pkgdir"/usr/bin/dropbox

	install -Dm644 "$srcdir"/dropbox.desktop "$pkgdir"/usr/share/applications/dropbox.desktop
	install -Dm644 "$srcdir"/dropbox.png "$pkgdir"/usr/share/pixmaps/dropbox.png
	install -Dm644 "$srcdir"/terms.txt "$pkgdir"/usr/share/licenses/$pkgname/terms.txt
	install -Dm644 "$srcdir"/dropbox.service "$pkgdir"/usr/lib/systemd/user/dropbox.service
	install -Dm644 "$srcdir"/dropbox@.service "$pkgdir"/usr/lib/systemd/system/dropbox@.service

	rm -f "$pkgdir"/opt/dropbox/library.zip
	ln -s dropbox "$pkgdir"/opt/dropbox/library.zip
}
