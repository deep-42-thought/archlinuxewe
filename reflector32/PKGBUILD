# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Xyne <ac xunilhcra enyx, backwards>
# repo: releng
pkgname=reflector32
pkgver=2021.11
pkgrel=1
pkgdesc='A Python 3 module and script to retrieve and filter the latest Pacman mirror list for Archlinux32.'
arch=(any)
license=(GPL)
url="https://xyne.archlinux.ca/projects/reflector"
depends=(python3)
optdepends=('rsync: rate rsync mirrors')
backup=(etc/xdg/reflector/reflector32.conf)
source=(
  "https://sources.archlinux32.org/sources/reflector32-${pkgver}.tar.gz"{,.sig}
)
sha512sums=('9186c66152d20e6c52b7a2f2cd8fb9e5c122f23ec70afc358bfa01dd196a94cb4ae96a7fc8974524ef632e6d591d7889e1128e1c7e0f300562b1372ca61716ed'
            'SKIP')
validpgpkeys=('DE9F7688CACF04FEB81A6C590AEEC90755DA7B5A' '38ACA6A026D25CDD227D24832F6399DCD2212195' '16194A82231E9EF823562181C8E8F5A0AF9BA7E7')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i '
    s/\b[Rr]eflector\b/\032/g
  ' \
    setup.py \
    reflector \
    Reflector.py
  sed -i '
    s/\b[Rr]eflector\b/\032/g
    s/\bmirrorlist\b/\032/g
    s@/etc/xdg/reflector32/reflector32@/etc/xdg/reflector/reflector32@
  ' \
    reflector.service \
    reflector.conf
  mv reflector{,32}
  mv Reflector{,32}.py
}

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 "man/${pkgname%32}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
  install -Dm644 'reflector.service' "$pkgdir/usr/lib/systemd/system/reflector32.service"
  install -Dm644 'reflector.timer' "$pkgdir/usr/lib/systemd/system/reflector32.timer"
  install -Dm644 'reflector.conf' "$pkgdir/etc/xdg/reflector/reflector32.conf"
}


# vim: set ts=2 sw=2 et:
