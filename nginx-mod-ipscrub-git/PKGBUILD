# Maintainer: Erich Eckner <arch at eckner dot net>

_nginxver=1.14.0

pkgname=nginx-mod-ipscrub-git
pkgver=0.r9.99230f6
pkgrel=1
pkgdesc='IP address anonymizer for nginx log files'

arch=('i686' 'x86_64')
depends=("nginx=$_nginxver")
url='https://github.com/masonicboom/ipscrub'
license=('custom')

source=(
    https://nginx.org/download/nginx-$_nginxver.tar.gz{,.asc}
    ipscrub::git+https://github.com/masonicboom/ipscrub.git#commit=${pkgver##*.}
)
validpgpkeys=(B0F4253373F8F6F510D42178520A9993A1C052F8) # Maxim Dounin <mdounin@mdounin.ru>
sha256sums=(
    '5d15becbf69aba1fe33f8d416d97edd95ea8919ea9ac519eff9bafebb6022cb5'
    'SKIP'
    'SKIP'
)

pkgver() {

  git -C "${srcdir}/ipscrub" fetch >/dev/null 2>&1

  _commit=$(
    git -C "${srcdir}/ipscrub" rev-parse --short HEAD
  )

  printf '%s.r%s.%s\n' \
    "${pkgver%%.*}" \
    "$(
      git -C "${srcdir}/ipscrub" rev-list --count "${_commit}"
    )" \
    "${_commit}"

}

build() {
    cd "$srcdir"/nginx-$_nginxver
    ./configure --with-compat \
        --add-dynamic-module=../ipscrub/ipscrub
    make modules
}

package() {

    cd "$srcdir/nginx-$_nginxver/objs"
    install -Dm755 -t "$pkgdir/usr/lib/nginx/modules/" *.so

    sed -n '/## License$/,/###/ { /##/! p }' "$srcdir/ipscrub/README.md" | \
        install -Dm644 /dev/stdin "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

}
