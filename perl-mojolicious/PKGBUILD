# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-mojolicious'
pkgver=9.24
pkgrel=1
pkgdesc='Web development toolkit'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl>=5.34'
  'perl<5.35'
  'perl-io-socket-ip>=0.37'
  'perl-scalar-list-utils>=1.41')
makedepends=(
  'perl-extutils-makemaker')
checkdepends=(
  'perl-ev>=4'
  'perl-io-socket-socks>=0.64'
  'perl-io-socket-ssl>=1.94'
  'perl-role-tiny'
  'perl-test-pod'
  'perl-test-pod-coverage')
url='https://metacpan.org/release/Mojolicious'
_distdir="Mojolicious-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/S/SR/SRI/${_distdir}.tar.gz")
sha512sums=('c8b12ca64e48b8ef56740ef1c39156f5b77bef863ffd93b8102c65f60f4addd3aca902d92f55b0c272c2916d5e4d426fb54879f5c0ddd307e389309915909893')

prepare() {
  cd "$srcdir/$_distdir"
  rm t/mojo/websocket_proxy_tls.t
  rm t/mojo/tls.t
  rm t/mojo/ioloop_tls.t
}

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    export TEST_EV=1
    export TEST_HYPNOTOAD=1
    export TEST_IPV6=1
    export TEST_MORBO=1
    export TEST_ONLINE=1
    export TEST_POD=1
    export TEST_PREFORK=1
    export TEST_SOCKS=1
    export TEST_SUBPROCESS=1
    export TEST_TLS=1
    export TEST_UNIX=1
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
