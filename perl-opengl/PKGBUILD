# Maintainer: Erich Eckner <arch at eckner dot net>
# Generator : neuesPerlPaket (54bfd7cca693b229aa53ffc98d551b62ea234aaa5be7e6717857610a2da6803b0be2dfe7ddfe9810cf320c477023b3b4356d47df7f8c202ed8c14d71b472c0bc)

pkgname='perl-opengl'
pkgver='0.70'
pkgrel=32
pkgdesc='Perl bindings to the OpenGL API, GLU, and GLUT/FreeGLUT'
arch=('x86_64' 'i686' 'pentium4')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
_pinned_dependencies=(
  'freeglut=3.2.2'
  'glibc>=2.31'
  'glu=9.0.2'
  'libglvnd=1.4.0'
  'libx11=1.8'
)
depends=(
  'perl>=5.34'
  'perl<5.35'
  "${_pinned_dependencies[@]}"
  'freeglut>=2.8.1'
  'glu>=9.0.0'
  'perl-test-simple'
  'libxmu')
makedepends=(
  'perl-extutils-makemaker'
  'mesa'
)
[ -z "$DISPLAY" ] && makedepends+=('xorg-server-xvfb')
url='https://metacpan.org/release/OpenGL'
_distdir="OpenGL-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/C/CH/CHM/${_distdir}.tar.gz")
sha512sums=('7d418015b2553dd69805252b5957bc97013260617d5a358f692e29b8da884590421c81fbc71d4dd678c93a5c89047d6af8c32f15bf0bd0e4ce514c200e634af5')

prepare() {
  # Setting these env variables overwrites any command-line-options we don't want...
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
}

build() {
  cd "$srcdir/$_distdir"
  if [ -z "$DISPLAY" ]; then
    echo "Empty \$DISPLAY - falling back to xvfb-run (xorg-server-xvfb)"
    xvfb-run -a -s "+extension GLX -screen 0 1280x1024x24" /usr/bin/perl Makefile.PL
  else
    /usr/bin/perl Makefile.PL
  fi
  make

}

check() {
  cd "$srcdir/$_distdir"
  echo "" > test.pl # Disable random X11 windows
  if [ -z "$DISPLAY" ]; then
    echo "Empty \$DISPLAY - falling back to xvfb-run (xorg-server-xvfb)"
    xvfb-run -a -s "+extension GLX -screen 0 1280x1024x24" make test
  else
    make test
  fi
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
