# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor:pzl <alsoelp _at_ gmail>
# CPAN Name  : Devel::CheckLib

pkgname='perl-devel-checklib'
pkgver=1.09
pkgrel=1
pkgdesc="Module to check if other perl modules are installed"
arch=('any')
url='http://search.cpan.org/dist/Devel-CheckLib/'
license=('PerlArtistic' 'GPL')
depends=(
  'perl>=5.4.5'
  'perl-exporter'
  'perl-file-temp>=0.16'
  'perl-pathtools')
makedepends=(
  'perl-extutils-makemaker'
  'perl-io-captureoutput>=1.0801'
  'perl-test-simple>=0.62')
checkdepends=('perl-io-captureoutput')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/MA/MATTN/Devel-CheckLib-${pkgver}.tar.gz")
sha512sums=('e0d7c284bf04af253b2a88caca87751c438c030c807aa068de35d9bab476bbdf9f25a15297e924c62e89ab781a8809750b4ef46e4e8ee6427dc817efbdb1c297')
_distdir="Devel-CheckLib-${pkgver}"

prepare() {
    export PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
        PERL_AUTOINSTALL=--skipdeps \
        PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
        PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
        MODULEBUILDRC=/dev/null
}

build() {
    cd "${srcdir}/${_distdir}"
    /usr/bin/perl Makefile.PL
    make
}

check() {
    cd "${srcdir}/${_distdir}"
    make test
}

package() {
    cd "${srcdir}/${_distdir}"
    make install

    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
