# $Id$
# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Gerardo Exequiel Pozzi <djgera@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

# repo: releng

_target_arch=$(printf '%s' x 8 6 _ 6 4 ' and ' i 6 8 6)

pkgname=archiso-dual
pkgver=43.2
pkgrel=1
pkgdesc='Tools for creating Arch Linux live and install iso images - for '"${_target_arch}"
arch=('any')
license=('GPL')
url='https://git.archlinux32.org/archlinux32/archiso32'
depends=('archiso' 'make' 'arch-install-scripts' 'squashfs-tools' 'libisoburn' 'dosfstools' 'lynx' 'pacman-mirrorlist32' 'archlinux32-keyring')
source=(
  "https://sources.archlinux32.org/sources/archiso32-dual-v${pkgver}.tar.gz"{,.sig}
)
sha512sums=('ad7f5460ebfa6a82ab5b08d31b57fb6f2835ce2c564e5e473ab9a7145f956c6a7b3e0b70181a098a8a31c6bb1eaf9fb547b060120debd89699a5add187271ca3'
            'SKIP')
validpgpkeys=('DE9F7688CACF04FEB81A6C590AEEC90755DA7B5A')

build() {
  mkdir "${srcdir}/pkg"
  make -C "${srcdir}/archiso32-dual-v${pkgver}" DESTDIR="${srcdir}/pkg" install
}

package() {

  mkdir -p "${pkgdir}/usr/share/archiso/configs"
  mv "${srcdir}/pkg/usr/share/archiso/configs/releng" "${pkgdir}/usr/share/archiso/configs/releng-dual"

  rm \
    "${srcdir}/pkg/usr/share/doc/archiso/README.altbootmethods" \
    "${srcdir}/pkg/usr/share/doc/archiso/README.transfer"

  (
    pacman -Qql archiso \
    | grep -v '/$'
    cd "${srcdir}/pkg"
    find * -not -type d \
    | sed 's,^,/,'
  ) \
  | sort \
  | uniq -d \
  | while read -r f; do
    diff -u --color "${f}" "${srcdir}/pkg${f}" || return $?
    rm "${srcdir}/pkg${f}"
  done

  if find "${srcdir}/pkg" -not -type d \
    | grep -F ''; then
    >&2 echo '^residual files found'
    return 4
  fi

}
