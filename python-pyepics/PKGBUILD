# Maintainer: Erich Eckner <arch at eckner dot net>
pkgbase=python-pyepics
pkgname=(python2-pyepics python-pyepics)
_pkgname="${pkgbase#*-}"
pkgver=3.4.0
pkgrel=4
pkgdesc='Python Interface to the Epics Channel Access protocol of the Epics control system'
arch=('i686' 'pentium4' 'x86_64')
url='https://pypi.python.org/pypi/pyepics'
license=('Epics Open License')
groups=()
_deppy2=(
  'python2>=2.7'
  'python2<2.8'
)
_deppy=(
  'python>=3.7'
  'python<3.8'
)
_makedepends=(
  'python-setuptools'
)
makedepends=(
  "${_deppy[@]}"
  "${_deppy2[@]}"
  "${_makedepends[@]}"
  "${_makedepends[@]/ython/ython2}"
)
source=(
    "https://pypi.python.org/packages/99/99/398cd2dbdabf889072546e997da635c3f6ddb3e00439653f2f41cf936875/${_pkgname}-${pkgver}.tar.gz"
)
sha512sums=('b7b2bbece6b180d21f45b3fced273b9ee98604a82543bf4579c5fc37cffc04fa3f7843dea2b38518748cae7120f41f69a41483afcf49374b8fde1e7b0b7f329b')

prepare() {
  cp -a ${_pkgname}-${pkgver}{,-py2}
}

build() {
  echo "Building python..."
  (
    cd ${_pkgname}-${pkgver}
    python setup.py build
  )
  echo "Building python2..."
  (
    cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build
  )
}

package_python-pyepics() {
  depends=(
    "${_deppy[@]}"
  )
  cd ${_pkgname}-${pkgver}
  python setup.py install --skip-build --root="${pkgdir}" --prefix=/usr
}

package_python2-pyepics() {
  depends=(
    "${_deppy2[@]}"
  )
  cd ${_pkgname}-${pkgver}-py2
  python2 setup.py install --skip-build --root="${pkgdir}" --prefix=/usr
  find "${pkgdir}" -type f -exec sed -si '1 s#[/ ]python\s*$# python2#' {} +
}
