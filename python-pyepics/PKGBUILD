# Maintainer: Erich Eckner <arch at eckner dot net>
pkgbase=python-pyepics
pkgname=(python2-pyepics python-pyepics)
_pkgname="${pkgbase#*-}"
pkgver=3.3.3
pkgrel=2
pkgdesc='Python Interface to the Epics Channel Access protocol of the Epics control system'
arch=('i686' 'pentium4' 'x86_64')
url='https://pypi.python.org/pypi/pyepics'
license=('Epics Open License')
groups=()
_deppy2=(
  'python2>=2.7'
  'python2<2.8'
)
_deppy=(
  'python>=3.7'
  'python<3.8'
)
_makedepends=(
  'python-setuptools'
)
makedepends=(
  "${_deppy[@]}"
  "${_deppy2[@]}"
  "${_makedepends[@]}"
  "${_makedepends[@]/ython/ython2}"
)
source=(
    "https://pypi.python.org/packages/e6/9a/68dbcdd21c857c396384343dddb7a2939cd7b61f3e851dda287e458e2e42/${_pkgname}-${pkgver}.tar.gz"
)
sha512sums=('522b59f678b2491ab4ddfa3863feac170c027a5abc545b93a82f73e4006f5b79544656e1f0fcb7bd080288cc271837b9cdd719c0104672bb70024d497a3f9c33')

prepare() {
  cp -a ${_pkgname}-${pkgver}{,-py2}
}

build() {
  echo "Building python..."
  (
    cd ${_pkgname}-${pkgver}
    python setup.py build
  )
  echo "Building python2..."
  (
    cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build
  )
}

package_python-pyepics() {
  depends=(
    "${_deppy[@]}"
  )
  cd ${_pkgname}-${pkgver}
  python setup.py install --skip-build --root="${pkgdir}" --prefix=/usr
}

package_python2-pyepics() {
  depends=(
    "${_deppy2[@]}"
  )
  cd ${_pkgname}-${pkgver}-py2
  python2 setup.py install --skip-build --root="${pkgdir}" --prefix=/usr
  find "${pkgdir}" -type f -exec sed -si '1 s#[/ ]python\s*$# python2#' {} +
}
