# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Dylan Whichard <dylan at whichard dot com>

pkgname=mtx-svn
pkgver=r200
_distdir="mtx-code-${pkgver}-trunk-mtx"
pkgrel=2
pkgdesc="Tools used to view information about, and to control, Media Changer devices such as Tape and DVD/CD libraries."
arch=('armv6h' 'armv7h' 'aarch64' 'i686' 'pentium4' 'x86_64')
url="https://sourceforge.net/projects/mtx/"
license=('GPL2')
provides=('mtx')
conflicts=('mtx')
makedepends=('subversion')
source=("https://sourceforge.net/code-snapshots/svn/m/mt/mtx/code/${_distdir}.zip")
sha512sums=('cd7e1f1f003acf95e1bb55936180ba6da47d146ce6bd5f87c2ba5ca02779a4e187d1a1e903c36bc6725e6d29e14574cf427933127621d3f441c95fdeb29f9f2f')

build() {
  cd "${srcdir}/${_distdir}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_distdir}"
  make prefix="$pkgdir/usr" sbindir="$pkgdir/usr/bin" mandir="$pkgdir/usr/share/man" install
}

# vim:set ts=2 sw=2 et:
