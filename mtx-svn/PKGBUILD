# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Dylan Whichard <dylan at whichard dot com>

pkgname=mtx-svn
pkgver=r200
_distdir="mtx-code-${pkgver}-trunk/mtx"
pkgrel=3
pkgdesc="Tools used to view information about, and to control, Media Changer devices such as Tape and DVD/CD libraries."
arch=('armv6h' 'armv7h' 'aarch64' 'i686' 'pentium4' 'x86_64')
url="https://sourceforge.net/projects/mtx/"
license=('GPL2')
provides=('mtx')
conflicts=('mtx')
makedepends=('subversion')
source=("https://sourceforge.net/code-snapshots/svn/m/mt/mtx/code/${_distdir%%/*}.zip")
sha512sums=('bb61c90a2507d509468b5c3a4a6785ef45989a8bb78ab10d9d72d194efe795de92761083b0dd99dee1d3bd77ffd6606f0df3eae9bd08593e9a152cb52e1ecb3d')

prepare() {
  sed -i '
    s@ | armv\*-\*@ | aarch64-*\0@
  ' "${srcdir}/${_distdir}/config.sub"
}

build() {
  cd "${srcdir}/${_distdir}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_distdir}"
  make prefix="$pkgdir/usr" sbindir="$pkgdir/usr/bin" mandir="$pkgdir/usr/share/man" install
}

# vim:set ts=2 sw=2 et:
