# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Dylan Whichard <dylan at whichard dot com>

pkgname=mtx-svn
pkgver=r200
pkgrel=10
pkgdesc="Tools used to view information about, and to control, Media Changer devices such as Tape and DVD/CD libraries."
arch=('armv6h' 'armv7h' 'aarch64' 'i486' 'i686' 'pentium4' 'x86_64')
url="https://sourceforge.net/projects/mtx/"
license=('GPL2')
provides=('mtx')
conflicts=('mtx')
_pinned_dependencies=(
  'glibc>=2.31'
)
_pinned_dependencies_armv6h=(
  'gcc-libs=12.1.0'
)
_pinned_dependencies_armv7h=(
  'gcc-libs=12.1.0'
)
depends=("${_pinned_dependencies[@]}")
depends_armv6h=("${_pinned_dependencies_armv6h[@]}")
depends_armv7h=("${_pinned_dependencies_armv7h[@]}")
makedepends=('subversion')
source=("mtx-svn::svn+https://svn.code.sf.net/p/mtx/code/trunk#revision=${pkgver#r}")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}"/mtx-svn
  printf 'r%s' \
    $(svnversion | tr -d [A-z])
}

prepare() {
  sed -i '
    s@ | armv\*-\*@ | aarch64-*\0@
  ' "${srcdir}/mtx-svn/mtx/config.sub"
}

build() {
  cd "${srcdir}/mtx-svn/mtx"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/mtx-svn/mtx"
  make prefix="$pkgdir/usr" sbindir="$pkgdir/usr/bin" mandir="$pkgdir/usr/share/man" install
}

# vim:set ts=2 sw=2 et:
