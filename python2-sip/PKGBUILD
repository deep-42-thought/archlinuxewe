# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>

pkgname='python2-sip'
pkgver=4.19.20
pkgrel=1
arch=('i686' 'pentium4' 'x86_64')
url='https://www.riverbankcomputing.com/software/sip/intro'
license=('custom:"sip"')
makedepends=('python2')
pkgdesc="Python 2.x SIP bindings for C and C++ libraries"
depends=('python2')
source=("https://www.riverbankcomputing.com/static/Downloads/sip/$pkgver/sip-$pkgver.tar.gz")
sha512sums=('d5cd49f6fcae08306ac5f993710c40b82804d6b268cf3fa14077e73f34b02d551aa3caf57a985fe39b5acd61c465b00372d2ada722ced76e670dec14b5f8de44')

prepare() {
  mkdir -p build
}

build() {
  cd "$srcdir"/build
  python2 ../sip-$pkgver/configure.py CFLAGS="$CFLAGS" LFLAGS="$LDFLAGS"
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{bin,include} # conflicts with sip

  install -Dm644 ../sip-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
