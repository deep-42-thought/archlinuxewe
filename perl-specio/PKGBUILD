# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-specio'
pkgver=0.16
pkgrel=1
pkgdesc='Type constraints and coercions for Perl'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl-class-method-modifiers'
  'perl-devel-partialdump'
  'perl-lingua-en-inflect'
  'perl-role-tiny'
  'perl-moo'
  'perl-class-load'
  'perl-devel-stacktrace'
  'perl-eval-closure'
)
checkdepends=(
  'perl-test-requires'
  'perl-test-fatal'
  'perl-namespace-autoclean'
  'perl-moose'
)
url='https://metacpan.org/release/Specio'
_distdir="Specio-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/D/DR/DROLSKY/${_distdir}.tar.gz")
sha512sums=('e47c173e22b5c9804eb097c984801f35ad4a947b6b253a056354ca0ec0841db5d5eea4964ad69d775c30e9ad6847b42270bf9ea5a6f6acea951dc8159b24c8b0')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
