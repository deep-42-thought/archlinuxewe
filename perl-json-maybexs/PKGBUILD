# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-json-maybexs'
pkgver=1.004003
pkgrel=4
pkgdesc='Use Cpanel::JSON::XS with a fallback to JSON::XS and JSON::PP'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
optdepends=(
  'perl-cpanel-json-xs')
makedepends=(
  'perl-extutils-makemaker'
  'perl-test-needs>=0.002006'
  'perl-test-simple>=0.88')
depends=(
  'perl>=5.36'
  'perl<5.37'
  'perl-carp'
  'perl-json-pp>=2.27300'
  'perl-scalar-list-utils')
checkdepends=(
  'perl-cpanel-json-xs'
  'perl-json-xs'
  'perl-test-without-module'
)
url='https://metacpan.org/release/JSON-MaybeXS'
_distdir="JSON-MaybeXS-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/E/ET/ETHER/${_distdir}.tar.gz")
sha512sums=('4690be671ea59f82ab24cc5c72d0f4a98a21f40d33c0acac853a2b15eac38933f7af3e1212ee6b5479d5e639246ebb5726c25e837996722fcfef7029dd5ed6fe')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
