# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-json-maybexs'
pkgver=1.003009
pkgrel='4'
pkgdesc='Use Cpanel::JSON::XS with a fallback to JSON::XS and JSON::PP'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl>=5.6.0'
  'perl-carp'
  'perl-json-pp>=2.27300'
  'perl-scalar-list-utils')
makedepends=(
  'perl-extutils-cbuilder>=0.27'
  'perl-extutils-makemaker'
  'perl-file-temp'
  'perl-pathtools'
  'perl-test-simple>=0.88')
optdepends=(
  'perl-cpanel-json-xs>=2.3310')
checkdepends=(
  'perl-cpanel-json-xs'
  'perl-json-xs'
  'perl-test-without-module'
)
url='https://metacpan.org/release/JSON-MaybeXS'
_distdir="JSON-MaybeXS-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/E/ET/ETHER/${_distdir}.tar.gz")
sha512sums=('a8606ccaac749650004d70bd5d6219fb18f66c4c86f720bbec3daf21a15bacd17d308c2a394fb1c9f6584be4d7c519cbc8653bdf5c7c7fa4129b75261645d8e3')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
