# Maintainer:  Erich Eckner <arch at eckner dot net>
# Contributor: Piotr Rogo≈ºa <piotr.r.public at gmail.com>
# Contributor: DarkHeart luke<at>archlinux.us

pkgname=perl-extutils-xspp
pkgver=0.18
pkgrel=12
_author=S/SM/SMUELLER
_distdir="ExtUtils-XSpp-${pkgver}"
pkgdesc='ExtUtils::XSpp - XS for C++'
arch=('any')
url="http://search.cpan.org/dist/ExtUtils-XSpp/"
license=('GPL' 'PerlArtistic')
provides=(
  'perl-extutils-xspp-cmd=0'
  'perl-extutils-xspp-driver=0'
  'perl-extutils-xspp-exception=0'
  'perl-extutils-xspp-exception-code=0'
  'perl-extutils-xspp-exception-object=0'
  'perl-extutils-xspp-exception-perlcode=0'
  'perl-extutils-xspp-exception-simple=0'
  'perl-extutils-xspp-exception-stdmessage=0'
  'perl-extutils-xspp-exception-unknown=0'
  'perl-extutils-xspp-grammar=0'
  'perl-extutils-xspp-grammar-yappdriver=1.05'
  'perl-extutils-xspp-lexer=0'
  'perl-extutils-xspp-node=0'
  'perl-extutils-xspp-node-access=0'
  'perl-extutils-xspp-node-argument=0'
  'perl-extutils-xspp-node-class=0'
  'perl-extutils-xspp-node-comment=0'
  'perl-extutils-xspp-node-constructor=0'
  'perl-extutils-xspp-node-destructor=0'
  'perl-extutils-xspp-node-enum=0'
  'perl-extutils-xspp-node-enumvalue=0'
  'perl-extutils-xspp-node-file=0'
  'perl-extutils-xspp-node-function=0'
  'perl-extutils-xspp-node-member=0'
  'perl-extutils-xspp-node-method=0'
  'perl-extutils-xspp-node-module=0'
  'perl-extutils-xspp-node-package=0'
  'perl-extutils-xspp-node-percany=0'
  'perl-extutils-xspp-node-preprocessor=0'
  'perl-extutils-xspp-node-raw=0'
  'perl-extutils-xspp-node-type=0'
  'perl-extutils-xspp-parser=0'
  'perl-extutils-xspp-plugin-feature-default_xs_typemap=0'
  'perl-extutils-xspp-typemap=0'
  'perl-extutils-xspp-typemap-parsed=0'
  'perl-extutils-xspp-typemap-reference=0'
  'perl-extutils-xspp-typemap-simple=0'
  'perl-extutils-xspp-typemap-wrapper=0')
depends=(
  'perl>=5.36'
  'perl<5.37'
  'perl-digest-md5>=2.0'
  'perl-extutils-parsexs>=3.07')
makedepends=(
  'perl-module-build>=0.40'
  'perl-test-base'
  'perl-test-differences')
checkdepends=(
  'perl-test-base'
  'perl-test-differences'
  'perl-test-pod'
)
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/$_author/${_distdir}.tar.gz")
sha256sums=('9176ad646729e3bd27cf7abf114bedd3424bff1ba61185cfc7d54f3a9223a8ff')

build(){
  cd "$srcdir/${_distdir}"

  # Setting these env variables overwrites any command-line-options we don't want...
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  /usr/bin/perl Build.PL
  perl Build
}

check(){
  cd "$srcdir/${_distdir}"
  export AUTHOR_TESTS=1
  perl Build test
}

package(){
  cd "$srcdir/${_distdir}"
  perl Build install
  # remove perllocal.pod and .packlist
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
