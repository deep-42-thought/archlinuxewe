# Contributor: James Barnett < james at wbarnett dot us >
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=avogadroapp
pkgver=1.93.0
pkgrel=17
pkgdesc="An advanced molecular editor designed for cross-platform use in computational chemistry, molecular modeling, bioinformatics, materials science, and related areas."
arch=('i686' 'pentium4' 'x86_64')
url="http://www.openchemistry.org/projects/avogadro2/"
license=('GPL2')
_pinned_dependencies=(
  'avogadrolibs=1.93.1'
  'gcc-libs=11.1.0'
  'glibc>=2.31'
  'molequeue=0.9.0'
  'qt5-base=5.15.2+kde+r193'
)
depends=('hdf5' 'vtk' 'qt5-x11extras' "${_pinned_dependencies[@]}")
optdepends=('openbabel: Open Babel plugin actions')
makedepends=('cmake' 'eigen')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenChemistry/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('8e4e15b6c2d1935f8c585c83761b28a6397bc1dc22cd5f66464d59dd25327b1cffec809af492586be4b56793448f94f9375c16e8a4c4969f7b8f2591049cc5fc')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake "${srcdir}/${pkgname}-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
