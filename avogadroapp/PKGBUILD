# Contributor: James Barnett < james at wbarnett dot us >
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=avogadroapp
pkgver=1.94.0
pkgrel=6
pkgdesc="An advanced molecular editor designed for cross-platform use in computational chemistry, molecular modeling, bioinformatics, materials science, and related areas."
arch=('i686' 'pentium4' 'x86_64')
url="http://www.openchemistry.org/projects/avogadro2/"
license=('GPL2')
_pinned_dependencies=(
  'avogadrolibs=1.94.0'
  'gcc-libs=11.1.0'
  'glibc>=2.31'
  'molequeue=0.9.0'
  'qt5-base=5.15.2+kde+r209'
)
depends=('hdf5' 'vtk' 'qt5-x11extras' "${_pinned_dependencies[@]}")
optdepends=('openbabel: Open Babel plugin actions')
makedepends=('cmake' 'eigen')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenChemistry/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('ab9b7527cd446803b48eb673ad15115a24b2b22fbee72c405955d86626c140d1870270648b112f530f51fc6d71a69942a3648867e5002f4b5bf8639c1f53d895')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake "${srcdir}/${pkgname}-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
