# Maintainer: Erich Eckner <arch at eckner dot net>
pkgbase=python-oauth2
_pkgname=python-oauth2
pkgname=(python-oauth2 python2-oauth2)
pkgver=1.1.0
pkgrel='2'
pkgdesc='framework that aims at making it easy to provide authentication via OAuth 2.0 within an application stack/'
url='https://github.com/wndhydrnt/python-oauth2/'
arch=('any')
license=('MIT')
_deppy2=(
  'python2>=2.7'
  'python2<2.8'
)
_deppy=(
  'python>=3.7'
  'python<3.8'
)
_depends=(
  'python-mock'
  'python-mysql-connector'
  'python-nose'
  'python-pymongo'
  'python-memcached'
  'python-redis'
  'python-tornado')
_makedepends=(
  'python-setuptools')
_optdepends=(
  'python-memcached'
  'python-pymongo'
  'python-redis'
)
makedepends=(
  "${_deppy[@]}"
  "${_deppy2[@]}"
  "${_depends[@]}"
  "${_depends[@]/python/python2}"
  "${_makedepends[@]}"
  "${_makedepends[@]/python/python2}"
)
source=("$pkgbase-$pkgver.tar.gz::https://github.com/wndhydrnt/python-oauth2/archive/v${pkgver}.tar.gz")
sha512sums=('376fa73711c64a787472b4be4b1e9407311886ed237dbe29399425fb5f92e2b72fab479d694e4acdbaa572aa0c43840e21cfb8fc4f128ee81240a59a3dcdc1b3')

prepare() {
  cp -a python-oauth2-${pkgver}{,-py2}
}

build() {
  echo "Building python..."
  (
    cd python-oauth2-${pkgver}
    python setup.py build
  )
  echo "Building python2..."
  (
    cd python-oauth2-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  if [ "$(hostname)" = 'rechenknecht' ]; then
    return 0
  fi
  echo "Checking python..."
  (
    cd python-oauth2-${pkgver}
    python setup.py test
  )
  echo "Checking python2..."
  (
    cd python-oauth2-${pkgver}-py2
    python2 setup.py test
  )
}

package_python-oauth2() {
  depends=(
    "${_deppy[@]}"
    "${_depends[@]}"
  )
  optdepends=("${_optdepends[@]}")

  cd python-oauth2-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

package_python2-oauth2() {
  depends=(
    "${_deppy2[@]}"
    "${_depends[@]/python/python2}"
  )
  optdepends=("${_optdepends[@]/python/python2}")

  cd python-oauth2-${pkgver}-py2
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
