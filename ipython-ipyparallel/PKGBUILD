# Maintainer: Erich Eckner <arch at eckner dot net>

pkgbase=ipython-ipyparallel
pkgname=('ipython-ipyparallel' 'ipython2-ipyparallel' 'ipython-ipyparallel-common')
pkgver=6.2.4
pkgrel=1
pkgdesc="Interactive Parallel Computing in Python"
url="https://github.com/ipython/ipyparallel"
arch=(any)
license=('BSD')
_deppy2=(
  'python2>=2.7'
  'python2<2.8'
)
_deppy=(
  'python>=3.7'
  'python<3.8'
)
_depends=(
  'ipython'
)
_makedepends=(
  'python-setuptools'
)
makedepends=(
  "${_deppy[@]}"
  "${_deppy2[@]}"
  "${_makedepends[@]}"
  "${_makedepends[@]/python/python2}"
  "${_depends[@]}"
  "${_depends[@]/python/python2}"
)
source=("https://github.com/ipython/ipyparallel/archive/${pkgver}.tar.gz")
sha512sums=('d21eb4fe647455d3819fb8be895490dea9d1da59f85b59c3b028a5211193efaf73fa05f8e8d99f4acfb2e3630ef37ae9906da812c39c5c9f004172db17c16e50')

_common_files=(
  'etc/'
  'usr/share/'
)

prepare() {
  cp -r "${srcdir}/ipyparallel-$pkgver" "${srcdir}/ipyparallel-$pkgver-py2"
}

build() {
  cd "${srcdir}/ipyparallel-$pkgver"
  python setup.py build
  
  cd "${srcdir}/ipyparallel-$pkgver-py2"
  python2 setup.py build
} 

_package_ipython-ipyparallel() {
  cd "${srcdir}/ipyparallel-$pkgver"
  python setup.py install --root="${pkgdir}"
  mv "${pkgdir}/usr/etc" "${pkgdir}/"
}

package_ipython-ipyparallel() {
  depends=(
    "${_deppy[@]}"
    "${_depends[@]}"
    'ipython-ipyparallel-common'
  )

  _package_ipython-ipyparallel
  for f in "${_common_files[@]}"; do
    rm -r --one-file-system "${pkgdir}/${f}"
  done
}

_package_ipython2-ipyparallel() {
  cd "${srcdir}/ipyparallel-$pkgver-py2"
  python2 setup.py install --root="${pkgdir}"
  mv "${pkgdir}"/usr/bin/ipcluster "${pkgdir}"/usr/bin/ipcluster2
  mv "${pkgdir}"/usr/bin/ipcontroller "${pkgdir}"/usr/bin/ipcontroller2
  mv "${pkgdir}"/usr/bin/ipengine "${pkgdir}"/usr/bin/ipengine2
  mv "${pkgdir}/usr/etc" "${pkgdir}/"
}

package_ipython2-ipyparallel() {
  depends=(
    "${_deppy2[@]}"
    "${_depends[@]/python/python2}"
    'ipython-ipyparallel-common'
  )

  _package_ipython2-ipyparallel
  for f in "${_common_files[@]}"; do
    rm -r --one-file-system "${pkgdir}/${f}"
  done
}

package_ipython-ipyparallel-common() {
  mkdir -p "${srcdir}/pkg" "${srcdir}/pkg2"
  pkgdir="${srcdir}/pkg" _package_ipython-ipyparallel
  pkgdir="${srcdir}/pkg2" _package_ipython2-ipyparallel

  for f in "${_common_files[@]}"; do
    diff -r "${srcdir}/pkg"{,2}"/${f}"
    mkdir -p "${pkgdir}/${f}"
    cp -ra "${srcdir}/pkg/${f}" "${pkgdir}/${f}"
  done
}
