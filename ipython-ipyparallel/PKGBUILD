# Maintainer: Erich Eckner <arch at eckner dot net>

pkgbase=ipython-ipyparallel
pkgname=('ipython-ipyparallel' 'ipython2-ipyparallel' 'ipython-ipyparallel-common')
pkgver=6.2.2
pkgrel=1
pkgdesc="Interactive Parallel Computing in Python"
url="https://github.com/ipython/ipyparallel"
arch=(any)
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'ipython' 'ipython2')
source=("https://github.com/ipython/ipyparallel/archive/${pkgver}.tar.gz")
sha512sums=('abfbfdc23999a442fb9a1cc1c04c71dbf456cb78af9133b7a5510819f55e645e0dc6ae9b0894c4d04a18591988c6ede8c2400cca6d11167797c7b41861f70d04')

_common_files=(
  'etc/'
  'usr/share/'
)

prepare() {
  cp -r "${srcdir}/ipyparallel-$pkgver" "${srcdir}/ipyparallel-$pkgver-py2"
}

build() {
  cd "${srcdir}/ipyparallel-$pkgver"
  python setup.py build
  
  cd "${srcdir}/ipyparallel-$pkgver-py2"
  python2 setup.py build
} 

_package_ipython-ipyparallel() {
  cd "${srcdir}/ipyparallel-$pkgver"
  python setup.py install --root="${pkgdir}"
  mv "${pkgdir}/usr/etc" "${pkgdir}/"
}

package_ipython-ipyparallel() {
  depends=('ipython' 'ipython-ipyparallel-common')

  _package_ipython-ipyparallel
  for f in "${_common_files[@]}"; do
    rm -r --one-file-system "${pkgdir}/${f}"
  done
}

_package_ipython2-ipyparallel() {
  cd "${srcdir}/ipyparallel-$pkgver-py2"
  python2 setup.py install --root="${pkgdir}"
  mv "${pkgdir}"/usr/bin/ipcluster "${pkgdir}"/usr/bin/ipcluster2
  mv "${pkgdir}"/usr/bin/ipcontroller "${pkgdir}"/usr/bin/ipcontroller2
  mv "${pkgdir}"/usr/bin/ipengine "${pkgdir}"/usr/bin/ipengine2
  mv "${pkgdir}/usr/etc" "${pkgdir}/"
}

package_ipython2-ipyparallel() {
  depends=('ipython2' 'ipython-ipyparallel-common')

  _package_ipython2-ipyparallel
  for f in "${_common_files[@]}"; do
    rm -r --one-file-system "${pkgdir}/${f}"
  done
}

package_ipython-ipyparallel-common() {
  mkdir -p "${srcdir}/pkg" "${srcdir}/pkg2"
  pkgdir="${srcdir}/pkg" _package_ipython-ipyparallel
  pkgdir="${srcdir}/pkg2" _package_ipython2-ipyparallel

  for f in "${_common_files[@]}"; do
    diff -r "${srcdir}/pkg"{,2}"/${f}"
    mkdir -p "${pkgdir}/${f}"
    cp -ra "${srcdir}/pkg/${f}" "${pkgdir}/${f}"
  done
}
