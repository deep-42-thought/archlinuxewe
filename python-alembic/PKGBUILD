# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: googol <googol@posteo.de>
# Former maintainer: Oliver Mangold <o.mangold at gmail dot com>
pkgname=python-alembic
pkgver=0.9.4
pkgrel=1
pkgdesc="Alembic is a lightweight database migration tool for usage with the SQLAlchemy Database Toolkit for Python 3."
arch=('any')
url='https://bitbucket.org/zzzeek/alembic'
license=('MIT')
depends=('python>=3.6' 'python<3.7' 'python-mako' 'python-sqlalchemy' 'python-editor')
makedepends=('python-setuptools')
source=("https://bitbucket.org/zzzeek/alembic/get/rel_${pkgver//./_}.tar.bz2")
sha512sums=('bf38de8a50de55aea210d7e11bfa7faf6abb91c3c2bd6c0c40ef1a393ba030cbca83c57b4d8b322cb3748f6add27db711f15884d0e40edbf2964c70edd438c30')

build() {
  cd "${srcdir}/zzzeek-${pkgname#*-}-"*
  python setup.py build
}

package() {
  cd "${srcdir}/zzzeek-${pkgname#*-}-"*
  python setup.py install --root "${pkgdir}"
  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
