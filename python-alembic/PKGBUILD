# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: googol <googol@posteo.de>
# Former maintainer: Oliver Mangold <o.mangold at gmail dot com>
pkgname=python-alembic
pkgver=0.8.9
pkgrel=1
pkgdesc="Alembic is a lightweight database migration tool for usage with the SQLAlchemy Database Toolkit for Python 3."
arch=('any')
url='https://bitbucket.org/zzzeek/alembic'
license=('MIT')
depends=('python' 'python-mako' 'python-sqlalchemy' 'python-editor')
makedepends=('python-setuptools')
source=("https://bitbucket.org/zzzeek/alembic/get/rel_${pkgver//./_}.tar.bz2")
sha512sums=('f8b8c26f2e89000b8ba57dff59e25addbdd8f52c629c806e85d95e16f7db9c49e72ed2cf5daeeaafab85d2c6a58729f579fb74c323100a9a24430fa9c4302be2')

build() {
  cd "${srcdir}/zzzeek-${pkgname#*-}-"*
  python setup.py build
}

package() {
  cd "${srcdir}/zzzeek-${pkgname#*-}-"*
  python setup.py install --root "${pkgdir}"
  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
