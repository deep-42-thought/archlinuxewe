# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: googol <googol@posteo.de>
# Former maintainer: Oliver Mangold <o.mangold at gmail dot com>
pkgname=python-alembic
pkgver=0.8.8
pkgrel=1
pkgdesc="Alembic is a lightweight database migration tool for usage with the SQLAlchemy Database Toolkit for Python 3."
arch=('any')
url='https://bitbucket.org/zzzeek/alembic'
license=('MIT')
depends=('python' 'python-mako' 'python-sqlalchemy' 'python-editor')
makedepends=('python-setuptools')
source=("https://bitbucket.org/zzzeek/alembic/get/rel_${pkgver//./_}.tar.bz2")
sha512sums=('9c4160a5adec811e56aaed2818e4d73a299a6b2949745c93cc780fe991965b1c58005778df2ea26cca4cfe79fb2a474e1cceeaa484675048894739b9d36ac15e')

build() {
  cd "${srcdir}/zzzeek-${pkgname#*-}-"*
  python setup.py build
}

package() {
  cd "${srcdir}/zzzeek-${pkgname#*-}-"*
  python setup.py install --root "${pkgdir}"
  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
