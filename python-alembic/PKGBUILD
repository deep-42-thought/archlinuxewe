# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: googol <googol@posteo.de>
# Former maintainer: Oliver Mangold <o.mangold at gmail dot com>
pkgname=python-alembic
pkgver=0.9.1
pkgrel=1
pkgdesc="Alembic is a lightweight database migration tool for usage with the SQLAlchemy Database Toolkit for Python 3."
arch=('any')
url='https://bitbucket.org/zzzeek/alembic'
license=('MIT')
depends=('python>=3.6' 'python<3.7' 'python-mako' 'python-sqlalchemy' 'python-editor')
makedepends=('python-setuptools')
source=("https://bitbucket.org/zzzeek/alembic/get/rel_${pkgver//./_}.tar.bz2")
sha512sums=('b6b3d4a81e8ffbacca10be0264cb595699f3aa6712795bb4e01b5b9968c9deaa509b5c789f8de9fc4abb98d2761a81dbc8bea6085d4a2eaad7cf813356abe701')

build() {
  cd "${srcdir}/zzzeek-${pkgname#*-}-"*
  python setup.py build
}

package() {
  cd "${srcdir}/zzzeek-${pkgname#*-}-"*
  python setup.py install --root "${pkgdir}"
  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
