# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: googol <googol@posteo.de>
# Former maintainer: Oliver Mangold <o.mangold at gmail dot com>
pkgname=python-alembic
pkgver=0.9.3
pkgrel=1
pkgdesc="Alembic is a lightweight database migration tool for usage with the SQLAlchemy Database Toolkit for Python 3."
arch=('any')
url='https://bitbucket.org/zzzeek/alembic'
license=('MIT')
depends=('python>=3.6' 'python<3.7' 'python-mako' 'python-sqlalchemy' 'python-editor')
makedepends=('python-setuptools')
source=("https://bitbucket.org/zzzeek/alembic/get/rel_${pkgver//./_}.tar.bz2")
sha512sums=('fd7d098085487f44b2f3fb304572d25bb2d9a039c08ce1ed4177cdf2b06a741de00b0bae1db36bb95a571d1fa37ca6ce4d759c68d4cc7da1db1bcc8d5d63ea92')

build() {
  cd "${srcdir}/zzzeek-${pkgname#*-}-"*
  python setup.py build
}

package() {
  cd "${srcdir}/zzzeek-${pkgname#*-}-"*
  python setup.py install --root "${pkgdir}"
  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
