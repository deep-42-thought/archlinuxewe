# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-log-dispatch'
pkgver=2.56
pkgrel=1
pkgdesc='Dispatches messages to one or more outputs'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl-dist-checkconflicts'
  'perl-devel-globaldestruction'
  'perl-module-runtime'
  'perl-params-validate'
)
checkdepends=(
  'perl-test-requires'
  'perl-ipc-run3'
  'perl-test-fatal'
)
url='https://metacpan.org/release/Log-Dispatch'
_distdir="Log-Dispatch-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/D/DR/DROLSKY/${_distdir}.tar.gz")
sha512sums=('daac593ed158f7e648464fce5dd23485902c45339b70ddc78cc3c712744de30d981beeb5c646dda04290d409e3141f5ab5b0995aa34d048c41c97f70a4c25619')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
