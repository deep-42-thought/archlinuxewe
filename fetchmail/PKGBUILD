# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=fetchmail
pkgver=6.4.29
pkgrel=2
pkgdesc="A remote-mail retrieval utility"
arch=('i486' 'i686' 'pentium4' 'x86_64')
url="http://www.fetchmail.info"
license=('GPL')
_pinned_dependencies=(
  'glibc>=2.31'
  'openssl=1.1.1.o'
)
depends=("${_pinned_dependencies[@]}")
makedepends=('python')
optdepends=('tk: for using fetchmailconf'
            'python2: for using fetchmailconf')
options=('!makeflags')
source=("https://sourceforge.net/projects/fetchmail/files/branch_${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        'disable-sslv3.patch' 'fetchmail.tmpfiles' 'fetchmail.sysusers' 'fetchmail.service')
sha512sums=('3fd2e34c1c45186c1261f5732cb5f907003bf9b9b3272d36a6ec43f5c64c4edf00724f71a0dc5629658a87653bf7b8cd2e19fd7ec4f9336e381caebcc0f80088'
            '1425f3655c8fe7c6ca32b6fc876fd7f0ecfb24a86f63d9ee123fe710ebbdd8fbf5b5ef0f1831cd9702de56cc707a2361a427cf2ed840b63a39beda71a18dfba3'
            'f6e3eb409a1d88bcfb02012491fdbbf859803dbee45998879525fd35a2377bae95504da8aafcb24bcc2e695e15c96f6ec6f7e6521d28012370a4ec5e2a25a351'
            'c1169c54b70be53e3093d65a2d0f1e251fadf24a8008484583d79dd3cedea13782137c0db5f9052b2d02fdbd6bd0fdcb78c6403fe39926ac636b45a44d452b29'
            '49c2843ddb06f587611f838bc9dab3fe04af991d70c27f8ffdd6351d8d6401683a356a03401e232a53e44ee43f6d85aa520f233f256974d07972913145fdf0f5')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../disable-sslv3.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-ssl=/usr
  make
}

package() {
  install -D -m644 fetchmail.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/fetchmail.conf
  install -D -m644 fetchmail.sysusers "${pkgdir}"/usr/lib/sysusers.d/fetchmail.conf
  install -D -m644 fetchmail.service "${pkgdir}"/usr/lib/systemd/system/fetchmail.service

  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -d -o 90 -g nobody "${pkgdir}/var/lib/fetchmail"
}
