# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Remi Gacogne <rgacogne at archlinux dot org>
pkgname=dnsdist
pkgver=1.7.0
_pkgver=${pkgver#*_}
_pkgver=${_pkgver//_/-}
pkgrel=11
pkgdesc='Highly DNS-, DoS- and abuse-aware loadbalancer'
arch=('x86_64')
url='https://dnsdist.org/'
license=('GPL2')
source=(https://downloads.powerdns.com/releases/${pkgname}-${_pkgver}.tar.bz2{,.asc}
        sysusers.conf
        gcc11.patch)
sha512sums=('abaed7aaa18f4acaf220dc938da6896e521373b1da3b6b9c4114467dd5d8b0e6cd9809534edbc6ba5b789ed8387fb47317c47bd4e00e66a6ccb035f6dd6068af'
            'SKIP'
            'd55ccd612cbe08b353815027d30a3b0f0ec7bf6b0d74a0a634939be53ce6e6b41d23e54c2328946f00738c03e9f306ce4f2dabe5e4b11d9fb28d0abf49917893'
            '97d12dd51ccb338fe0a907e684333936596501fdb316bdcddc7616ea723e6dfc9ca3ca3e479d17d14b317a0668658eac6fcb72e343919c0a2dea3ea99d34485d')
validpgpkeys=('D6300CABCBF469BBE392E503A208ED4F8AF58446') # Remi Gacogne <remi.gacogne@powerdns.com>
makedepends=('boost' 'systemd')
_pinned_dependencies=(
  'fstrm=0.6.1'
  'gcc-libs=11.2.0'
  'glibc>=2.31'
  'gnutls=3.7.4'
  'libcap.so=2'
  'libcdb.so=1'
  'libedit.so=0'
  'libnghttp2=1.47.0'
  'libsodium=1.0.18'
  'libsystemd.so=0'
  'lmdb=0.9.29'
  'luajit=2.1.0.beta3.r397.g20aea939'
  'net-snmp=5.9.1'
  'openssl=1.1.1.n'
  're2=1:20220401'
)
depends=('h2o' "${_pinned_dependencies[@]}")

prepare() {
  cd "${pkgname}-${_pkgver}"
  patch -p1 -i "${srcdir}/gcc11.patch"
}

build() {
  cd "${pkgname}-${_pkgver}"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-ebpf \
    --with-protobuf \
    --with-gnutls \
    --with-libsodium \
    --with-libssl \
    --with-re2 \
    --enable-dnstap \
    --enable-dns-over-tls \
    --enable-dns-over-https \
    --enable-dnscrypt \
    --enable-systemd
  make
  sed -i 's,CapabilityBoundingSet=CAP_NET_BIND_SERVICE CAP_SETGID CAP_SETUID,CapabilityBoundingSet=CAP_NET_BIND_SERVICE\nAmbientCapabilities=CAP_NET_BIND_SERVICE\nUser=dnsdist\nGroup=dnsdist,' dnsdist.service
}

package() {
  cd "${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}"
  install -D -m644 sysusers.conf "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
}
