# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Remi Gacogne <rgacogne at archlinux dot org>
pkgname=dnsdist
pkgver=1.4.9999_1.5.0_rc3
_pkgver=${pkgver#*_}
_pkgver=${_pkgver//_/-}
pkgrel=3
pkgdesc='Highly DNS-, DoS- and abuse-aware loadbalancer'
arch=('x86_64')
url='https://dnsdist.org/'
license=('GPL2')
source=(https://downloads.powerdns.com/releases/${pkgname}-${_pkgver}.tar.bz2{,.asc}
        sysusers.conf)
sha512sums=('b1885be45a12e4f793c7091c8223096f99d24e52031ed6c05c6cc7139e687e7b7ef8b3083308737b0425b3960fd3c09403bf85b6717bf4ab8adf0872628888fc'
            'SKIP'
            'd55ccd612cbe08b353815027d30a3b0f0ec7bf6b0d74a0a634939be53ce6e6b41d23e54c2328946f00738c03e9f306ce4f2dabe5e4b11d9fb28d0abf49917893')
validpgpkeys=('D6300CABCBF469BBE392E503A208ED4F8AF58446') # Remi Gacogne <remi.gacogne@powerdns.com>
makedepends=('boost' 'systemd')
_pinned_dependencies=(
  'fstrm=0.6.0'
  'gcc-libs=10.1.0'
  'glibc>=2.31'
  'gnutls=3.6.14'
  'libcap.so=2'
  'libedit=20191231_3.1'
  'libprotobuf.so=23'
  'libsodium=1.0.18'
  'libsystemd.so=0'
  'lmdb=0.9.25'
  'luajit=2.0.5'
  'net-snmp=5.8'
  'openssl=1.1.1.g'
  're2=1:20200706'
  'tinycdb=0.78'
)
depends=('h2o' "${_pinned_dependencies[@]}")

build() {
  cd "${pkgname}-${_pkgver}"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-ebpf \
    --with-protobuf \
    --with-gnutls \
    --with-libsodium \
    --with-libssl \
    --with-re2 \
    --enable-dnstap \
    --enable-dns-over-tls \
    --enable-dns-over-https \
    --enable-dnscrypt \
    --enable-systemd
  make
  sed -i 's,CapabilityBoundingSet=CAP_NET_BIND_SERVICE CAP_SETGID CAP_SETUID,CapabilityBoundingSet=CAP_NET_BIND_SERVICE\nAmbientCapabilities=CAP_NET_BIND_SERVICE\nUser=dnsdist\nGroup=dnsdist,' dnsdist.service
}

package() {
  cd "${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}"
  install -D -m644 sysusers.conf "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
}
