# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Kyle Keen <keenerd@gmail.com>
pkgname=hyperrogue
pkgver=12.0m
pkgrel=2
pkgdesc="You are a lone outsider in a strange, non-Euclidean hyperbolic world.  Optional music."
arch=('i686' 'pentium4' 'x86_64')
url="http://www.roguetemple.com/z/hyper.php"
license=('GPL2')
_pinned_dependencies=(
  'gcc-libs=12.1.0'
  'glibc>=2.31'
  'libglvnd=1.4.0'
  'sdl_gfx=2.0.26'
  'zlib=1:1.2.12'
)
depends=('sdl_ttf' 'sdl_mixer' 'mesa' 'ttf-dejavu' "${_pinned_dependencies[@]}")
makedepends=('glu')

source=("https://github.com/zenorogue/hyperrogue/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('7d5e3797b9e23c0a480390bf6dd0005310d0f64a822cdcacb8d5173df4f3c0e1dded0331e28c9df73b5edf4615a335b098e4331c50b27d53ba73b360e24416c3')

prepare() {
    cd "$srcdir/$pkgname-${pkgver}"
    sed -i 's|TTF_OpenFont.*;|TTF_OpenFont("/usr/share/fonts/TTF/DejaVuSans-Bold.ttf", siz);|' basegraph.cpp
    #sed -i 's|<SDL.h>|"SDL.h"|' /usr/include/SDL/SDL_gfxPrimitives.h  # todo: confirm bug and open
    sed -i 's|-c hyper.cpp|& -I/usr/include/SDL/|' Makefile  # remove when above bug is fixed
    sed -i 's|savepng.c|savepng.cpp|' Makefile
    sed -i 's|-Werror||' Makefile
}

build() {
    cd "$srcdir/$pkgname-${pkgver}"
    make
}

package() {
    cd "$srcdir/$pkgname-${pkgver}"
    install -Dm755 hyperrogue "$pkgdir/usr/bin/hyperrogue"
}

