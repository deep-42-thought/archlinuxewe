# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Kyle Keen <keenerd@gmail.com>
pkgname=hyperrogue
pkgver=12.0j
pkgrel=2
pkgdesc="You are a lone outsider in a strange, non-Euclidean hyperbolic world.  Optional music."
arch=('i686' 'pentium4' 'x86_64')
url="http://www.roguetemple.com/z/hyper.php"
license=('GPL2')
_pinned_dependencies=(
  'gcc-libs=11.1.0'
  'glibc>=2.31'
  'libglvnd=1.4.0'
  'sdl_gfx=2.0.26'
  'zlib=1:1.2.11'
)
depends=('sdl_ttf' 'sdl_mixer' 'mesa' 'ttf-dejavu' "${_pinned_dependencies[@]}")
makedepends=('glu')

source=("https://github.com/zenorogue/hyperrogue/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('38c1a669679dee5faed2ec5dc05c793df4a3599a7d51e6c79042da4bb4801ba29578f0b7e08fff6c22fbfde37a03e5eb151812354f687993e4caa9cde1ed0171')

prepare() {
    cd "$srcdir/$pkgname-${pkgver}"
    sed -i 's|TTF_OpenFont.*;|TTF_OpenFont("/usr/share/fonts/TTF/DejaVuSans-Bold.ttf", siz);|' basegraph.cpp
    #sed -i 's|<SDL.h>|"SDL.h"|' /usr/include/SDL/SDL_gfxPrimitives.h  # todo: confirm bug and open
    sed -i 's|-c hyper.cpp|& -I/usr/include/SDL/|' Makefile  # remove when above bug is fixed
    sed -i 's|savepng.c|savepng.cpp|' Makefile
    sed -i 's|-Werror||' Makefile
}

build() {
    cd "$srcdir/$pkgname-${pkgver}"
    make
}

package() {
    cd "$srcdir/$pkgname-${pkgver}"
    install -Dm755 hyperrogue "$pkgdir/usr/bin/hyperrogue"
}

