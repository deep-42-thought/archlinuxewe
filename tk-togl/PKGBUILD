# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Mirko Scholz <srtlg>
pkgname=tk-togl
pkgver=2.0
pkgrel=21
pkgdesc="A Tcl/Tk widget for OpenGL rendering."
arch=('i686' 'pentium4' 'x86_64')
url="https://sourceforge.net/projects/togl/"
license=('BSD')
_pinned_dependencies=(
  'glibc>=2.31'
  'libglvnd=1.4.0'
  'libx11=1.8'
  'libxmu=1.1.3'
)
depends=(tk tcl "${_pinned_dependencies[@]}")
_pkgname=Togl
source=("${_pkgname}${pkgver}.tar.gz::https://sourceforge.net/projects/togl/files/Togl/2.0/${_pkgname}${pkgver}-src.tar.gz"
        "fix-configure.patch")
sha256sums=('b7d4a90bbad3aca618d505ee99e7fd8fb04c829f63231dda2360f557ba3f7610'
            '79d1519517787c1a36d3493dde915a06005633fff6fba60d88b4516fce3d0268')

prepare() {
    cd ${_pkgname}${pkgver}
    patch -p1 -i "$srcdir/fix-configure.patch"
}

build() {
    cd ${_pkgname}${pkgver}
    ./configure
    make
}

package() {
    cd ${_pkgname}${pkgver}
    make install "DESTDIR=$pkgdir"
    mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
    ln -s /usr/lib/Togl2.0/LICENSE "$pkgdir/usr/share/licenses/$pkgname"
    rm -rf "$pkgdir/usr/bin"
    chmod 444 "$pkgdir/usr/lib/libToglstub2.0.a"
}

