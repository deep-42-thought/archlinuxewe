# Maintainer:  Erich Eckner <arch at eckner dot net>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python2-qtconsole
pkgver=4.6.0
pkgrel=1
pkgdesc="Qt-based console for Jupyter with support for rich media output"
arch=('any')
url="https://pypi.org/project/qtconsole/"
license=('BSD')
makedepends=('python2-setuptools')
source=("$pkgbase-$pkgver.tgz::https://github.com/jupyter/qtconsole/archive/$pkgver.tar.gz")
sha512sums=('4e6ae477796d7cc46f009658f29275ed75467f1e5c2436552b7a6df73eb35a382a54b4dc06ebe8827e2a009b79f08166dbce869832cb545fcc4db4299df1e004')
depends=('python2-ipykernel' 'python2-jupyter_core' 'python2-jupyter_client'
         'python2-traitlets' 'python2-pygments' 'python2-pyzmq'
         'qt5-svg' 'python2-pyqt5' 'python2-sip')

build() {
  cd "$srcdir/qtconsole-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/qtconsole-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr/bin"
  mv jupyter-qtconsole jupyter-qtconsole-py2
}
