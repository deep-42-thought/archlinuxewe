# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python2-qtconsole
pkgver=4.5.5
pkgrel=3
pkgdesc="Qt-based console for Jupyter with support for rich media output"
arch=('any')
url="https://pypi.org/project/qtconsole/"
license=('BSD')
makedepends=('python2-setuptools' 'qt5-svg')
source=("$pkgbase-$pkgver.tgz::https://github.com/jupyter/qtconsole/archive/$pkgver.tar.gz")
md5sums=('b6e58c890712d9522f2bb2d28b5ace39')
depends=('python2-ipykernel' 'python2-jupyter_core' 'python2-jupyter_client'
         'python2-traitlets' 'python2-pygments' 'python2-pyzmq'
         'qt5-svg' 'python2-pyqt5' 'python2-sip')

build() {
  cd "$srcdir/qtconsole-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/qtconsole-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr/bin"
  mv jupyter-qtconsole jupyter-qtconsole-py2
}
