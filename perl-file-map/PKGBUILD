# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=perl-file-map
_pkgname='File-Map'
pkgver=0.67
pkgrel=5
_distdir="${_pkgname}-${pkgver}"
pkgdesc="Memory mapping made simple and safe."
arch=('x86_64' 'i686' 'pentium4')
url="https://github.com/Leont/file-map"
license=('GPL' 'PerlArtistic')
_pinned_dependencies=(
  'glibc>=2.31'
)
makedepends=(
  'perl-io'
  'perl-module-build>=0.28'
  'perl-scalar-list-utils'
  'perl-test-fatal'
  'perl-test-simple'
  'perl-test-warnings>=0.005'
  'perl-time-hires')
depends=(
  'perl>=5.36'
  'perl<5.37'
  "${_pinned_dependencies[@]}"
  'perl-carp'
  'perl-perlio-layers'
  'perl-sub-exporter-progressive>=0.001005'
  'perl-xsloader')
checkdepends=(
  'perl-perlio-layers'
  'perl-sub-exporter-progressive'
  'perl-test-fatal')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/L/LE/LEONT/${_distdir}.tar.gz")
sha512sums=('52bd9ebb546e6b0e0d58cce3e5f5983c0f89bf05ba3f29d220b6d83ca150e200ac9c6e7d2adb717838c0b20e9ddd68c94509b47775d27273c37bc44dcff1d507')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_distdir}"
    perl Build.PL
    perl Build
  )
}

check() {
  cd "${srcdir}/${_distdir}"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    perl Build test
  )
}

package() {
  cd "${srcdir}/${_distdir}"
  perl Build install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
