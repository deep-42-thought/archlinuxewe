# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='python-pyshortcuts'
_pkgname='pyshortcuts'
pkgver=1.7.2
pkgrel=1
pkgdesc='create desktop shortcuts to python scripts on Windows, Mac, or Linux'
arch=('any')
url='https://github.com/newville/pyshortcuts'
license=('MIT')
makedepends=(
  'python-setuptools')
depends=(

  'python>=3.8'
  'python<3.9')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/newville/${_pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('4a4a1951faad374a6046b7b31dd94ae055042a806c33df9cc093d84a0f61563b152b40a4baf06a1e0c9e8d408a2ca8fdcd04b1a9138297995ee111b905eb9130')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  errors=$(
    find "${pkgdir}/usr/lib"/python*/site-packages/pyshortcuts-*.egg-info -name 'requires.txt' -execdir cat {} \; \
      | sed 's/^/python-/' \
      | grep -vxF "$(printf '%s\n' "${depends[@]}")"
  ) || true
  if [ -n "${errors}" ]; then
    echo 'missing dependencies:'
    printf '%s\n' "${errors}"
    return 1
  fi
  rm -rf --one-file-system "${pkgdir}/usr/bin"
}
