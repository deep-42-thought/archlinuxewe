# Maintainer: Erich Eckner <arch at eckner dot net>

pkgbase=python-pyshortcuts
pkgname=('python-pyshortcuts' 'python2-pyshortcuts')
_pkgname='pyshortcuts'
pkgver=1.6
pkgrel=1
pkgdesc='create desktop shortcuts to python scripts on Windows, Mac, or Linux'
arch=('any')
url='https://github.com/newville/pyshortcuts'
license=('MIT')
_deppy2=(
  'python2>=2.7'
  'python2<2.8'
)
_deppy=(
  'python>=3.7'
  'python<3.8'
)
_depends=(
  'python-six')
_makedepends=(
  'python-setuptools')
makedepends=(
  "${_deppy[@]}"
  "${_deppy2[@]}"
  "${_depends[@]}"
  "${_depends[@]/python/python2}"
  "${_makedepends[@]}"
  "${_makedepends[@]/python/python2}"
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/newville/${_pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('d9facff4dff00478e0f79bb4413204066a80da8bb65f33e6f826e987930b386f4356211b09d1be1940d91b1e68f53d021339a490f693ad55130d151a2974ab3b')

prepare() {
  cd "${srcdir}"

  cp -a "${_pkgname}-${pkgver}" "${pkgname[0]}"
  mv "${_pkgname}-${pkgver}" "${pkgname[1]}"
}

build() {
  cd "${srcdir}/${pkgname[0]}"
  python setup.py build

  cd "${srcdir}/${pkgname[1]}"
  python2 setup.py build
}

package_python-pyshortcuts() {
  depends=(
    "${_deppy[@]}"
    "${_depends[@]}"
  )

  cd "${srcdir}/${pkgname[0]}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  errors=$(
    find "${pkgdir}/usr/lib"/python*/site-packages/pyshortcuts-*.egg-info -name 'requires.txt' -execdir cat {} \; \
      | sed 's/^/python-/' \
      | grep -vxF "$(printf '%s\n' "${depends[@]}")"
  ) || true
  if [ -n "${errors}" ]; then
    echo 'missing dependencies:'
    printf '%s\n' "${errors}"
    return 1
  fi
  rm -rf --one-file-system "${pkgdir}/usr/lib"/python*/site-packages/pyshortcuts-*.egg-info "${pkgdir}/usr/bin"
}

package_python2-pyshortcuts() {
  depends=(
    "${_deppy2[@]}"
    "${_depends[@]/python/python2}"
  )

  cd "${srcdir}/${pkgname[1]}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  errors=$(
    find "${pkgdir}/usr/lib"/python*/site-packages/pyshortcuts-*.egg-info -name 'requires.txt' -execdir cat {} \; \
      | sed 's/^/python2-/' \
      | grep -vxF "$(printf '%s\n' "${depends[@]}")"
  ) || true
  if [ -n "${errors}" ]; then
    echo 'missing dependencies:'
    printf '%s\n' "${errors}"
    return 1
  fi
  rm -rf --one-file-system "${pkgdir}/usr/lib"/python*/site-packages/pyshortcuts-*.egg-info "${pkgdir}/usr/bin"
}

