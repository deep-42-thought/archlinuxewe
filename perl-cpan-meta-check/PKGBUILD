# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Moritz Bunkus <moritz@bunkus.org>

pkgname='perl-cpan-meta-check'
pkgver=0.014
pkgrel='2'
pkgdesc="Verify requirements in a CPAN::Meta object"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl>=5.6.0'
  'perl-base'
  'perl-cpan-meta>=2.132830'
  'perl-cpan-meta-requirements>=2.121'
  'perl-exporter'
  'perl-module-metadata>=1.000023')
makedepends=(
  'perl-env'
  'perl-extutils-makemaker'
  'perl-lib'
  'perl-test-deep'
  'perl-test-simple>=0.88')
url='http://search.cpan.org/dist/CPAN-Meta-Check'
_distdir="CPAN-Meta-Check-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/L/LE/LEONT/${_distdir}.tar.gz")
sha512sums=('a10af09a7ce771ca3983adea8053d141750fd09849609a28e7f973831fd3a3ece55281841aec93c099154bb59135986da4983bf612ae4e956c45dbb3f2770f5e')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                   \
    PERL_AUTOINSTALL=--skipdeps                              \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='${pkgdir}'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '${pkgdir}'" \
    MODULEBUILDRC=/dev/null
  cd "${srcdir}/${_distdir}"
}

build() {
  prepare_environment
  perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
