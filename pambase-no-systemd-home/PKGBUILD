# Maintainer: Erich Eckner <arch at eckner dot net>

arch=('any')
pkgname=pambase-no-systemd-home
pkgver=20211210
pkgrel=1

source=("http://ftp.gwdg.de/pub/linux/archlinux/core/os/x86_64/pambase-$pkgver-${pkgrel%.*}-any.pkg.tar.zst" system-auth.patch)
sha512sums=('e9640c59e0e6468c37f1335a8f88041a94aeb8f64be7ee5b434cac5043439767ad789073ca298d693e8ed08bb3151c04ef4360b1d59c9bb4917df2bb27033393'
            '1577816a179dd7c5e460cf673022a5cc7bb3ffadcbbb6b00e2452587e39a8b184692e4517c4d43018a6e28d185397a2adcf0aed3744f773d77e41e37871875f8')
provides=("pambase=$pkgver-${pkgrel%.*}")
conflicts=('pambase')

package() {
  cd "$srcdir"
  patch -p1 -i 'system-auth.patch'
  mv 'etc' "$pkgdir/"

  while read -r var eq value; do
    [ "x$eq" = 'x=' ] || continue
    case "$var" in
      pkgdesc|url|license)
        eval "$var"'="$value"'
      ;;
      backup)
        backup+=("$value")
      ;;
    esac
  done < "$srcdir/.PKGINFO"
}
