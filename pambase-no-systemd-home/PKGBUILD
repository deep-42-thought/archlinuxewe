# Maintainer: Erich Eckner <arch at eckner dot net>

arch=('any')
pkgname=pambase-no-systemd-home
pkgver=20210605
pkgrel=2.1

source=("http://ftp.gwdg.de/pub/linux/archlinux/core/os/x86_64/pambase-$pkgver-${pkgrel%.*}-any.pkg.tar.zst" system-auth.patch)
sha512sums=('f884f32d265f4920cb237c6258a4eda907e5a9b332eb44b0d0b489a8126c6c93e11fdf7f45cdfe205994b686f1d41984b78a03e60fd484c888e4da943207f621'
            '1577816a179dd7c5e460cf673022a5cc7bb3ffadcbbb6b00e2452587e39a8b184692e4517c4d43018a6e28d185397a2adcf0aed3744f773d77e41e37871875f8')
provides=("pambase=$pkgver-${pkgrel%.*}")
conflicts=('pambase')

package() {
  cd "$srcdir"
  patch -p1 -i 'system-auth.patch'
  mv 'etc' "$pkgdir/"

  while read -r var eq value; do
    [ "x$eq" = 'x=' ] || continue
    case "$var" in
      pkgdesc|url|license)
        eval "$var"'="$value"'
      ;;
      backup)
        backup+=("$value")
      ;;
    esac
  done < "$srcdir/.PKGINFO"
}
