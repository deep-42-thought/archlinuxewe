# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=xraylarch
pkgver=0.9.45
pkgrel=2
pkgdesc="Data Analysis Tools for X-ray Spectroscopy"
arch=('i686' 'pentium4' 'x86_64')
url="http://xraypy.github.io/xraylarch/"
license=('BSD')
groups=()
depends=(
  'python>=3.7'
  'python<3.8'
  'python-asteval>=0.9.12'
  'python-h5py>=2.4'
  'python-lmfit>=0.9.11'
  'python-matplotlib>=2.0'
  'python-numpy>=1.10'
  'python-peakutils>=1.0.0'
  'python-pillow>=3.4'
  'python-psutil'
  'python-pyepics>=3.2.4'
  'python-pyfai>=0.12.0'
  'python-pyaml'
  'python-pyshortcuts'
  'python-requests>=2.1'
  'python-scikit-learn>=0.18'
  'python-scipy>=0.17'
  'python-silx'
  'python-six>=1.10'
  'python-sqlalchemy>=1.1.5'
  'python-termcolor'
  'python-uncertainties>=3.0'
  'python-wxpython>=2.8.12'
  'python-wxutils'
  'python-wxmplot'
)
makedepends=()
checkdepends=(
  'python-pytest'
)
optdepends=(
  'python-CifFile:        needed for xrd modules'
  'python-epics:          needed for using the EPICS control system'
  'python-fabio:          needed for xrd modules'
  'python-nose:           needed for testing tools'
  'python-pyFAI:          needed for xrd modules'
  'python-xrayutilities:  needed for xrd modules'
  'python-yaml:           needed for graphics and plotting'
)
provides=()
conflicts=()
replaces=()
backup=()
options=()
source=(
    "${pkgname}-${pkgver}.tar.gz::https://github.com/xraypy/${pkgname}/archive/${pkgver}.tar.gz"
    'remove-icons.patch'
)
sha512sums=(
    '227e0ccbf599cf9ef3ba69bb48005202e3166f97c5d24a99883ad2a94c5013ab60ac24494b87514a6008899209532c305829506c0d01dc5b3eaf60a74f2087ab'
    '8a2cff40315c38cb17a6841731704d5a375d25a13949227ad914abfdd46aef55dcc24e375176119634df773ece63422a2dc64fc843b6bae40232ffa2a5bb80ab'
)

prepare() {

  cd "${pkgname}-${pkgver}"
  patch -p1 -i "../remove-icons.patch"

}

build() {

  cd "${pkgname}-${pkgver}"
  sed "s|DEBUG = False|DEBUG = True|" -i setup.py
  python setup.py build

}

check() {

  cd "${pkgname}-${pkgver}"
  python setup.py check

}

package() {

  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build --root="${pkgdir}" --prefix=/usr

}
