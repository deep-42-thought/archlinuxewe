# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=xraylarch
pkgver=0.9.41
pkgrel=1
pkgdesc="Data Analysis Tools for X-ray Spectroscopy"
arch=('i686' 'x86_64')
url="http://xraypy.github.io/xraylarch/"
license=('BSD')
groups=()
depends=(
  'python2>=2.7'
  'python2<2.8'
  'python2-asteval>=0.9.12'
  'python2-backports.functools_lru_cache'
#  'python2-epics>=3.2.4'
  'python2-h5py>=2.4'
  'python2-lmfit>=0.9.11'
  'python2-matplotlib>=2.0'
  'python2-numpy>=1.10'
  'python2-peakutils>=1.0.0'
  'python2-pillow>=3.4'
  'python2-psutil'
  'python2-pyfai>=0.12.0'
  'python2-pyaml'
  'python2-pyshortcuts'
  'python2-requests>=2.1'
  'python2-scikit-learn>=0.18'
  'python2-scipy>=0.17'
  'python2-silx'
  'python2-six>=1.10'
  'python2-sqlalchemy>=1.1.5'
  'python2-termcolor'
  'python2-uncertainties>=3.0'
  'python2-wxpython3>=2.8.12'
  'python2-wxutils'
  'python2-wxmplot'
)
makedepends=()
checkdepends=(
  'python2-pytest'
)
optdepends=(
  'python2-CifFile:        needed for xrd modules'
  'python2-epics:          needed for using the EPICS control system'
  'python2-fabio:          needed for xrd modules'
  'python2-nose:           needed for testing tools'
  'python2-pyFAI:          needed for xrd modules'
  'python2-xrayutilities:  needed for xrd modules'
  'python2-yaml:           needed for graphics and plotting'
)
provides=()
conflicts=()
replaces=()
backup=()
options=()
source=(
    "${pkgname}-${pkgver}.tar.gz::https://github.com/xraypy/${pkgname}/archive/${pkgver}.tar.gz"
)
sha512sums=('49d8f07bb3a03c8f54a99621bb7c9100d4e5fdbe4105c8a3b57381e0f050dbc9354fa1526a76396604a8b84be0638be85e8cdd56ec3254e7e2ed47e36a62b757')

prepare() {

  sed -i '
    /^\s*pyexe\s*=/ s/python/python2/
    s/^\s*setup(/_setup = \0/
    s/^\(\s*dest\s*=\s*\)\(\S.*\)$/\1_setup.get_command_obj('"'"'install'"'"').root + os.path.abspath(\2)/
    /subprocess\.check_call/ { N; d; }
  ' "${pkgname}-${pkgver}/setup.py"

  if [ "${CARCH}" = 'i686' ]; then
    sed -i '
      /^\s*compiled_exes\s*=\s*(/,/)/ {
        s/(.*/(/
        t
        s/.*)/)/
        t
        d
      }
    ' "${pkgname}-${pkgver}/setup.py"
  fi

}

build() {

  cd "${pkgname}-${pkgver}"
  sed "s|DEBUG = False|DEBUG = True|" -i setup.py
  python2 setup.py build

}

check() {

  cd "${pkgname}-${pkgver}"
  python2 setup.py check

}

package() {

  cd "${pkgname}-${pkgver}"
  python2 setup.py install --skip-build --root="${pkgdir}" --prefix=/usr
  sed 's| python$| python2|' -i "${pkgdir}/usr/bin/"*

}
