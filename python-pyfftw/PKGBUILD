# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Antony Lee <anntzer dot lee at gmail dot com>

_pyname=pyFFTW
pkgname=("python-pyfftw" "python2-pyfftw")
pkgbase="python-pyfftw"
pkgver=0.11.0
pkgrel=1
pkgdesc='A pythonic wrapper around FFTW.'
url='http://hgomersall.github.io/pyFFTW/'
_deppy2=(
  'python2>=2.7'
  'python2<2.8'
)
_deppy=(
  'python>=3.7'
  'python<3.8'
)
_depends=(
  'python-numpy'
  'python-scipy'
  'fftw'
)
_makedepends=(
  'cython'
  'python-setuptools'
)
makedepends=(
  "${_deppy[@]}"
  "${_deppy2[@]}"
  "${_depends[@]}"
  "${_depends[@]/python-/python2-}"
  "${_makedepends[@]}"
  "${_makedepends[@]/ython/ython2}"
)
license=('BSD')
arch=('i686' 'x86_64')
source=("https://pypi.python.org/packages/64/39/8ab4ffd393bfff34619195227f6cba218972ab9536a46a4845b4e3c8ab25/pyFFTW-${pkgver}.tar.gz")
sha512sums=('04dbc48136359f324551755682f9e87672ff896c0842db2d1ecd98a0cccf05186cb638a19ea79fe5d5c0199bdd3c21a107d62e117b362a89b8fe79c6c7ec15cb')

build() {
  cd $srcdir/$_pyname-$pkgver
  python setup.py build_ext --inplace
  python setup.py test
}

package_python-pyfftw() {
  depends=(
    "${_deppy[@]}"
    "${_depends[@]}"
  )
  cd $srcdir/$_pyname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 
  install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pyfftw() {
  depends=(
    "${_deppy2[@]}"
    "${_depends[@]/python-/python2-}"
  )
  cd $srcdir/$_pyname-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1 
  install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
