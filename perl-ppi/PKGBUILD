# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor:chimeracoder <dev@chimeracoder.net>

pkgname='perl-ppi'
pkgver=1.234
pkgrel=1
pkgdesc="Parse, Analyze and Manipulate Perl (without perl)"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl>=5.6.0'
  'perl-clone>=0.30'
  'perl-digest-md5>=2.35'
  'perl-io-string>=1.07'
  'perl-list-moreutils>=0.16'
  'perl-params-util>=1.00'
  'perl-pathtools>=3.27.01'
  'perl-scalar-list-utils>=1.33'
  'perl-storable>=2.17'
  'perl-task-weaken')
makedepends=(
  'perl-class-inspector>=1.22'
  'perl-extutils-makemaker>=6.59'
  'perl-file-remove>=1.42'
  'perl-test-deep'
  'perl-test-object>=0.07'
  'perl-test-simple>=0.86'
  'perl-test-subcalls>=1.07')
checkdepends=('perl-test-nowarnings>=0.084' 'perl-test-object>=0.07' 'perl-test-subcalls>=1.07')
url='http://search.mcpan.org/dist/PPI'
source=("http://search.mcpan.org/CPAN/authors/id/M/MI/MITHALDU/PPI-${pkgver}.tar.gz")
sha512sums=('9284a3c9799c602491e26cd941b39bd80c1ce9ca03eeef5fd0d06cd2af543fabd6f0b65a068d1d2a8dc24febeb5917b0f8284d582519125dfeda87dd2eea0316')
_distdir="PPI-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
