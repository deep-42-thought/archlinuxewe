# Maintainer: Erich Eckner <arch at eckner dot net>
# CPAN Name  : ExtUtils::F77
# Contributor: Anton Leontiev <bunder /at/ t-25.ru>
# Generator  : CPANPLUS::Dist::Arch 1.30

pkgname=perl-extutils-f77
pkgver=1.20
pkgrel=1
pkgdesc='Perl module providing simple interface to F77 libraries'
arch=('any')
url='https://metacpan.org/release/ExtUtils-F77'
license=('PerlArtistic' 'GPL')
depends=('perl' 'gcc-fortran')
source=(http://search.cpan.org/CPAN/authors/id/C/CH/CHM/ExtUtils-F77-${pkgver}.tar.gz)
options=(!emptydirs)
sha512sums=('0d7cf351d84676d3dea2ec781b1e230ab4a737d34de4c15c5045603ac6e6e411b5d9b235e68bb9ba0552cc4de0ea67c8355c160840b3205429802ea1569f0400')

sanitize() {
	unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
	export PERL_MM_USE_DEFAULT=1
}

build() {
	cd ExtUtils-F77-${pkgver}
	sanitize
	perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd ExtUtils-F77-${pkgver}
	sanitize
	F77LIBS='-lgfortran -lm' make test
}

package() {
	cd ExtUtils-F77-${pkgver}
	sanitize
	make install DESTDIR="$pkgdir"
	find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
