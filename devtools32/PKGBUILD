# $Id$
# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

# repo: releng

pkgname=devtools32
pkgver=20210202
pkgrel=1
pkgdesc='Tools for Arch Linux package maintainers, also for 32 bit'
arch=('any')
license=('GPL')
url='https://git.archlinux32.org/archlinux32/devtools32/'
depends=('namcap' 'openssh' 'subversion' 'rsync' 'arch-install-scripts'
	'pacman-mirrorlist32' 'archlinux32-keyring' 'git' 'mercurial' 'bzr')
makedepends=('asciidoc')
conflicts=('devtools')
source=(
  "https://sources.archlinux32.org/sources/${pkgname}-v${pkgver}.tar.gz"{,.sig}
)
sha512sums=('428fcb112750721be90e8af53e3f963cfc7afab083bad53882b2594f2ff77d77b505d31b9d1cb2df49dccc827b9aab7bbec69d4d3159908257c20280f115ca25'
            'SKIP')
validpgpkeys=('DE9F7688CACF04FEB81A6C590AEEC90755DA7B5A')

build() {
	cd "${pkgname}-v${pkgver}"
	make PREFIX=/usr
}

package() {
	cd "${pkgname}-v${pkgver}"
	make PREFIX=/usr DESTDIR=${pkgdir} install
}

