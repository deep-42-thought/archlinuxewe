# $Id$
# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

# repo: releng

pkgname=devtools32
pkgver=20190410
pkgrel=1
pkgdesc='Tools for Arch Linux package maintainers, also for 32 bit'
arch=('any')
license=('GPL')
url='https://git.archlinux32.org/archlinux32/devtools32/'
depends=('namcap' 'openssh' 'subversion' 'rsync' 'arch-install-scripts'
	'pacman-mirrorlist32' 'archlinux32-keyring' 'git' 'mercurial' 'bzr')
makedepends=('asciidoc')
conflicts=('devtools')
source=(
  "https://sources.archlinux32.org/sources/${pkgname}-v${pkgver}.tar.gz"{,.sig}
)
sha512sums=('b2889c790ef31fe0673dd174f40d1cec348c1e5c59793f50db2315f728dc9d811c0c5d517a1bee3c3590a0dba5e89209d85013abe95379f2cd1b75cbcc0ab330'
            'SKIP')
validpgpkeys=('DE9F7688CACF04FEB81A6C590AEEC90755DA7B5A')

build() {
	cd "${pkgname}-v${pkgver}"
	make PREFIX=/usr
}

package() {
	cd "${pkgname}-v${pkgver}"
	make PREFIX=/usr DESTDIR=${pkgdir} install
}

