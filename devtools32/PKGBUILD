# $Id$
# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

# repo: releng

pkgname=devtools32
pkgver=20180528
pkgrel=1
pkgdesc='Tools for Arch Linux package maintainers, also for 32 bit'
arch=('any')
license=('GPL')
url='https://github.com/archlinux32/devtools32/'
depends=('namcap' 'openssh' 'subversion' 'rsync' 'arch-install-scripts'
	'pacman-mirrorlist32' 'archlinux32-keyring' 'git' 'mercurial' 'bzr')
conflicts=('devtools')
source=(
  "https://github.com/archlinux32/$pkgname/releases/download/v$pkgver/$pkgname-v$pkgver.tar.xz"{,.sig}
)
sha512sums=('b1b8b7fe2e4d1e33a39b011137c4b8350e45d99f1d665cd7d4202b44c4d16ae5d74f291c2a981f67223cfbdeada5825c1848f7b3d20796df2c652afc04211887'
            'SKIP')
validpgpkeys=(
  '5FDCA472AB93292BC678FD59255A76DB9A12601A'
  'DE9F7688CACF04FEB81A6C590AEEC90755DA7B5A'
)

build() {
	cd "${pkgname}-v${pkgver}"
	make PREFIX=/usr
}

package() {
	cd "${pkgname}-v${pkgver}"
	make PREFIX=/usr DESTDIR=${pkgdir} install
}

