# $Id$
# Contributor: Dan McGee <dan@archlinux.org>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=pacman-mirrorlist-ewe
pkgver=20200411
provides=("pacman-mirrorlist=${pkgver}")
conflicts=('pacman-mirrorlist')
pkgrel=1
pkgdesc="Arch Linux mirror list for use by pacman - ewe's favourites"
arch=('any')
url="https://www.archlinux.org/mirrorlist/"
license=('GPL')
backup=(etc/pacman.d/mirrorlist)
source=("https://arch.eckner.net/merged/core/os/x86_64/${pkgname%-ewe}-${pkgver}-${pkgrel%%.*}-any.pkg.tar.zst")
sha512sums=('e114beba9f9a3443149215ea11bb93e5cfbc027e35aacbfa3645de2fadf9d64f18064eca42fa2e70506b8b82af4acdc35dd54c70948cb113e1856d211577270f')

build() {
  sed -i '
    0,/^$/ {
      /^$/ i Server = https://arch.eckner.net/merged/$repo/os/$arch
    }
  ' 'etc/pacman.d/mirrorlist'
}

package() {
  mkdir -p "$pkgdir/etc/pacman.d"
  install -m644 'etc/pacman.d/mirrorlist' "$pkgdir/etc/pacman.d/"
}
