# $Id$
# Contributor: Dan McGee <dan@archlinux.org>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=pacman-mirrorlist-ewe
pkgver=20181205
provides=("pacman-mirrorlist=${pkgver}")
conflicts=('pacman-mirrorlist')
pkgrel=1
pkgdesc="Arch Linux mirror list for use by pacman - ewe's favourites"
arch=('any')
url="https://www.archlinux.org/mirrorlist/"
license=('GPL')
backup=(etc/pacman.d/mirrorlist)
source=("https://arch.eckner.net/merged/core/os/$(uname -m)/${pkgname%-ewe}-${pkgver}-${pkgrel%%.*}-any.pkg.tar.xz")
sha512sums=('0fb92014acf22b2a433fb8d2032b3c43bfb2861c068be27c206c256230e5486f9d21259d54032ffabde768edfd17d7427ace1d26f4eb861e5b35bb4f401423fb')

build() {
  sed -i '
    0,/^$/ {
      /^$/ i Server = https://arch.eckner.net/merged/$repo/os/$arch
    }
  ' 'etc/pacman.d/mirrorlist'
}

package() {
  mkdir -p "$pkgdir/etc/pacman.d"
  install -m644 'etc/pacman.d/mirrorlist' "$pkgdir/etc/pacman.d/"
}
