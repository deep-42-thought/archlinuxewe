# $Id$
# Contributor: Dan McGee <dan@archlinux.org>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=pacman-mirrorlist-ewe
pkgver=20200606
provides=("pacman-mirrorlist=${pkgver}")
conflicts=('pacman-mirrorlist')
pkgrel=1
pkgdesc="Arch Linux mirror list for use by pacman - ewe's favourites"
arch=('any')
url="https://www.archlinux.org/mirrorlist/"
license=('GPL')
backup=(etc/pacman.d/mirrorlist)
source=("https://arch.eckner.net/merged/core/os/x86_64/${pkgname%-ewe}-${pkgver}-${pkgrel%%.*}-any.pkg.tar.zst")
sha512sums=('912dc6a1d508ceaec4b41da40f0446c17c94023c094d8d187d35ac0d7ca2cc44c4afec6a9b5ea5dfa77ccd85645f1614b73486b15125ba74f7788c92c7f2730b')

build() {
  sed -i '
    0,/^$/ {
      /^$/ i Server = https://arch.eckner.net/merged/$repo/os/$arch
    }
  ' 'etc/pacman.d/mirrorlist'
}

package() {
  mkdir -p "$pkgdir/etc/pacman.d"
  install -m644 'etc/pacman.d/mirrorlist' "$pkgdir/etc/pacman.d/"
}
