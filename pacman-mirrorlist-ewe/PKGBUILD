# $Id$
# Contributor: Dan McGee <dan@archlinux.org>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=pacman-mirrorlist-ewe
pkgver=20180710
provides=("pacman-mirrorlist=${pkgver}")
conflicts=('pacman-mirrorlist')
pkgrel=1
pkgdesc="Arch Linux mirror list for use by pacman - ewe's favourites"
arch=('any')
url="https://www.archlinux.org/mirrorlist/"
license=('GPL')
backup=(etc/pacman.d/mirrorlist)
source=("https://arch.eckner.net/merged/core/os/$(uname -m)/${pkgname%-ewe}-${pkgver}-${pkgrel%%.*}-any.pkg.tar.xz")
sha512sums=('08ae8f57247999d779abe8540e654fbd3203c2cef2a35ef89c27da7cb5ba368f5c8e502c0d5102453ee863ba28ad3d80bd938ccdd07a27bb8eb47497c9bff536')

build() {
  sed -i '
    0,/^$/ {
      /^$/ i Server = https://arch.eckner.net/merged/$repo/os/$arch
    }
  ' 'etc/pacman.d/mirrorlist'
}

package() {
  mkdir -p "$pkgdir/etc/pacman.d"
  install -m644 'etc/pacman.d/mirrorlist' "$pkgdir/etc/pacman.d/"
}
