# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-astro-fits-cfitsio'
pkgver=1.11
pkgrel='5'
pkgdesc='Perl extension for using the cfitsio library'
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl>=5.28'
  'perl<5.29'
  'cfitsio')
makedepends=(
  'perl-extutils-makemaker')
url='https://metacpan.org/pod/Astro::FITS::CFITSIO'
_distdir="Astro-FITS-CFITSIO-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/P/PR/PRATZLAFF/${_distdir}.tar.gz")
sha512sums=('e823831c51f20ea775b66c0fa3855451ca0b9de984968daa07e365b86151578a4424089d21c5b7528c15c1eb49e9517935f266d60c1b074ce7d9d02265fc20a5')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
