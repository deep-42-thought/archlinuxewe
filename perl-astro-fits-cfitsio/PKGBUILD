# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-astro-fits-cfitsio'
pkgver=1.14
pkgrel=2
pkgdesc='Perl extension for using the cfitsio library'
arch=('i686' 'pentium4' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
_pinned_dependencies=(
  'cfitsio=1:3.49'
  'glibc>=2.31'
)
makedepends=(
  'perl-extutils-makemaker')
depends=(
  'cfitsio'
  'cfitsio=1:3.48'
  'glibc>=2.31')
url='https://metacpan.org/pod/Astro::FITS::CFITSIO'
_distdir="Astro-FITS-CFITSIO-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/P/PR/PRATZLAFF/${_distdir}.tar.gz")
sha512sums=('32e708ed03bd5263fc53b610a799103ef733047cee41c1b26a79a5978862668faaaeff96b330cf195d85a46b241c6b20631678625dc9ed43e85f823a5c35d397')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
