# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-astro-fits-cfitsio'
pkgver=1.12
pkgrel=1
pkgdesc='Perl extension for using the cfitsio library'
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'cfitsio')
makedepends=(
  'perl-extutils-makemaker')
url='https://metacpan.org/pod/Astro::FITS::CFITSIO'
_distdir="Astro-FITS-CFITSIO-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/P/PR/PRATZLAFF/${_distdir}.tar.gz")
sha512sums=('cd10ee29da5a3676600c56ed9abba2b5bcdba64ea12c9331df58f37f166a129a3dc17cc5298351bdafc1dab811bc9844e71e5afb90e2a2f0c087d0844ca10810')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
