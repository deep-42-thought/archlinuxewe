# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Mirko Scholz
# modeled after qwtplot3d
# Contributor: Ronald van Haren <ronald@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=qwtplot3d-qt5
pkgver=0.2.7
pkgrel=3
pkgdesc="Qt/OpenGL-based C++ programming library containing 3d-widgets"
arch=('i686' 'pentium4' 'x86_64')
license=('custom:zlib')
url="http://qwtplot3d.sourceforge.net/"
depends=('qt5-base')
makedepends=('mesa' 'glu')
source=("https://downloads.sourceforge.net/sourceforge/qwtplot3d/qwtplot3d-$pkgver.tgz"
        qwtplot3d-gcc44.patch 
        qwtplot3d-qt-4.8.0.patch
        qwtplot3d-qt5.patch)
sha512sums=('88e6d270695931775f3c1c0e718118e31118dbfe9f5f582834de09ab515fdf9e8a0f90d424f276653fdf1cf0b39e2060282385701a4ab48843420fe55a1dcf0c'
            '4f10bfabc68ec313aa8f3f624632c59bfad18f701bc802cd222607ac551b2ad6736dca14e3b63bd238c9c2b68128cb0b66eb1801dc6efebc800e71b0d03cbd31'
            '7cb30ea968e491b0bdfd6a27ce6a47dfbf9888048a4368b14947988e29f4041814d9f452960851d959177a5ced58fda74f4cfdac1f178bb6b44fb9ccc98d53c7'
            '7cd4def315f238acb61f8416c06012c16967985bda8762fd6aff512b1690ae3d2c29fb5fc35c61bd1c95df99344f245fd1cff12542a87dfa9baaf7a80c58f67b')

prepare() {
    cd qwtplot3d
    patch -p1 -i "${srcdir}"/qwtplot3d-gcc44.patch
    patch -p1 -i "${srcdir}"/qwtplot3d-qt-4.8.0.patch
    patch -p1 -i "${srcdir}"/qwtplot3d-qt5.patch
}

build() {
    cd qwtplot3d
    # build qwt:
    qmake-qt5 qwtplot3d.pro "CONFIG+=staticlib"
    make
}

package() {
    cd qwtplot3d

    # install qwtplot3d: (by hand, because the Makefile do not provide a "install:")
    install -d "${pkgdir}"/usr/{include/qt/qwtplot3d-qt5,lib}

    for n in include/* ; do
     install -Dm644 $n "${pkgdir}"/usr/include/qt/qwtplot3d-qt5
    done

#   for n in lib/libqwtplot3d-qt5.so* ; do
#    cp -d $n "${pkgdir}"/usr/lib
#   done
    install -Dm644 lib/libqwtplot3d-qt5.a "${pkgdir}"/usr/lib

    # install custom license
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

