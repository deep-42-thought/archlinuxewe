# Maintainer:  Erich Eckner <arch at eckner dot net>
# Contributor: Piotr Rogoza <piotr dot r dot public at gmail dot com>

pkgname=perl-wx
pkgver=0.9932
_author=M/MD/MDOOTSON
_perlmod=Wx
pkgrel=16
_distdir="${_perlmod}-${pkgver}"
pkgdesc="Wx - interface to the wxWidgets GUI toolkit"
arch=('i686' 'pentium4' 'x86_64')
url="http://search.cpan.org/dist/Wx"
license=('GPL' 'PerlArtistic')
_pinned_dependencies=(
  'gcc-libs=12.1.0'
  'glibc>=2.31'
  'wxgtk3=3.0.5.1'
  'wxgtk-common=3.0.5.1'
)
depends=(
  'perl>=5.36'
  'perl<5.37'
  "${_pinned_dependencies[@]}"
  'perl-alien-wxwidgets>=0.25')
makedepends=(
  'perl-extutils-makemaker>=6.48'
  'perl-extutils-parsexs>=3.15'
  'perl-extutils-xspp>=0.16.02'
  'perl-if>=0.03'
  'perl-pathtools>=0.82'
  'perl-test-harness>=2.26'
  'perl-test-simple>=0.45'
  'xorg-server-xvfb')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/$_author/${_distdir}.tar.gz")
sha512sums=('49f9a32bcd65ad4f2017f322e45ddaad741cdc0f0245489425771dda917553c354945fa949521c18730d27e78444a79d0d56374d462bcd92a96c0e76a4471c0e')

build(){
  cd "${srcdir}/${_distdir}"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
}

check(){
  cd "${srcdir}/${_distdir}"
  if [[ -z "$DISPLAY" || -x /usr/bin/xvfb-run ]]; then
#     warning 'Empty $DISPLAY - falling back to xvfb-run (xorg-server-xvfb)'
    xvfb-run -a -s "+extension GLX -screen 0 1280x1024x24" make test
  else
    make test
  fi
}

package(){
  cd "${srcdir}/${_distdir}"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
