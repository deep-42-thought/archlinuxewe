# Maintainer:  Erich Eckner <arch at eckner dot net>
# Contributor: Piotr Rogoza <piotr dot r dot public at gmail dot com>

pkgname=perl-wx
pkgver=0.9931
_author=M/MD/MDOOTSON
_perlmod=Wx
pkgrel=1
_distdir="${_perlmod}-${pkgver}"
pkgdesc="Wx - interface to the wxWidgets GUI toolkit"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/Wx"
license=('GPL' 'PerlArtistic')
depends=(
  'perl-alien-wxwidgets>=0.25'
  'wxgtk')
makedepends=(
  'perl-extutils-makemaker>=6.48'
  'perl-extutils-parsexs>=3.15'
  'perl-extutils-xspp>=0.16.02'
  'perl-if>=0.03'
  'perl-pathtools>=0.82'
  'perl-test-harness>=2.26'
  'perl-test-simple>=0.45'
  'webkitgtk2'
  'xorg-server-xvfb')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/$_author/${_distdir}.tar.gz")
sha512sums=('3edb2e5009a68cd95f4ef02e75d80a0976b46d188db6801d62cf8385ab5770e0cbe8fa6d8f49829093abbb650b101fb9bdf537f5f1c4ded0b494a0393c81266c')

build(){
  cd "${srcdir}/${_distdir}"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
}

check(){
  cd "${srcdir}/${_distdir}"
  if [[ -z "$DISPLAY" || -x /usr/bin/xvfb-run ]]; then
#     warning 'Empty $DISPLAY - falling back to xvfb-run (xorg-server-xvfb)'
    xvfb-run -a -s "+extension GLX -screen 0 1280x1024x24" make test
  else
    make test
  fi
}

package(){
  cd "${srcdir}/${_distdir}"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
