# -*- shell-script -*-
#
# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Adrian C. <anrxc..sysphere.org>

pkgname=alpine
pkgver=2.21.999.r71.08fcd1b
_commit=${pkgver##*.}
pkgrel='3'
arch=("i686" "x86_64")
pkgdesc="Apache licensed PINE mail user agent"
url="http://alpine.freeiz.com"
license=("APACHE")
depends=("libldap" "krb5" "gettext")
makedepends=('openssl' 'git')
optdepends=("aspell: for spell-checking support"
            "hunspell: for spell-checking support"
            "topal: glue program that links GnuPG and alpine")
provides=("pine")
conflicts=("pine" "re-alpine")
replaces=("pine")
options=("!makeflags")
source=(
  "alpine::git+https://git.eckner.net/Erich/alpine#commit=${_commit}"
  'topal-patch.patch'
)
sha512sums=('SKIP'
            'bc7a3143cb2f7d79438c49b89bfdc52db7473521a7870ad621ecba96fff2f1688f4f3d58def9b8d57f8beba4e1e9c8b193f9281571e0564538ca7c7db16f0027')

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -p1 -i "${srcdir}/topal-patch.patch"
}

pkgver() {
  cd "${srcdir}/${pkgname}"
  _rev=$(
    git rev-parse --short master
  )
  _pkgver=$(
    git archive "${_rev}" -- Makefile | \
      tar -Ox | \
      sed -n '
        /^VERSION\s*=/!d
        =
        s/^.*=\s*//
        p
      ' | \
      sed '
        N
        s/\n/ /
      '
  )
  _line=${_pkgver% *}
  _pkgver=${_pkgver#* }
  _rev_count=$(
    git rev-list "${_rev}" ^$(git blame -L${_line},${_line} "${_rev}" -- Makefile | cut -d' ' -f1) --count
  )
  printf '%s.r%s.%s' \
    "${_pkgver}" \
    "${_rev_count}" \
    "${_rev}"
}

build() {
  cd "${srcdir}/${pkgname}"

# Configure Alpine
  ./configure --prefix=/usr \
  --with-passfile=.pine-passfile \
  --without-tcl --disable-shared \
  --with-system-pinerc=/etc/${pkgname}.d/pine.conf \
  --with-system-fixed-pinerc=/etc/${pkgname}.d/pine.conf.fixed

# Build Alpine
  make
}

package() {
  cd "${srcdir}/${pkgname}"

# Install Alpine
  make DESTDIR="${pkgdir}" install
}
