# -*- shell-script -*-
#
# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Adrian C. <anrxc..sysphere.org>

pkgname=alpine
pkgver=2.21
pkgrel=1
arch=("i686" "x86_64")
pkgdesc="Apache licensed PINE mail user agent"
url="http://alpine.freeiz.com"
license=("APACHE")
depends=("libldap" "krb5" "gettext")
optdepends=("aspell: for spell-checking support"
	    "hunspell: for spell-checking support"
            "topal: glue program that links GnuPG and alpine")
provides=("pine")
conflicts=("pine" "re-alpine")
replaces=("pine")
options=("!makeflags")
source=("http://alpine.freeiz.com/${pkgname}/release/src/${pkgname}-${pkgver}.tar.xz"
        "http://alpine.freeiz.com/${pkgname}/release/src/sig/${pkgname}-${pkgver}.tar.xz.sig"
        "http://alpine.freeiz.com/${pkgname}/patches/${pkgname}-${pkgver}/all.patch.gz")
sha512sums=('a2a36a033c8af79810816a7da7185c269808ba6d84d013691fd8b3764c63f5fb2284e6844ec5a5e99d168514ae636debf59fae962533a2916679e4e9109c6264'
            'SKIP'
            'f03ce32551e0e9bc83381cf752bf0fa271d51bfc9bb091019e962b4fa77d0d0c20407a1964dca3adb92e74af98a07198a3959828eb1bb2eb7af2628c5562bd82')
validpgpkeys=('DA67606281D7D25A91BCF88BA361CBF86AA14BD5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

# User compile time patches
# - Eduardo Chappa patches
  patch -p1 < ../all.patch
# - Phil Brooke patches (optional topal support)
  #patch -p1 < ../topal-1.patch
  #patch -p1 < ../topal-2.patch


# Configure Alpine
  LIBS+="-lpam -lkrb5 -lcrypto" ./configure --prefix=/usr \
  --without-passfile --without-tcl --disable-shared \
  --with-system-pinerc=/etc/${pkgname}.d/pine.conf \
  --with-system-fixed-pinerc=/etc/${pkgname}.d/pine.conf.fixed

# Build Alpine
  make
}


package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

# Install Alpine
  make DESTDIR="${pkgdir}" install
}
