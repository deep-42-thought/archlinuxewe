# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=python2-courier-pythonfilter
_pkgname='courier-pythonfilter'
pkgver=3.0
pkgrel=1
pkgdesc='collection of useful filters for the Courier MTA, and a framework for developing new filters in Python'
arch=('any')
url='https://bitbucket.org/gordonmessmer/courier-pythonfilter'
license=('GPL')
depends=(
  'python2>=2.7'
  'python2<2.8'
)
makedepends=(
  'python2-setuptools')
source=("https://bitbucket.org/gordonmessmer/${_pkgname}/get/release-${pkgver//./-}.tar.gz")
sha512sums=('fbfa641045237c9a5e6460e83f9d6670f560a0a697fa35ab24aadafb59926a3ecd320252910e15491857ff7d7e13da018253fa4e1b71c93b86942ac6bb457265')
backup=('etc/pythonfilter-modules.conf' 'etc/pythonfilter.conf')
optdepends=(
  'python2-pyclamav:  Virus Scan'
  'python2-pydns:     dialback'
  'python2-spf:       spfcheck and whitelist_spf'
  'spamassassin:      Spam Detection'
)

build() {
  cd "${srcdir}/gordonmessmer-${_pkgname}-"*
  python2 setup.py build
}

package() {
  depends+=('courier-mta')
  cd "${srcdir}/gordonmessmer-${_pkgname}-"*
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  find "${pkgdir}" -type f -exec sed -i ' 1 s,^#!/usr/bin/python$,\02,' {} \;
  rm "${pkgdir}/usr/lib/python2"*"/site-packages/${_pkgname//-/_}-${pkgver}-py2"*".egg-info"
}

