# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: gucong <gucong43216@gmail.com>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=embree-isa
pkgver=3.13.3
pkgrel=3
pkgdesc="A collection of high-performance ray tracing kernels (with build-time ISA detection)"
arch=('x86_64')
url="https://embree.github.io/"
license=('Apache')
_pinned_dependencies=(
  'gcc-libs=12.1.0'
  'glibc>=2.31'
  'tbb=2021.5.0'
)
depends=("${_pinned_dependencies[@]}")
provides=('embree')
conflicts=('embree')
makedepends=('cmake' 'ispc' 'freeglut' 'libxmu' 'openexr')
source=("embree-${pkgver}.tar.gz::https://github.com/embree/embree/archive/v${pkgver}.tar.gz")
sha512sums=('eef8d9101f0bf95d6706a495a9aa628c10749862aeb2baa6bba2f82fcc3a96467a28ca1f522d672eb5aa7b29824363674feda25832724da361b3334334a218cd')

build() {
  cd "$srcdir/embree-$pkgver"

  MAX_ISA="SSE2"
  cat /proc/cpuinfo | grep sse3 > /dev/null && MAX_ISA="SSE3"
  cat /proc/cpuinfo | grep sse4_1 > /dev/null && MAX_ISA="SSE4.1"
  cat /proc/cpuinfo | grep sse4_2 > /dev/null && MAX_ISA="SSE4.2"
  cat /proc/cpuinfo | grep avx > /dev/null && MAX_ISA="AVX"
  cat /proc/cpuinfo | grep avx2 > /dev/null && MAX_ISA="AVX2"
  # ICC required for avx512 ?

  echo MAX_ISA:  $MAX_ISA

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DEMBREE_TUTORIALS=OFF \
    -DEMBREE_MAX_ISA="$MAX_ISA"
  make
}

package() {
  cd "$srcdir/embree-$pkgver"
  make DESTDIR="$pkgdir" install
}

