# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=perl-convert-uu
_pkgname='Convert-UU'
pkgver=0.5201
pkgrel=11
pkgdesc="Perl module for uuencode and uudecode"
arch=('any')
url="http://search.cpan.org/~andk/Convert-UU-0.5201/"
license=('GPL' 'PerlArtistic')
depends=(
  'perl>=5.34'
  'perl<5.35'
)
makedepends=()
checkdepends=('perl-test-pod>=1.00' 'perl-test-pod-coverage')
options=('!emptydirs')
_distdir="${_pkgname}-${pkgver}"
source=("http://search.cpan.org/CPAN/authors/id/A/AN/ANDK/${_distdir}.tar.gz")
sha256sums=('92329ce1c32b5952c48e1223db018c8c58ceafef03bfa0fd4817cd89c355a3bd')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                   \
      PERL_AUTOINSTALL=--skipdeps                              \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='${pkgdir}'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '${pkgdir}'" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_distdir}"
    perl Makefile.PL
    make
  )
}

check() {
  cd "${srcdir}/${_distdir}"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "${srcdir}/${_distdir}"
  make install
  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
