# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=perl-pdl
_pkgname='PDL'
pkgver=2.019
pkgrel=19
_distdir="${_pkgname}-${pkgver}"
pkgdesc="the Perl Data Language"
arch=('x86_64' 'i686' 'pentium4')
url="http://pdl.perl.org/"
license=('GPL' 'PerlArtistic')
_pinned_dependencies=(
  'glibc>=2.31'
  'libglvnd=1.4.0'
)
depends=(
  'perl>=5.36'
  'perl<5.37'
  "${_pinned_dependencies[@]}"
  'hdf4'
  'perl-astro-fits-header'
  'perl-carp'
  'perl-convert-uu'
  'perl-data-dumper>=2.121'
  'perl-extutils-makemaker>=6.56'
  'perl-extutils-manifest'
  'perl-file-map>=0.57'
  'perl-file-path'
  'perl-file-temp'
  'perl-filter-simple>=0.88'
  'perl-filter-util-call'
  'perl-inline>=0.68'
  'perl-inline-c>=0.62'
  'perl-list-moreutils'
  'perl-module-compile>=0.23'
  'perl-opengl>=0.67.02'
  'perl-pathtools>=0.6'
  'perl-pod-parser'
  'perl-scalar-list-utils'
  'perl-storable>=1.03'
  'perl-text-balanced>=1.89')
makedepends=(
  'perl-carp>=1.2'
  'perl-cpan-meta>=2.120900'
  'perl-devel-checklib>=1.01'
  'perl-extutils-parsexs>=3.01'
  'perl-ipc-cmd>=0.72'
  'perl-test-deep'
  'perl-test-exception'
  'perl-test-warn')
provides=(
  "perl-pdl-core=${pkgver}")
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/C/CH/CHM/${_distdir}.tar.gz")
sha512sums=('9a02a1ed5857bae0e9f6bca81bced291eea081c5ba57884fd19ca0d9117ac5aa45616b94e56c20e937a913b685e9325413ebfbefcadcb4488356b605a84983b2')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_distdir}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "${srcdir}/${_distdir}"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "${srcdir}/${_distdir}"
  make pure_install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
