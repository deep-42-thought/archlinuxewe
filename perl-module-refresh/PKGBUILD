# CPAN Name  : Module::Refresh
# Contributor: Daenyth
# Contributor: Anton Leontiev <bunder /at/ t-25.ru>
# Generator  : CPANPLUS::Dist::Arch 1.25
# Maintainer : Erich Eckner <arch at eckner dot net>

pkgname=perl-module-refresh
pkgver=0.17
pkgrel=10
_distdir="Module-Refresh-${pkgver}"
pkgdesc="Perl module to refresh %INC files when updated on disk"
arch=("any")
license=("GPL" "PerlArtistic")
url="http://search.cpan.org/dist/Module-Refresh"
options=(!emptydirs)
depends=(
  'perl>=5.34'
  'perl<5.35'
  'perl>=5.8.1')
makedepends=(
  'perl-extutils-makemaker>=6.42'
  'perl-file-temp>=0.19'
  'perl-path-class'
  'perl-test-simple'
  'perl-module-install')
checkdepends=("perl-path-class") # 'perl' provides 'perl-test-simple' and 'perl-file-temp'
source=("http://search.cpan.org/CPAN/authors/id/A/AL/ALEXMV/${_distdir}.tar.gz")
sha512sums=('48bbcf14d5c6f13217b41c9a4683fd5ff492d3d33f8d6d50e89eae8a6336f306358e0070ecf1617d68c6eb9f91caaec6e1048e09b741d3fa0a19f6d38a75f17f')

build() {
  cd "${srcdir}/${_distdir}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_distdir}"
  make test
}

package() {
  cd "${srcdir}/${_distdir}"
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
