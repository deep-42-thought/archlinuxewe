# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: 3ED <krzysztof1987 at gmail dot com>

pkgname=perl-class-load-xs
_lastauthor=E/ET/ETHER
_pkgname=Class-Load-XS
pkgver=0.09
pkgrel='5'
pkgdesc="XS implementation of parts of Class::Load"
arch=('x86_64' 'i686')
license=('PerlArtistic2')
options=('!emptydirs')
# https://bugs.archlinux.org/task/43204
# perl-class-load should depend on perl-namespace-clean
depends=(
  'perl>=5.6.0'
  'perl-class-load>=0.20'
  'perl-xsloader')
makedepends=(
  'perl-constant'
  'perl-extutils-makemaker'
  'perl-lib'
  'perl-module-implementation>=0.04'
  'perl-pathtools'
  'perl-test-fatal'
  'perl-test-requires'
  'perl-test-simple>=0.88'
  'perl-version')
checkdepends=('perl-module-implementation' 'perl-test-fatal' 'perl-test-requires' 'perl-test-without-module')
url="https://metacpan.org/release/${_pkgname}"
_distdir="${_pkgname}-${pkgver}"
source=(https://cpan.metacpan.org/authors/id/${_lastauthor}/${_distdir}.tar.gz)
sha512sums=('9b24637cf7b035ba369f05e42c0bd5a8590e001f2395b46df078635a50bb3df58ad57850be0010d092ae2530d9a545bbcc5f0055f6ae7f166c93905c022d469d')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/${_distdir}"
  perl Makefile.PL
  make
}
check() {
  cd "${srcdir}/${_distdir}"
  make test
}
package() {
  cd "${srcdir}/${_distdir}"
  make install
}
