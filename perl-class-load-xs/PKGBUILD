# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: 3ED <krzysztof1987 at gmail dot com>

pkgname=perl-class-load-xs
_lastauthor=E/ET/ETHER
_pkgname=Class-Load-XS
pkgver=0.10
pkgrel='3'
pkgdesc="XS implementation of parts of Class::Load"
arch=('x86_64' 'i686')
license=('PerlArtistic2')
options=('!emptydirs')
# https://bugs.archlinux.org/task/43204
# perl-class-load should depend on perl-namespace-clean
depends=(
  'perl>=5.6.0'
  'perl-class-load>=0.20'
  'perl-xsloader')
makedepends=(
  'perl-constant'
  'perl-extutils-makemaker'
  'perl-lib'
  'perl-module-implementation>=0.04'
  'perl-pathtools'
  'perl-test-fatal'
  'perl-test-needs'
  'perl-test-simple>=0.88'
  'perl-version')
checkdepends=('perl-module-implementation' 'perl-test-fatal' 'perl-test-requires' 'perl-test-without-module')
url="https://metacpan.org/release/${_pkgname}"
_distdir="${_pkgname}-${pkgver}"
source=(https://cpan.metacpan.org/authors/id/${_lastauthor}/${_distdir}.tar.gz)
sha512sums=('16ab845dc393567f0b0e9597eda7d7678b8a41d218823a166bacdfd4dc93e704a6aff98662340b8d7f4d045a60530071bb84ea8da8be5a6930af47722207d126')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/${_distdir}"
  perl Makefile.PL
  make
}
check() {
  cd "${srcdir}/${_distdir}"
  make test
}
package() {
  cd "${srcdir}/${_distdir}"
  make install
}
