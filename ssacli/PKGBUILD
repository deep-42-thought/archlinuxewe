# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=ssacli
pkgver=3.40_3.0
pkgrel=3
pkgdesc="HP Command Line Smart Storage Administrator"
arch=(x86_64)
url="https://downloads.linux.hpe.com/SDR/downloads/mcp"
depends=(bash)
replaces=(hpacucli hpssacli)
groups=(hpproliant)
license=("CUSTOM")

source_x86_64=("https://downloads.linux.hpe.com/SDR/repo/mcp/centos/7/${CARCH}/current/$pkgname-${pkgver//_/-}.${CARCH}.rpm")
source=('ssacli.conf')
sha512sums=('32cf56d9d40bb537273d27ad73afe1af30c283cd13bd4bc57df96ebd6538e8f8d55d352895e40e33a35c16c245da46071707fcf3d676387d72f20a22e9ecf50d')
sha512sums_x86_64=('372a08098cb4769455e0ced7ebe3e25589353f2feb689fba6234272b79c21fac7e419d559279dedca1e18b7fe0250efb624b0e1b7a8a8ea4720a67701b507d3d')

package() {
  mv usr/{sbin,bin}
  mv usr/{,share/}man
  install -Dm644 opt/smartstorageadmin/ssacli/bin/ssacli.license "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  install -Dm644 "$srcdir/ssacli.conf" "$pkgdir/usr/lib/modules-load.d/ssacli.conf"
  rm opt/smartstorageadmin/ssacli/bin/ssacli.license
  cp -a opt usr "$pkgdir"
}
