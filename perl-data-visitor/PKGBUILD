# CPAN Name  : Data-Visitor
# Submitter  : aCiD2
# Contributor: SIGTERM
# Maintainer:  Erich Eckner <arch at eckner dot net>

pkgname='perl-data-visitor'
pkgver='0.30'
pkgrel=8
pkgdesc="Visitor style traversal of Perl data structures"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl>=5.34'
  'perl<5.35'
  'perl-class-load>=0.06'
  'perl-moose>=0.89'
  'perl-namespace-clean>=0.19'
  'perl-task-weaken'
  'perl-tie-toobject>=0.01')
makedepends=(
  'perl-extutils-makemaker>=6.30'
  'perl-test-requires'
  'perl-test-simple>=0.88')
checkdepends=('perl-data-alias')
url='http://search.cpan.org/dist/Data-Visitor'
_distdir="Data-Visitor-${pkgver}"
source=("http://search.cpan.org/CPAN/authors/id/D/DO/DOY/${_distdir}.tar.gz")
sha512sums=('cdd3f5076dbbfb3afce714677e936417b96315dd50735c015795fb2c9d143d4e2ceeb305366f9985032b17899b911267dbafefa56493ff20f16c74da1a9330a8')

build() {
  cd "$srcdir/${_distdir}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/${_distdir}"
  PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "$srcdir/${_distdir}"
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -o -name '*.pod' -delete
}
