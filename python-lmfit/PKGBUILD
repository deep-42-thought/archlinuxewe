# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='python-lmfit'
_pkgname=lmfit-py
pkgver=1.0.1
pkgrel=3
pkgdesc="Least-Squares Minimization with Constraints for Python"
arch=('any')
url="http://lmfit.github.io/lmfit-py/"
license=('BSD')
depends=(
  'python>=3.9'
  'python<3.10'
  'python-asteval>=0.9.16'
  'python-numpy>=1.16'
  'python-scipy>=1.2'
  'python-uncertainties>=3.0.1')
makedepends=(
  'python-setuptools')
checkdepends=(
  'python-nose')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/lmfit/lmfit-py/archive/${pkgver}.tar.gz")
sha512sums=('80ae4464c3c27cb559d1227c9e71f94d33463559adf777c3121ee47d2f5e6fd8f6f5c7c665e632253902f271f5ca967e232bbb1a3973fadb5452a6959cc87aec')

build() {
  cd "$srcdir/lmfit-py-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/lmfit-py-$pkgver"
  nosetests3 tests || echo 'WARNING: python3 tests failed'
}

package(){
  cd "$srcdir/lmfit-py-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}
