# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgbase=python-lmfit
_pkgname=lmfit-py
pkgname=('python-lmfit' 'python2-lmfit')
pkgver=0.9.14
pkgrel=1
pkgdesc="Least-Squares Minimization with Constraints for Python"
arch=('any')
url="http://lmfit.github.io/lmfit-py/"
license=('BSD')
_deppy2=(
  'python2>=2.7'
  'python2<2.8'
)
_deppy=(
  'python>=3.7'
  'python<3.8'
)
_depends=(
  'python-asteval>=0.9.12'
  'python-numpy>=1.10'
  'python-scipy>=0.19'
  'python-six>1.10'
  'python-uncertainties>=3.0')
_makedepends=(
  'python-setuptools')
_checkdepends=(
  'python-nose')
makedepends=(
  "${_deppy[@]}"
  "${_deppy2[@]}"
  "${_depends[@]}"
  "${_depends[@]/python/python2}"
  "${_makedepends[@]}"
  "${_makedepends[@]/python/python2}"
)
checkdepends=(
  "${_checkdepends[@]}"
  "${_checkdepends[@]/python/python2}"
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/lmfit/lmfit-py/archive/${pkgver}.tar.gz")
sha512sums=('fed620bd8cfa736a43e3456154e893138aa3f75def1f06a66110900c7f38f15c953ad5682cbf5cd1b436594ddd59a3eef9851aa4a53948d66ffc36e1b68d14e2')

prepare() {
  cp -a "lmfit-py-${pkgver}" "lmfit-py2-${pkgver}"
}

build() {
  cd "$srcdir/lmfit-py-$pkgver"
  python setup.py build
  
  cd "$srcdir/lmfit-py2-$pkgver"
  python2 setup.py build
}

check() {
  cd "$srcdir/lmfit-py-$pkgver"
  nosetests3 tests || echo 'WARNING: python3 tests failed'

  cd "$srcdir/lmfit-py2-$pkgver"
  nosetests2 tests || echo 'WARNING: python2 tests failed'
}

package_python-lmfit(){
  depends=(
    "${_deppy[@]}"
    "${_depends[@]}"
  )
  cd "$srcdir/lmfit-py-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-lmfit(){
  depends=(
    "${_deppy2[@]}"
    "${_depends[@]/python/python2}"
  )
  cd "$srcdir/lmfit-py2-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:ts=2:sw=2:et:
