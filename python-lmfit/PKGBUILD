# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgbase=python-lmfit
_pkgname=lmfit-py
pkgname=('python-lmfit' 'python2-lmfit')
pkgver=0.9.15
pkgrel=1
pkgdesc="Least-Squares Minimization with Constraints for Python"
arch=('any')
url="http://lmfit.github.io/lmfit-py/"
license=('BSD')
_deppy2=(
  'python2>=2.7'
  'python2<2.8'
)
_deppy=(
  'python>=3.8'
  'python<3.9'
)
_depends=(
  'python-asteval>=0.9.16'
  'python-numpy>=1.16'
  'python-scipy>=1.2'
  'python-six>=1.11'
  'python-uncertainties>=3.0.1')
_makedepends=(
  'python-setuptools')
_checkdepends=(
  'python-nose')
makedepends=(
  "${_deppy[@]}"
  "${_deppy2[@]}"
  "${_depends[@]}"
  "${_depends[@]/python/python2}"
  "${_makedepends[@]}"
  "${_makedepends[@]/python/python2}"
)
checkdepends=(
  "${_checkdepends[@]}"
  "${_checkdepends[@]/python/python2}"
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/lmfit/lmfit-py/archive/${pkgver}.tar.gz")
sha512sums=('336cc32f446188594c7f914044fabf02380ff58a1aa55ce07437f09149be057e8c6d22d286d2514df0a68c75835455570ee0b7729cfeca201477b8835250fc78')

prepare() {
  cp -a "lmfit-py-${pkgver}" "lmfit-py2-${pkgver}"
}

build() {
  cd "$srcdir/lmfit-py-$pkgver"
  python setup.py build
  
  cd "$srcdir/lmfit-py2-$pkgver"
  python2 setup.py build
}

check() {
  cd "$srcdir/lmfit-py-$pkgver"
  nosetests3 tests || echo 'WARNING: python3 tests failed'

  cd "$srcdir/lmfit-py2-$pkgver"
  nosetests2 tests || echo 'WARNING: python2 tests failed'
}

package_python-lmfit(){
  depends=(
    "${_deppy[@]}"
    "${_depends[@]}"
  )
  cd "$srcdir/lmfit-py-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-lmfit(){
  depends=(
    "${_deppy2[@]}"
    "${_depends[@]/python/python2}"
  )
  cd "$srcdir/lmfit-py2-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:ts=2:sw=2:et:
