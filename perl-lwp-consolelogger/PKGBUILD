# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-lwp-consolelogger'
pkgver=0.000044
pkgrel=2
pkgdesc='Easy LWP tracing and debugging'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl>=5.34'
  'perl<5.35'
  'perl-class-method-modifiers'
  'perl-data-printer>=0.36'
  'perl-datetime'
  'perl-html-restrict'
  'perl-http-body'
  'perl-http-cookiemonster'
  'perl-http-message'
  'perl-json-maybexs>=1.003005'
  'perl-libwww'
  'perl-list-allutils'
  'perl-log-dispatch>=2.56'
  'perl-module-load-conditional'
  'perl-module-runtime'
  'perl-moo'
  'perl-moox-strictconstructor'
  'perl-parse-mime'
  'perl-ref-util'
  'perl-string-trim'
  'perl-sub-exporter'
  'perl-term-size-any'
  'perl-text-simpletable-autowidth>=0.09'
  'perl-try-tiny'
  'perl-type-tiny'
  'perl-uri'
  'perl-xml-simple')
makedepends=(
  'perl-capture-tiny'
  'perl-extutils-makemaker'
  'perl-html-formattext-withlinks'
  'perl-http-cookiejar-lwp'
  'perl-log-dispatch-array'
  'perl-path-tiny'
  'perl-pathtools'
  'perl-plack'
  'perl-plack-handler-http-server-simple>=0.016'
  'perl-plack-test-agent'
  'perl-test-fatal'
  'perl-test-lwp-useragent'
  'perl-test-most'
  'perl-test-simple'
  'perl-test-warnings'
  'perl-version'
  'perl-www-mechanize')
optdepends=(
  'perl-html-formattext-lynx')
checkdepends=(
  'perl-datetime'
  'perl-html-formattext-withlinks'
  'perl-path-tiny'
  'perl-log-dispatch-array'
  'perl-test-most'
  'perl-http-server-simple-psgi'
  'perl-plack'
  'perl-plack-test-agent'
  'perl-test-failwarnings'
  'perl-json-maybexs'
  'perl-cgi'
  'perl-file-which'
  'perl-mojolicious')
url='https://metacpan.org/release/LWP-ConsoleLogger'
_distdir="LWP-ConsoleLogger-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/O/OA/OALDERS/${_distdir}.tar.gz")
sha512sums=('c3685cb15fcc30d8bca121db9953adbb1271725cb1bb9a1f3e84f57741027489e2f0ba34ba9a4a71d8d201591c7dea2024574e44421d9660a21db60396a4daeb')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
