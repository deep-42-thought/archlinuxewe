# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-cpanel-json-xs'
pkgver=3.0220
pkgrel=1
pkgdesc='cPanel fork of JSON::XS, fast and correct serializing'
arch=('x86_64' 'i686')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl-podlators>=2.08')
makedepends=(
  'perl-extutils-makemaker')
checkdepends=(
  'perl-json'
  'perl-json-xs'
  'perl-mojolicious'
  'perl-test-leaktrace'
)
url='https://metacpan.org/release/Cpanel-JSON-XS'
_distdir="Cpanel-JSON-XS-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/R/RU/RURBAN/${_distdir}.tar.gz")
sha512sums=('caa1c37db799c25d58cc338a7a79af5679352af920972be3153a0adc83b41d01f6de0625498cc72edcee803af245581e683c79e6c0f07732fe8a4ea34196d3c7')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
