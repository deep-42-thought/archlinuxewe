# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-cpanel-json-xs'
pkgver=4.06
pkgrel='2'
pkgdesc='cPanel fork of JSON::XS, fast and correct serializing'
arch=('x86_64' 'i686')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl-podlators>=2.08')
makedepends=(
  'perl-extutils-makemaker')
checkdepends=(
  'perl-class-xsaccessor'
  'perl-json'
  'perl-json-xs'
  'perl-mojolicious'
  'perl-perl-minimumversion'
  'perl-pod-spell-commonmistakes'
  'perl-test-cpan-meta'
  'perl-test-leaktrace'
  'perl-test-minimumversion'
  'perl-test-pod'
  'perl-test-pod-coverage'
  'perl-test-spelling'
)
url='https://metacpan.org/release/Cpanel-JSON-XS'
_distdir="Cpanel-JSON-XS-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/R/RU/RURBAN/${_distdir}.tar.gz")
sha512sums=('df765265c23982a6c5eb80d16d9c9e3b3ab9f0b34aa130df8da084936aa2b7e9f4b362466948925df7be9c448892eef93017cf1fa7be7f640b62c83493829cec')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    export AUTHOR_TESTING=1
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
