# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-cpanel-json-xs'
pkgver=3.0239
pkgrel='3'
pkgdesc='cPanel fork of JSON::XS, fast and correct serializing'
arch=('x86_64' 'i686')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl-podlators>=2.08')
makedepends=(
  'perl-extutils-makemaker')
checkdepends=(
  'perl-class-xsaccessor'
  'perl-json'
  'perl-json-xs'
  'perl-mojolicious'
  'perl-perl-minimumversion'
  'perl-pod-spell-commonmistakes'
  'perl-test-cpan-meta'
  'perl-test-leaktrace'
  'perl-test-minimumversion'
  'perl-test-pod'
  'perl-test-pod-coverage'
  'perl-test-spelling'
)
url='https://metacpan.org/release/Cpanel-JSON-XS'
_distdir="Cpanel-JSON-XS-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/R/RU/RURBAN/${_distdir}.tar.gz")
sha512sums=('fee7b52f2bb48a4dc6314da4c274bb3456b6af2a0d891c1125425e5a0898e3115ea486a7dc057c25aaf7fc141bfebb5ea4341b17d00373b68a7388eb75f5bdb0')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    export AUTHOR_TESTING=1
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
