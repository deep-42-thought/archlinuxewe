# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-cpanel-json-xs'
pkgver=4.19
pkgrel=2
pkgdesc='cPanel fork of JSON::XS, fast and correct serializing'
arch=('x86_64' 'i686' 'pentium4')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
optdepends=(
  'perl-math-bigfloat'
  'perl-math-bigint')
makedepends=(
  'perl-data-dumper'
  'perl-extutils-makemaker'
  'perl-lib'
  'perl-test'
  'perl-test-simple'
  'perl-time-piece')
_pinned_dependencies=(
  'glibc>=2.31'
)
depends=(
  "${_pinned_dependencies[@]}"
  'perl'
  'perl-carp'
  'perl-encode>=1.9801'
  'perl-exporter'
  'perl-podlators>=2.08'
  'perl-xsloader')
checkdepends=(
  'perl-class-xsaccessor'
  'perl-json'
  'perl-json-xs'
  'perl-mojolicious'
  'perl-perl-minimumversion'
  'perl-pod-spell-commonmistakes'
  'perl-test-cpan-meta'
  'perl-test-leaktrace'
  'perl-test-minimumversion'
  'perl-test-pod'
  'perl-test-pod-coverage'
  'perl-test-spelling'
)
url='https://metacpan.org/release/Cpanel-JSON-XS'
_distdir="Cpanel-JSON-XS-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/R/RU/RURBAN/${_distdir}.tar.gz")
sha512sums=('c67b62e8f95900cb3626357cfa9976f1553f84ffd10a977d47839ca3b317014473c3a6e48f6680478b89bbdc2ac177b22441ad5e49e7e8eb0344ef14a3c52b10')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    export AUTHOR_TESTING=1
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
