# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-test-trailingspace'
pkgver=0.0601
pkgrel=2
pkgdesc="test for trailing space in source files."
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl>=5.34'
  'perl<5.35'
  'perl-autodie'
  'perl-file-find-object-rule>=0.0301'
  'perl-test-simple>=0.88')
makedepends=(
  'perl-extutils-makemaker'
  'perl-file-path'
  'perl-file-treecreate'
  'perl-io'
  'perl-module-build>=0.28'
  'perl-pathtools')
checkdepends=(
  'perl-cpan-changes'
  'perl-pod-coverage-trustpod'
  'perl-test-kwalitee-extra'
  'perl-test-pod'
  'perl-test-pod-coverage'
)
url='http://search.cpan.org/~shlomif/Test-TrailingSpace-0.0205/'
_distdir="Test-TrailingSpace-${pkgver}"
source=("http://search.cpan.org/CPAN/authors/id/S/SH/SHLOMIF/${_distdir}.tar.gz")
sha512sums=('71a207be6d3d0791ca3f7784cda053a1de33969a531805afea3d2ce4985eb745afa5ab8cc9aa8642e236eb516dafe5962c3a433c2b53910d0b272e94efb5586b')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    export AUTHOR_TESTING=1
    export RELEASE_TESTING=1
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
