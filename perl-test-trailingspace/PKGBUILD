# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-test-trailingspace'
pkgver=0.0400
pkgrel=1
pkgdesc="test for trailing space in source files."
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
makedepends=(
  'perl'
  'perl-extutils-makemaker'
  'perl-file-path'
  'perl-io'
  'perl-lib'
  'perl-module-build>=0.28'
  'perl-pathtools'
  'perl-test-simple>=0.88')
depends=(
  'perl>=5.14.0'
  'perl-autodie'
  'perl-file-find-object-rule>=0.0301'
  'perl-test-simple>=0.88')
checkdepends=(
  'perl-cpan-changes'
  'perl-pod-coverage-trustpod'
  'perl-test-kwalitee-extra'
  'perl-test-pod'
  'perl-test-pod-coverage'
)
url='http://search.cpan.org/~shlomif/Test-TrailingSpace-0.0205/'
_distdir="Test-TrailingSpace-${pkgver}"
source=("http://search.cpan.org/CPAN/authors/id/S/SH/SHLOMIF/${_distdir}.tar.gz")
sha512sums=('c757d70e6e8c53f3acc3b1605b80892c9ce01263ef4af77a1ef965fb7577af9c56fc880529860ce8c88ec9f9502e14dd6b12f7cd70102c48eee58a4627600db0')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    export AUTHOR_TESTING=1
    export RELEASE_TESTING=1
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
