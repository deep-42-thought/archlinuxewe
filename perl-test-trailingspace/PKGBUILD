# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-test-trailingspace'
pkgver=0.0301
pkgrel='5'
pkgdesc="test for trailing space in source files."
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl>=5.28'
  'perl<5.29'
  'perl-autodie'
  'perl-file-find-object-rule>=0.0301'
  'perl-test-simple')
makedepends=(
  'perl-extutils-makemaker'
  'perl-file-path'
  'perl-lib'
  'perl-module-build>=0.28'
  'perl-pathtools')
checkdepends=(
  'perl-cpan-changes'
  'perl-pod-coverage-trustpod'
  'perl-test-kwalitee-extra'
  'perl-test-pod'
  'perl-test-pod-coverage'
)
url='http://search.cpan.org/~shlomif/Test-TrailingSpace-0.0205/'
_distdir="Test-TrailingSpace-${pkgver}"
source=("http://search.cpan.org/CPAN/authors/id/S/SH/SHLOMIF/${_distdir}.tar.gz")
sha512sums=('9ec511759b12265c2a98a06a54081e0ca0377e707b29174fb874fd505330e0abfafebe910f7ceba9f8a6bcbfcf5d74fc8ffdca2e17b9cad15c207213449085d4')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    export AUTHOR_TESTING=1
    export RELEASE_TESTING=1
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
