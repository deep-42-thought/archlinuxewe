# $Id$
# Contributor: Dan McGee <dan@archlinux.org>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=pacman-mirrorlist
pkgver=20180203
pkgrel=1
pkgdesc="Arch Linux mirror list for use by pacman"
arch=('any')
url="https://www.archlinux.org/mirrorlist/"
license=('GPL')
backup=(etc/pacman.d/mirrorlist)
source=("${pkgname}-${pkgver}-${pkgrel%%.*}-any-original.tar.xz::https://arch.eckner.net/merged/core/os/$(uname -m)/${pkgname}-${pkgver}-${pkgrel%%.*}-any.pkg.tar.xz")
sha512sums=('0ba24e6bcb2f54e9614f9da1d1ae258e5feeb9f78969c54476c3e00121d29d9b5645b6b0bf59af48178c4aaf44145ba6705829ad3432ab62b5717e6f8f164f3f')

build() {
  {
    while read s
    do
      echo "${s}"
      if [ -z "${s}" ]
      then
        echo 'Server = https://arch.eckner.net/merged/$repo/os/$arch'
        echo ''
        break
      fi
    done
    cat
  } \
    < etc/pacman.d/mirrorlist \
    > mirrorlist
}

package() {
  mkdir -p $pkgdir/etc/pacman.d
  install -m644 mirrorlist $pkgdir/etc/pacman.d/
}
