# $Id$
# Contributor: Dan McGee <dan@archlinux.org>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=pacman-mirrorlist
pkgver=20180415
pkgrel=1
pkgdesc="Arch Linux mirror list for use by pacman"
arch=('any')
url="https://www.archlinux.org/mirrorlist/"
license=('GPL')
backup=(etc/pacman.d/mirrorlist)
source=("${pkgname}-${pkgver}-${pkgrel%%.*}-any-original.tar.xz::https://arch.eckner.net/merged/core/os/$(uname -m)/${pkgname}-${pkgver}-${pkgrel%%.*}-any.pkg.tar.xz")
sha512sums=('81e106049170a3ba27f2ab8d1db0ae281119f0d27f98f8886d99f1a9df01229b6423acc2547060d4cf590a5b19dcbea351f5b21a864d7dabe2655dc991ffab2a')

build() {
  {
    while read s
    do
      echo "${s}"
      if [ -z "${s}" ]
      then
        echo 'Server = https://arch.eckner.net/merged/$repo/os/$arch'
        echo ''
        break
      fi
    done
    cat
  } \
    < etc/pacman.d/mirrorlist \
    > mirrorlist
}

package() {
  mkdir -p $pkgdir/etc/pacman.d
  install -m644 mirrorlist $pkgdir/etc/pacman.d/
}
