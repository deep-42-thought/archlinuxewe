# $Id$
# Contributor: Dan McGee <dan@archlinux.org>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=pacman-mirrorlist
pkgver=20180524
pkgrel=1
pkgdesc="Arch Linux mirror list for use by pacman"
arch=('any')
url="https://www.archlinux.org/mirrorlist/"
license=('GPL')
backup=(etc/pacman.d/mirrorlist)
source=("${pkgname}-${pkgver}-${pkgrel%%.*}-any-original.tar.xz::https://arch.eckner.net/merged/core/os/$(uname -m)/${pkgname}-${pkgver}-${pkgrel%%.*}-any.pkg.tar.xz")
sha512sums=('1c1066b9a741b7e9d7eb0b6f50653df87c2013962ed8ed20d0b3c6b84bef3132f9dc9cb842e56d24edc3338c2e5505834eb56b05ae43084fd1715311c559b107')

build() {
  {
    while read s
    do
      echo "${s}"
      if [ -z "${s}" ]
      then
        echo 'Server = https://arch.eckner.net/merged/$repo/os/$arch'
        echo ''
        break
      fi
    done
    cat
  } \
    < etc/pacman.d/mirrorlist \
    > mirrorlist
}

package() {
  mkdir -p $pkgdir/etc/pacman.d
  install -m644 mirrorlist $pkgdir/etc/pacman.d/
}
