# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Michal Kaliszka <desmont@gmail.com>
# Contributor: Zsolt Varadi <sysop_xxl@fibermail.hu>
# Contributor: Holger Rauch < holger dot rauch at posteo dot de >

pkgname=tea
pkgver=44.1.1
pkgrel=1
pkgdesc="A Qt-based text editor for Linux and *BSD. With an ultimate small size TEA provides you hundreds of functions."
arch=('x86_64')
url="http://semiletov.org/tea/"
license=('GPL')
depends=('qt5-base' 'qt5-declarative' 'gcc-libs' 'aspell' 'hunspell')
install=tea.install
DLAGENTS=("http::/usr/bin/curl -A 'Mozilla' -fLC - --retry 3 --retry-delay 3 -o %o %u")
source=(http://semiletov.org/tea/dloads/tea-$pkgver.tar.bz2
        tea.desktop)
sha512sums=('fc7bf6f76ac3a48767a39ff5bcada4c7dae40950bfd9aca4021b3581e1b702c73f2608b070306dd528932d5eaa73bf01632a4376bbb71deaf1189a0f790c05a9'
            'f7fee6a36cf5ef60f6bcac36c1a97f104c84b9fbdc7d6269349137c5e107987b87c4f01e22f32568ec6548916cf92696e72352756cfe7747f8744adbeca3d285')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|/usr/local|/usr|g' src.pro
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export LANG="en_US.UTF-8"
  export LC_ALL="en_US.UTF-8"
  qmake-qt5
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  export LANG="en_US.UTF-8"
  export LC_ALL="en_US.UTF-8"
  make INSTALL_ROOT="${pkgdir}" install
  install -Dm644 "${srcdir}/tea.desktop" "${pkgdir}/usr/share/applications/tea.desktop"
  install -Dm644 icons/tea_icon_v2.png "${pkgdir}/usr/share/pixmaps/tea.png"
}
