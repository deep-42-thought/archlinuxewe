# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: L <alaviss 0 at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Valeriy Lyasotskiy <onestep@ukr.net>
# Contributor: Jan Willemson <janwil@hot.ee>
# Contributor: Hugo Ideler <hugoideler@dse.nl>
# Contributor: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Original PKGBUILD: Andre Naumann <anaumann@SPARCed.org>
# See http://bbs.archlinux.org/viewtopic.php?t=9318&highlight=fpc

pkgname='fpc-docs-svn'
pkgver=2.6.0.r1079.54c7140c
_pkgver="${pkgver%.r*}"
_commit="${pkgver##*.}"
pkgrel=1
arch=('any')
url='http://www.freepascal.org/'
license=('GPL' 'LGPL' 'custom')
pkgdesc='Documentation for the Free Pascal Compiler'
options=('!strip')
makedepends=('subversion'
  'texlive-core' 'texlive-htmlxml'
  'texlive-latexextra' 'ghostscript' 'fpc-src-svn' 'fpc-svn')
source=('git+https://gitlab.com/freepascal.org/fpc/documentation.git#commit='"${_commit}")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}"/documentation
  last_version=$(
    git tag \
    | sed '
      s@^release_\([0-9_]\+\)$@\1@
      t
      d
    ' \
    | tr '_' '.' \
    | sort -V \
    | tail -n1
  )
  _commit=$(
    git rev-parse "${_commit}" \
    | cut -c1-8
  )
  revision=$(
    git rev-list --count "${_commit}" "^release_${last_version//./_}"
  )
   printf '%s.r%s.%s' \
    "${last_version}" \
    "${revision}" \
    "${_commit}"
}

build() {
  cd "${srcdir}"/documentation
  cp -r /usr/lib/fpc/src ../fpcsrc
  make -j1 FPCSRCDIR="${srcdir}"/fpcsrc html
}

package() {
  cd "${srcdir}"/documentation
  make PREFIX="${pkgdir}"/usr htmlinstall
}
