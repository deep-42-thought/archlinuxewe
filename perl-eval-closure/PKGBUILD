# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: 3ED <krzysztof1987 at gmail dot com>
#
pkgname=perl-eval-closure
_lastauthor=D/DO/DOY
_pkgname=Eval-Closure
pkgver=0.14
pkgrel='3'
pkgdesc="safely and cleanly create closures via string eval"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl-carp'
  'perl-constant'
  'perl-exporter'
  'perl-scalar-list-utils')
makedepends=(
  'perl-extutils-makemaker'
  'perl-io'
  'perl-pathtools'
  'perl-test-fatal'
  'perl-test-requires'
  'perl-test-simple>=0.88')
optdepends=(
  'perl-devel-lexalias>=0.05'
  'perl-perl-tidy')
checkdepends=('perl-test-requires' 'perl-test-fatal' 'perl-test-output' 'perl-devel-lexalias' 'perl-padwalker')
url="https://metacpan.org/release/${_pkgname}"
_distdir="${_pkgname}-${pkgver}"
source=(https://cpan.metacpan.org/authors/id/${_lastauthor}/${_distdir}.tar.gz)
sha512sums=('fc55206bd39c4cb39360d06b6f39a65743f34b5e59d1a1ce99bf5831b9d88a03fb6dadf32fa9f0868e140fce719d53a7b13027f397cdd7f6ca05cc81277bdc08')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/${_distdir}"
  perl Makefile.PL
  make
}
check() {
  cd "${srcdir}/${_distdir}"
  make test
}
package() {
  cd "${srcdir}/${_distdir}"
  make install
}
