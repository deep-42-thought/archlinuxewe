# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=boulder
pkgver=2019_09_09
_pkgver="${pkgver//_/-}"
pkgrel=1
pkgdesc='ACME-compatible X.509 Certificate Authority'
arch=('x86_64' 'i686' 'pentium4')
url='https://github.com/letsencrypt/boulder'
license=('MPL2')
groups=()
depends=(
  'libtool'
)
makedepends=(
  'go'
)
checkdepends=()
optdepends=()
provides=()
conflicts=()
replaces=()
backup=(
  'etc/boulder/config/admin-revoker.json'
  'etc/boulder/config/akamai-purger.json'
  'etc/boulder/config/ca-a.json'
  'etc/boulder/config/ca-b.json'
  'etc/boulder/config/cert-checker.json'
  'etc/boulder/config/contact-exporter.json'
  'etc/boulder/config/expiration-mailer.json'
  'etc/boulder/config/nonce.json'
  'etc/boulder/config/notify-mailer.json'
  'etc/boulder/config/ocsp-responder.json'
  'etc/boulder/config/ocsp-updater.json'
  'etc/boulder/config/orphan-finder.json'
  'etc/boulder/config/publisher.json'
  'etc/boulder/config/ra.json'
  'etc/boulder/config/sa.json'
  'etc/boulder/config/va.json'
  'etc/boulder/config/wfe.json'
  'etc/boulder/config/wfe2.json'
  'etc/boulder/data/'
  'etc/boulder/data/production-email.template'
  'etc/boulder/data/staging-email.template'
)
options=()
source=(
  "${pkgname}-${_pkgver}.tar.gz::https://github.com/letsencrypt/${pkgname}/archive/release-${_pkgver}.tar.gz"
)
sha512sums=('5b4caa5cff7d79b9d3174aa88f715ac5529910bc07e3d0bb4c5c78b340b64c42b5ccfd91154bc823ecf3b03bd5563a5c18d6dc78258c04a7f8a50e6f3dd1f95c')

prepare() {

  cd "${pkgname}-release-${_pkgver}"
  sed -i '
    / go install / s/ \$(GO_BUILD_FLAGS) / -buildmode=pie /
  ' 'Makefile'

}

build() {

  cd "${pkgname}-release-${_pkgver}"
  make

  find 'test/config' -type f \
    -exec sed -i '
      s@"test/@"@
    ' {} +

}

package() {

  cd "${pkgname}-release-${_pkgver}"

  install -d -m755 "${pkgdir}/usr/bin"

  find cmd -maxdepth 1 -mindepth 1 -type d -printf 'bin/%f\n' \
  | grep -vxF 'bin/testdata' \
  | xargs -r install -m755 -t "${pkgdir}/usr/bin/"

  install -D -m755 -t "${pkgdir}/etc/boulder/config" 'test/config/'*
  install -D -m755 -t "${pkgdir}/etc/boulder/data" 'data/'*
  install -d -m755 "${pkgdir}/var/lib/boulder/sa"
  mv sa/_db "${pkgdir}/var/lib/boulder/sa"

}
