# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Adrian HÃ¼hn <adrian.huehn@web.de>
# Previous Maintainer: Vit <vitruss@gmail.com>

pkgname=xcrysden
pkgver=1.6.2
pkgrel=31
epoch=
pkgdesc="crystalline and molecular structure visualisation program"
arch=('i686' 'pentium4' 'x86_64')
url="http://www.xcrysden.org/"
license=('GPL2')
_pinned_dependencies=(
  'gcc-libs=12.1.0'
  'gl2ps=1.4.2'
  'glibc>=2.31'
  'glu=9.0.2'
  'libfftw3.so=3'
  'libgfortran.so=5'
  'libglvnd=1.4.0'
  'libx11=1.8.1'
  'libxmu=1.1.3'
  'tcl=8.6.12'
  'tk=8.6.12'
)
depends=('tk-togl' 'gl2ps' 'bwidget' 'imagemagick' 'gcc-fortran' "${_pinned_dependencies[@]}")
replaces=('xcrysden-bin')
source=("http://www.xcrysden.org/download/xcrysden-${pkgver}.tar.gz"
        "fix-xcrysden-install.patch"
	"Make.sys")
sha512sums=('9ed1b7fc11674509a3f2bf6604aa11e087fd77a971afb93039ca542cb9abb62b2553c7f386b845f004df5d3d340bdf1fe673552319788f006fd031efb6a4ad67'
            '17981ae72fc1b1d36388830c49ebc5d5d6916ee56c06b3fd3132edbe21a880d39bd85903183a4270b494e510e2c76d9276660ffeed92d6cc34abf33c3b2ae0ff'
            'd6f2a09a54570e6a0d0c8540d6c4eeffb12ffe9b5ed5840bcb61c48f4329e1ef5114d2c5b7882057465458fce7b78dec8fb009a761a5aa0e444d35458840ec8e')

prepare() {
	mv Make.sys "$pkgname-$pkgver"
	cd "$pkgname-$pkgver"
	patch -p1 -i $srcdir/fix-xcrysden-install.patch
}

build() {
	cd "$pkgname-$pkgver"
	make xcrysden
}

package() {
	cd "$pkgname-$pkgver"
	prefix=$pkgdir/usr make install
}
