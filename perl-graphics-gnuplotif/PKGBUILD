# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-graphics-gnuplotif'
pkgver='1.8'
pkgrel=12
pkgdesc="A dynamic Perl interface to gnuplot"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl>=5.36'
  'perl<5.37'
  'gnuplot'
  'perl-test-simple'
  'perl-version')
makedepends=(
  'perl-extutils-makemaker')
checkdepends=(
  'perl-test-pod'
  'perl-test-pod-coverage'
  'xorg-server-xvfb')
url='http://search.cpan.org/~mehner/Graphics-GnuplotIF-1.8/'
_distdir="Graphics-GnuplotIF-${pkgver}"
source=("http://search.cpan.org/CPAN/authors/id/M/ME/MEHNER/${_distdir}.tar.gz")
sha512sums=('c0eb2d39d38a891b69969448a1e9be4184c4ea045c1eee813f6c7293d8c3c23d044e1e90c0834fcc76b7f84d388706e0f58bbee044dc9b5d6e4b5cc523a61ea6')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    export USER=mehner
    xvfb-run -a -s "+extension GLX -screen 0 1280x1024x24" make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
