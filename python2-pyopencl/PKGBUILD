# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

_pypiname=pyopencl
pkgname=python2-pyopencl
pkgver=2019.1.1
pkgrel=2
epoch=1
pkgdesc="A complete, object-oriented language binding of OpenCL to Python2"
arch=('i686' 'pentium4' 'x86_64')
url="https://mathema.tician.de/software/pyopencl"
license=('MIT' 'Apache' 'BSD')
depends=(
  'boost-libs'
  'mesa'
  'opencl-headers'
  'opencl-icd-loader'
  'pyopencl-headers'
  'python2>=2.7'
  'python2<2.8'
  'python2-cffi'
  'python2-mako'
  'python2-numpy'
  'python2-pytools'
  'python2-setuptools'
)
makedepends=('ctags' 'boost' 'git' 'pybind11')
checkdepends=('python2-six' 'python2-appdirs'
            'python2-pytest' 'python2-pytools')
source=("git+https://github.com/inducer/pyopencl.git?signed#tag=v${pkgver}")
validpgpkeys=("900A958D9A0ACA58B1468F2471AA298BCA171145") # Andreas Ratchke
sha512sums=('SKIP')

build() {
    cd "pyopencl"
    git submodule init && git submodule update
    python2 ./configure.py --python-exe=python2 --cl-pretend-version=1.2 # --boost-python-libname=boost_python3
    make
}

check(){
    # INFO: to avoid a PLATFORM_NOT_FOUND_KHR error here, you would have to install
    # the driver of your specific video card in here
    # checkdepends+=('amdgpu')
    cd pyopencl
    PYTHONPATH="$PWD/build/lib.linux-${CARCH}-2.7/" make tests
}

package() {
    cd pyopencl
    python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

    rm -fr "${pkgdir}"/usr/include

    install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
