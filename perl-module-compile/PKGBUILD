# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=perl-module-compile
_pkgname='Module-Compile'
pkgver=0.38
pkgrel=5
_distdir="${_pkgname}-${pkgver}"
pkgdesc="Perl Module Compilation"
arch=('any')
url="http://pdl.perl.org/"
license=('GPL' 'PerlArtistic')
makedepends=(
  'perl-extutils-makemaker')
depends=(
  'perl>=5.36'
  'perl<5.37'
  'perl-capture-tiny'
  'perl-digest-sha1>=2.13'
  'perl-test-harness')
checkdepends=(
  'perl-test-pod')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/I/IN/INGY/${_distdir}.tar.gz")
sha512sums=('50f17cf6e88f9834d69944e64a46b2ca260824220cf09fe962c29dd36a8d0cdd7a2d59c7e0cf360a7fb506252cdea3b13b99437e443735b35c7a713a0bea1f31')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_distdir}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "${srcdir}/${_distdir}"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    export RELEASE_TESTING=1
    make test
  )
}

package() {
  cd "${srcdir}/${_distdir}"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
