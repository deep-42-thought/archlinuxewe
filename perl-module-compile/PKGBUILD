# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=perl-module-compile
_pkgname='Module-Compile'
pkgver=0.37
pkgrel='3'
_distdir="${_pkgname}-${pkgver}"
pkgdesc="Perl Module Compilation"
arch=('any')
url="http://pdl.perl.org/"
license=('GPL' 'PerlArtistic')
depends=(
  'perl>=5.28'
  'perl<5.29'
  'perl-capture-tiny'
  'perl-digest-sha1>=2.13'
  'perl-test-harness')
makedepends=(
  'perl-extutils-makemaker')
checkdepends=(
  'perl-test-pod')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/I/IN/INGY/${_distdir}.tar.gz")
sha512sums=('cc0382ad43d5307480d741604c17d654b919521306e37e47e7d19da8bf52208815733f9f6227ab56fe8c3022dbd46842b9d8c4203e1450ad0a393ee1b9ce7bc3')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_distdir}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "${srcdir}/${_distdir}"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    export RELEASE_TESTING=1
    make test
  )
}

package() {
  cd "${srcdir}/${_distdir}"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
