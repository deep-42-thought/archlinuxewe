# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname='perl-math-derivative'
pkgver=1.01
pkgrel=1
pkgdesc="Perl package for performing numerical differentiation on vectors of data"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl>=5.10.1')
makedepends=(
  'perl-math-utils>=1.10'
  'perl-module-build'
  'perl-test-simple')
checkdepends=('perl-test-pod>=1.22')
url='http://search.mcpan.org/dist/Math-Derivative'
source=("https://cpan.metacpan.org/authors/id/J/JG/JGAMBLE/Math-Derivative-${pkgver}.tar.gz")
sha512sums=('8fb13bb64b205892f39ba732f864010971cb08eef26987e9ef31e43c2b0eae2758992cbb719d38cbbb0858c0e55067a0d009322e65f31817d713a84d157e7805')
_distdir="Math-Derivative-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
