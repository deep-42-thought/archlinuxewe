# Contributor: Adrian C. (anrxc) <anrxc..sysphere.org>
# Contributor: Jose Riha jose1711 gmail com
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=topal
pkgver=80
pkgrel=1
pkgdesc='glue program that links GnuPG and Alpine'
arch=('i686' 'x86_64')
url="https://zircon.org.uk/topal/rel-${pkgver}/"
license=("GPL3")
makedepends=("gcc-ada" "make" "lynx")
optdepends=("alpine: for which topal was mainly written for"
            "re-alpine: fork of alpine for which topal was mainly written for"
            "gnupg: to encrypt, decrypt, sign and verify e-mail"
            "metamail: to display and process MIME messages"
            "mime-support: its run-mailcap tool as alternative to metamail"
            "openssh: for remote and server mode of operation"
            "procmail: to use formail")
install="${pkgname}.install"
options=("!makeflags")
source=("https://zircon.org.uk/topal/rel-${pkgver}/topal-package-${pkgver}.tgz"{,.asc})
sha512sums=('06c6bb9f504e5df5ffb2c8c776dd95071c90087bcc8353664d979ea4acd8d7563a8ca0b32cbe85f64c23419f479134d48bc795eb6db6bd716877ab414a37a030'
'SKIP')
validpgpkeys=('D312B4FF825FCA97472A558F9623D47747DC67A1') # Phil Brooke

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

# Remove PDF build stuff
  sed -i 's/binary topal.pdf/binary/' Makefile

# Build Topal and MIME-tool
  make distclean
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

# Install Topal binary, manual page and documentation
  install -D -m755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -D -m644 "${pkgname}.man" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
# Replaced by some PDF stuff which we will not build
  #install -D -m644 README.txt "${pkgdir}/usr/share/doc/${pkgname}/README"

# Install Topal's version of MIME-tool and manual page
  install -D -m755 MIME-tool/mime-tool "${pkgdir}/usr/bin/mime-tool"
  install -D -m644 MIME-tool/mime-tool.man "${pkgdir}/usr/share/man/man1/mime-tool.1"
}
