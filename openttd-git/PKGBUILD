# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=openttd-git
pkgver=29779.8596b43b2
_commit=${pkgver#*.}
pkgrel=2
pkgdesc="A FOSS clone of Transport Tycoon Deluxe."
arch=('x86_64' 'i686' 'pentium4')
url="http://www.openttd.org"
license=('GPL2')
groups=()
_pinned_dependencies=(
  'gcc-libs=10.2.0'
  'glibc>=2.31'
  'libfluidsynth.so=2'
  'libfontconfig.so=1'
  'libfreetype.so=6'
  'libicui18n.so=68'
  'libicuuc.so=68'
  'libpng=1.6.37'
  'libunwind=1.3.1'
  'lzo=2.10'
  'sdl2=2.0.14'
  'xz=5.2.5'
  'zlib=1:1.2.11'
)
depends=(
  'hicolor-icon-theme'
  "${_pinned_dependencies[@]}"
)
makedepends=('git' 'cmake')
checkdepends=(
    'openttd-opengfx'
)
optdepends=(
    'openttd-opengfx: free graphics'
    'openttd-opensfx: free soundset'
    'openttd-openmsx: free music'
)
provides=('openttd')
conflicts=('openttd')
replaces=()
backup=()
options=()
source=(
    "${pkgname}::git://github.com/OpenTTD/OpenTTD.git#commit=${_commit}"
    "chunnel-signaltunnel-pre.patch"
    "chunnel-underground-pre.patch"
    "chunnel.patch"
    "signaltunnel.patch"
    "sloped-stations.patch"
    "underground.patch"
    'opntitle.dat'
)
sha512sums=('SKIP'
            '26fa515d2706455f0f713ef8aab80fb2a52f665b54ae354d3feab2c25e194f80187125a28429a25ad86b58a2e902862aeb1ebae74e36d974ec78f4304df94b61'
            '09200144aa4dd5beaf12787d175aa47226205427721c05f681730474a95a2d2f8692be63df568c622f78edf81347dec249d0926203be3aa6dd902ff93f7aa047'
            '74283b8adb001a010aa05a8cee33b620371544e2909317f4c7156852e4ea440cdf8793dbe8957d646734ee309fdb5924915e0d967d78d43329ea4fc211e7e8ef'
            '497f395320edff647dd58c53abc451f706db9934fc847c95ecbbb33d519f67fe94ecd913e13bd7d770320370ea088b675e6583e8b59b69e2d3ee587a62ad2261'
            '11c87d30824bca5738e46c14422c622cdfe4d7e55a5b3a4fe5e77a0a14ceee742bfa8f6c0e8212e9b38c2a1d1db3a4f525c7a70a0b0ed03751dee2539d3da929'
            'f18a389800e5641ebc55d4322089d97a851125ae6d19ff944d3648b3b603128fe0fcb7ba2210cbd13ac8a0839003144299c217b994bdc0bc5e601fbff63308d2'
            'a3fd5b230b7cdd67a1b6607887433c0cb85ba1dfd30cedfddf573a58b23ccce7c470a22cf7f65a29c173bba12976c5889482cef24f49c8c0a829883a71bcd87d')

pkgver() {
  printf '%s.%s\n' \
    "$((
      $(git -C "${srcdir}/${pkgname}" rev-list --count "${_commit}" ^f84ad5f7c) + 28004
    ))" \
    "$(
      git -C "${srcdir}/${pkgname}" rev-parse --short=9 "${_commit}"
    )"
}

prepare() {

    cd "${srcdir}"
    cp chunnel.patch chunnel.pre.patch
    patch -p1 -i "chunnel-signaltunnel-pre.patch"
    patch -p1 -i "chunnel-underground-pre.patch"

    cd "${srcdir}/${pkgname}"
    cp "${srcdir}/opntitle.dat" media/baseset/

    ISODATE=$(
      date -d@$(
        git log -n1 --pretty=format:%ct
      ) +'%Y%m%d'
    )
    HASH=$(find {src,bin} -type f -exec sha512sum {} \; | sort | sha512sum - | cut -d' ' -f1)
    SHORTHASH=$(echo ${HASH} | cut -c1-8)
    printf '%s\t%s\t%s\t%s\t%s\t%s\t%s\n' \
      "${ISODATE}-ewe-g${SHORTHASH}" \
      "${ISODATE}" \
      "0" \
      "${HASH}" \
      "0" \
      "0" \
      "${ISODATE:0:4}" \
      > .ottdrev
    sed -i '
      s,"\${CMAKE_SOURCE_DIR}/\.git","/dev/does/not/exist",
    ' cmake/scripts/FindVersion.cmake

#    for _p in "${srcdir}/"{signaltunnel,sloped-stations,underground}.patch; do
    for _p in "${srcdir}/"{signaltunnel,underground,chunnel.pre}.patch; do
      >&2 echo "patching ${_p##*/} ..."
      patch -p1 -i "${_p}"
      >&2 echo "... ok"
    done
}

build() {

    cd "${srcdir}/${pkgname}"
    mkdir build
    cd build
    cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DPERSONAL_DIR=.openttd \
      ..
    make

#      -DBINARY_DIR=bin \
#      -DDATA_DIR=share/openttd \
#      -DICON_DIR=share/openttd \
#      -DMAN_DIR=share/man \

}

check() {
    cd "${srcdir}/${pkgname}/build"
    make test || true
}

package() {

    cd "${srcdir}/${pkgname}/build"
    make DESTDIR=$pkgdir install

    # Remove unnecessary languages
    cp $pkgdir/usr/share/games/openttd/lang/{english,german}.lng $srcdir
    rm $pkgdir/usr/share/games/openttd/lang/*
    install -m 644 $srcdir/{english,german}.lng $pkgdir/usr/share/games/openttd/lang

    # Remove junk
    rm -rf $pkgdir/usr/share/games/doc
    rm -rf $pkgdir/usr/share/games/openttd/scripts

}
