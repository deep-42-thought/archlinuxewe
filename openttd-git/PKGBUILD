# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=openttd-git
pkgver=29495.ee570e1b6
_commit=${pkgver#*.}
pkgrel=1
pkgdesc="A FOSS clone of Transport Tycoon Deluxe."
arch=('x86_64' 'i686' 'pentium4')
url="http://www.openttd.org"
license=('GPL2')
groups=()
_pinned_dependencies=(
  'fontconfig=2:2.13.91+24+g75eadca'
  'gcc-libs=10.1.0'
  'glibc>=2.31'
  'libfluidsynth.so=2'
  'libfreetype.so=6'
  'libicui18n.so=67'
  'libicuuc.so=67'
  'libpng=1.6.37'
  'libunwind=1.3.1'
  'lzo=2.10'
  'sdl2=2.0.12'
  'xz=5.2.5'
  'zlib=1:1.2.11'
)
depends=(
  'hicolor-icon-theme'
  "${_pinned_dependencies[@]}"
)
makedepends=('git' 'cmake')
checkdepends=(
    'openttd-opengfx'
)
optdepends=(
    'openttd-opengfx: free graphics'
    'openttd-opensfx: free soundset'
    'openttd-openmsx: free music'
)
provides=('openttd')
conflicts=('openttd')
replaces=()
backup=()
options=()
source=(
    "${pkgname}::git://github.com/OpenTTD/OpenTTD.git#commit=${_commit}"
    "signaltunnel.patch"
    "sloped-stations.patch"
    "underground.patch"
    'opntitle.dat'
)
sha512sums=('SKIP'
            '55f751de3846d62a6eb1088bfea356914ab368d9fabeb5cdd3fee72f3131e45226ab30e8c6cf1746d01bc58879e5244507fa4c6d5b6c9e685e56d1819b2884cb'
            'e6379448a49c0db8b07bb090ed14fe019611667b79506b4441219ee172893b726bdc431943c15a9f8fcc5a7b53de8e557929f10c27af32896a54463e2719e0a6'
            '8a54b6990f507879a57aedcdebc5dedddcf7d94cf1cf699ac0f4222ed657971c5414715dc6a246fcd86efb2d4e8cc3a785058773054642523788ec179eb08671'
            'a3fd5b230b7cdd67a1b6607887433c0cb85ba1dfd30cedfddf573a58b23ccce7c470a22cf7f65a29c173bba12976c5889482cef24f49c8c0a829883a71bcd87d')

pkgver() {
  printf '%s.%s\n' \
    "$((
      $(git -C "${srcdir}/${pkgname}" rev-list --count "${_commit}" ^f84ad5f7c) + 28004
    ))" \
    "$(
      git -C "${srcdir}/${pkgname}" rev-parse --short=9 "${_commit}"
    )"
}

prepare() {

    cd "${srcdir}/${pkgname}"
    cp "${srcdir}/opntitle.dat" media/baseset/

    ISODATE=$(
      date -d@$(
        git log -n1 --pretty=format:%ct
      ) +'%Y%m%d'
    )
    HASH=$(find {src,bin} -type f -exec sha512sum {} \; | sort | sha512sum - | cut -d' ' -f1)
    SHORTHASH=$(echo ${HASH} | cut -c1-8)
    printf '%s\t%s\t%s\t%s\t%s\t%s\t%s\n' \
      "${ISODATE}-ewe-g${SHORTHASH}" \
      "${ISODATE}" \
      "0" \
      "${HASH}" \
      "0" \
      "0" \
      "${ISODATE:0:4}" \
      > .ottdrev
    sed -i '
      s,"\${CMAKE_SOURCE_DIR}/\.git","/dev/does/not/exist",
    ' cmake/scripts/FindVersion.cmake

#    for _p in "${srcdir}/"{signaltunnel,sloped-stations,underground}.patch; do
    for _p in "${srcdir}/"{signaltunnel,underground}.patch; do
      >&2 echo "patching ${_p##*/} ..."
      patch -p1 -i "${_p}"
      >&2 echo "... ok"
    done
}

build() {

    cd "${srcdir}/${pkgname}"
    mkdir build
    cd build
    cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DPERSONAL_DIR=.openttd \
      ..
    make

#      -DBINARY_DIR=bin \
#      -DDATA_DIR=share/openttd \
#      -DICON_DIR=share/openttd \
#      -DMAN_DIR=share/man \

}

check() {
    cd "${srcdir}/${pkgname}/build"
    make test || true
}

package() {

    cd "${srcdir}/${pkgname}/build"
    make DESTDIR=$pkgdir install

    mv "$pkgdir/usr/share/man/openttd"/* "$pkgdir/usr/share/man/"
    rmdir "$pkgdir/usr/share/man/openttd"

    mv "$pkgdir/usr/share/games/openttd" "$pkgdir/usr/share/"
    rmdir "$pkgdir/usr/share/games"

    # Remove unnecessary languages
    cp $pkgdir/usr/share/openttd/lang/{english,german}.lng $srcdir
    rm $pkgdir/usr/share/openttd/lang/*
    install -m 644 $srcdir/{english,german}.lng $pkgdir/usr/share/openttd/lang

    # Remove junk
    rm -rf $pkgdir/usr/share/doc
    rm -rf $pkgdir/usr/share/openttd/scripts

}
