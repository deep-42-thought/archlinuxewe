# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=openttd-git
pkgver=28482.029c48cf4
_commit=${pkgver#*.}
pkgrel=1
pkgdesc="A FOSS clone of Transport Tycoon Deluxe."
arch=('x86_64' 'i686')
url="http://www.openttd.org"
license=('GPL2')
groups=()
depends=(
    'fluidsynth'
    'fontconfig'
    'hicolor-icon-theme'
    'icu'
    'libpng'
    'lzo'
    'sdl'
    'xz'
)
makedepends=('subversion')
checkdepends=(
    'openttd-opengfx'
)
optdepends=(
    'openttd-opengfx: free graphics'
    'openttd-opensfx: free soundset'
    'openttd-openmsx: free music'
)
provides=('openttd')
conflicts=('openttd')
replaces=()
backup=()
options=()
source=(
    "${pkgname}::git://github.com/OpenTTD/OpenTTD.git#commit=${_commit}"
    "signaltunnel.patch"
    "sloped-stations.patch"
    "underground.patch"
)
sha512sums=('SKIP'
            '8809a919aec99f5235067dcea1beda18ae65d12f3a37f304e3e341a94312819cb13a46c70c42d261e5487eeeb784d4ec098df6887dde5bb6d447a8dfbd5f08f4'
            '61b707d6ae0ad9a50a68fe3e1de9d28c20ee062552c045ca4e786968dfb6681abcf215ea59d67d415f81e7fdbceaa208f7184cb468a6c170a30e38b292c05723'
            '7369d488fc66c4a2249352f2481c31da91b19b3aed2a42b7bcf456c59ccd18dc011d42df3c2ad68be57e427237895fefa6d7b6c6661fa971875fa8f3d299a415')

pkgver() {
  printf '%s.%s\n' \
    "$((
      $(git -C "${srcdir}/${pkgname}" rev-list --count "${_commit}" ^f84ad5f7c) + 28004
    ))" \
    "$(
      git -C "${srcdir}/${pkgname}" rev-parse --short=9 "${_commit}"
    )"
}

prepare() {

    cd "${srcdir}/${pkgname}"

    ISODATE=$(
      date -d@$(
        git log -n1 --pretty=format:%ct
      ) +'%Y%m%d'
    )
    HASH=$(find {src,bin} -type f -exec sha512sum {} \; | sort | sha512sum - | cut -d' ' -f1)
    SHORTHASH=$(echo ${HASH} | cut -c1-8)
    printf '%s\t%s\t%s\t%s\n' \
      "${ISODATE}-ewe-g${SHORTHASH}" \
      "${ISODATE}" \
      "0" \
      "${HASH}" \
      > .ottdrev
    sed -i '
      s,"\$ROOT_DIR/\.git","/dev/does/not/exist",
    ' findversion.sh

    for _p in "${srcdir}/"{signaltunnel,sloped-stations,underground}.patch; do
      >&2 echo "patching ${_p##*/} ..."
      patch -p1 -i "${_p}"
      >&2 echo "... ok"
    done
    sed -i '
      s/readme\.txt/README.md/g
    ' Makefile.bundle.in

}

build() {

    cd "${srcdir}/${pkgname}"
    ./configure ${_targetHost} \
        --prefix-dir=/usr \
        --binary-dir=bin \
        --data-dir=share/openttd \
        --icon-dir=share/openttd \
        --man-dir=share/man \
        --personal-dir=.openttd \
        --install-dir=$pkgdir
    make

}

check() {
    cd "${srcdir}/${pkgname}"
    make test || true
}

package() {

    cd "${srcdir}/${pkgname}"
    make DESTDIR=$pkgdir install

    # Remove unnecessary languages
    cp $pkgdir/usr/share/openttd/lang/{english,german}.lng $srcdir
    rm $pkgdir/usr/share/openttd/lang/*
    install -m 644 $srcdir/{english,german}.lng $pkgdir/usr/share/openttd/lang

    # Remove junk
    rm -rf $pkgdir/usr/share/doc
    rm -rf $pkgdir/usr/share/openttd/scripts

}
