# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=openttd-git
pkgver=29436.b8a896d52
_commit=${pkgver#*.}
pkgrel=1
pkgdesc="A FOSS clone of Transport Tycoon Deluxe."
arch=('x86_64' 'i686' 'pentium4')
url="http://www.openttd.org"
license=('GPL2')
groups=()
depends=(
    'fluidsynth'
    'fontconfig'
    'hicolor-icon-theme'
    'icu'
    'libpng'
    'lzo'
    'sdl2'
    'xz'
)
makedepends=('git')
checkdepends=(
    'openttd-opengfx'
)
optdepends=(
    'openttd-opengfx: free graphics'
    'openttd-opensfx: free soundset'
    'openttd-openmsx: free music'
)
provides=('openttd')
conflicts=('openttd')
replaces=()
backup=()
options=()
source=(
    "${pkgname}::git://github.com/OpenTTD/OpenTTD.git#commit=${_commit}"
    "signaltunnel.patch"
    "sloped-stations.patch"
    "underground.patch"
)
sha512sums=('SKIP'
            '37dfab1746fc595a947da87bc591a7c9e9747d8bbb6d6a4ca934d13f60ac55c137bb2bed8789f775c4213dd3c9d6f7da9c3675177916c79e35389f48f0aa2fea'
            'cd208f7f58c9eb6814f201ea642139d353de5cdc39612e0fd8133c2357e12fbcc9af61e4ff093c939db336207c10206ee8c31c7dbce198787c20a18fd301c7f6'
            '029d4fa307ec1b4d5bcdbd1d0d66ff101851d9267e0a33f69864949af941751ab13113a71502d27a43c67e9eb75a58a489002ea335b69a495a139e4f99e072a7')

pkgver() {
  printf '%s.%s\n' \
    "$((
      $(git -C "${srcdir}/${pkgname}" rev-list --count "${_commit}" ^f84ad5f7c) + 28004
    ))" \
    "$(
      git -C "${srcdir}/${pkgname}" rev-parse --short=9 "${_commit}"
    )"
}

prepare() {

    cd "${srcdir}/${pkgname}"

    ISODATE=$(
      date -d@$(
        git log -n1 --pretty=format:%ct
      ) +'%Y%m%d'
    )
    HASH=$(find {src,bin} -type f -exec sha512sum {} \; | sort | sha512sum - | cut -d' ' -f1)
    SHORTHASH=$(echo ${HASH} | cut -c1-8)
    printf '%s\t%s\t%s\t%s\t%s\t%s\n' \
      "${ISODATE}-ewe-g${SHORTHASH}" \
      "${ISODATE}" \
      "0" \
      "${HASH}" \
      "0" \
      "0" \
      > .ottdrev
    sed -i '
      s,"\$ROOT_DIR/\.git","/dev/does/not/exist",
    ' findversion.sh

#    for _p in "${srcdir}/"{signaltunnel,sloped-stations,underground}.patch; do
    for _p in "${srcdir}/"{signaltunnel,underground}.patch; do
      >&2 echo "patching ${_p##*/} ..."
      patch -p1 -i "${_p}"
      >&2 echo "... ok"
    done
    sed -i '
      s/multiplayer\.txt/multiplayer.md/g
    ' Makefile.bundle.in
}

build() {

    cd "${srcdir}/${pkgname}"
    ./configure \
        --prefix-dir=/usr \
        --binary-dir=bin \
        --data-dir=share/openttd \
        --icon-dir=share/openttd \
        --man-dir=share/man \
        --personal-dir=.openttd \
        --install-dir=$pkgdir
    make

}

check() {
    cd "${srcdir}/${pkgname}"
    make test || true
}

package() {

    cd "${srcdir}/${pkgname}"
    make DESTDIR=$pkgdir install

    # Remove unnecessary languages
    cp $pkgdir/usr/share/openttd/lang/{english,german}.lng $srcdir
    rm $pkgdir/usr/share/openttd/lang/*
    install -m 644 $srcdir/{english,german}.lng $pkgdir/usr/share/openttd/lang

    # Remove junk
    rm -rf $pkgdir/usr/share/doc
    rm -rf $pkgdir/usr/share/openttd/scripts

}
