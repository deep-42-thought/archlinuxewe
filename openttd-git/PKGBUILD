# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=openttd-git
pkgver=28726.3357cac84
_commit=${pkgver#*.}
pkgrel=1
pkgdesc="A FOSS clone of Transport Tycoon Deluxe."
arch=('x86_64' 'i686')
url="http://www.openttd.org"
license=('GPL2')
groups=()
depends=(
    'fluidsynth'
    'fontconfig'
    'hicolor-icon-theme'
    'icu'
    'libpng'
    'lzo'
    'sdl'
    'xz'
)
makedepends=('subversion')
checkdepends=(
    'openttd-opengfx'
)
optdepends=(
    'openttd-opengfx: free graphics'
    'openttd-opensfx: free soundset'
    'openttd-openmsx: free music'
)
provides=('openttd')
conflicts=('openttd')
replaces=()
backup=()
options=()
source=(
    "${pkgname}::git://github.com/OpenTTD/OpenTTD.git#commit=${_commit}"
    "signaltunnel.patch"
    "sloped-stations.patch"
    "underground.patch"
)
sha512sums=('SKIP'
            '002c8e005f698abef938288bd79929a9dfeef581c33d936b03b81da495662bbe4de3afc48187dcb4c2660bf55004801bee9e447120e93c74610aec294ea22722'
            'b0172f664446896ff2131d169b3391bbac5168c9fc12334bf6947bb5afb298404f7f882b34e7b4d7cd24ee2b67d60d2da181ba64e0eca8873e46bce43efc3416'
            '78b8a6157a436fd091cce1eb651d2dd861f01e7d357f340f1b277928136b32181d43da4445ef1fb9030af9e8cca31ac7187127d0cc3c7df7f895eb3f570d3c0d')

pkgver() {
  printf '%s.%s\n' \
    "$((
      $(git -C "${srcdir}/${pkgname}" rev-list --count "${_commit}" ^f84ad5f7c) + 28004
    ))" \
    "$(
      git -C "${srcdir}/${pkgname}" rev-parse --short=9 "${_commit}"
    )"
}

prepare() {

    cd "${srcdir}/${pkgname}"

    ISODATE=$(
      date -d@$(
        git log -n1 --pretty=format:%ct
      ) +'%Y%m%d'
    )
    HASH=$(find {src,bin} -type f -exec sha512sum {} \; | sort | sha512sum - | cut -d' ' -f1)
    SHORTHASH=$(echo ${HASH} | cut -c1-8)
    printf '%s\t%s\t%s\t%s\t%s\t%s\n' \
      "${ISODATE}-ewe-g${SHORTHASH}" \
      "${ISODATE}" \
      "0" \
      "${HASH}" \
      "0" \
      "0" \
      > .ottdrev
    sed -i '
      s,"\$ROOT_DIR/\.git","/dev/does/not/exist",
    ' findversion.sh

    for _p in "${srcdir}/"{signaltunnel,sloped-stations,underground}.patch; do
      >&2 echo "patching ${_p##*/} ..."
      patch -p1 -i "${_p}"
      >&2 echo "... ok"
    done
    sed -i '
      s/readme\.txt/README.md/g
    ' Makefile.bundle.in

}

build() {

    cd "${srcdir}/${pkgname}"
    ./configure ${_targetHost} \
        --prefix-dir=/usr \
        --binary-dir=bin \
        --data-dir=share/openttd \
        --icon-dir=share/openttd \
        --man-dir=share/man \
        --personal-dir=.openttd \
        --install-dir=$pkgdir
    make

}

check() {
    cd "${srcdir}/${pkgname}"
    make test || true
}

package() {

    cd "${srcdir}/${pkgname}"
    make DESTDIR=$pkgdir install

    # Remove unnecessary languages
    cp $pkgdir/usr/share/openttd/lang/{english,german}.lng $srcdir
    rm $pkgdir/usr/share/openttd/lang/*
    install -m 644 $srcdir/{english,german}.lng $pkgdir/usr/share/openttd/lang

    # Remove junk
    rm -rf $pkgdir/usr/share/doc
    rm -rf $pkgdir/usr/share/openttd/scripts

}
