# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=openttd-git
pkgver=28280.6950ea0ba
_commit=${pkgver#*.}
pkgrel=1
pkgdesc="A FOSS clone of Transport Tycoon Deluxe."
arch=('x86_64' 'i686')
url="http://www.openttd.org"
license=('GPL2')
groups=()
depends=(
    'fontconfig'
    'hicolor-icon-theme'
    'icu'
    'libpng'
    'lzo'
    'sdl'
    'xz'
)
makedepends=('subversion')
checkdepends=(
    'openttd-opengfx'
)
optdepends=(
    'openttd-opengfx: free graphics'
    'openttd-opensfx: free soundset'
    'openttd-openmsx: free music'
)
provides=('openttd')
conflicts=('openttd')
replaces=()
backup=()
options=()
source=(
    "${pkgname}::git://github.com/OpenTTD/OpenTTD.git#commit=${_commit}"
    "everything.patch"
    "clipboard.grf"
)
sha512sums=('SKIP'
            'b012afff56ba3d129c54c748f96815518a2d1d1b86087c5f0bedbf8703aa1159f9cb3ba1c259656b549a06b5630812e878ca5fdf59e9d3089a59c4e6455fa6cd'
            'aa1f5d5c4fd9ff487bc03ed5c10701e99ae9fd29ae0cd65a06171486298558d1c26ced49ac8687acc5b1003a6538f6c83917992348cbd2b7426afdf0759cb1a5')

pkgver() {
  printf '%s.%s\n' \
    "$((
      $(git -C "${srcdir}/${pkgname}" rev-list --count "${_commit}" ^f84ad5f7c) + 28004
    ))" \
    "$(
      git -C "${srcdir}/${pkgname}" rev-parse --short=9 "${_commit}"
    )"
}

prepare() {

    cd "${srcdir}/${pkgname}"

    ISODATE=$(
      date -d@$(
        git log -n1 --pretty=format:%ct
      ) +'%Y%m%d'
    )
    HASH=$(find {src,bin} -type f -exec sha512sum {} \; | sort | sha512sum - | cut -d' ' -f1)
    SHORTHASH=$(echo ${HASH} | cut -c1-8)
    printf '%s\t%s\t%s\t%s\n' \
      "${ISODATE}-ewe-g${SHORTHASH}" \
      "${ISODATE}" \
      "0" \
      "${HASH}" \
      > .ottdrev
    sed -i '
      s,"\$ROOT_DIR/\.git","/dev/does/not/exist",
    ' findversion.sh

    git apply < ${srcdir}/everything.patch
    sed -i '
      s/readme\.txt/README.md/g
    ' Makefile.bundle.in

}

build() {

    cd "${srcdir}/${pkgname}"
    ./configure ${_targetHost} \
        --prefix-dir=/usr \
        --binary-dir=bin \
        --data-dir=share/openttd \
        --icon-dir=share/openttd \
        --man-dir=share/man \
        --personal-dir=.openttd \
        --install-dir=$pkgdir
    make
    install -m644 "${srcdir}/clipboard.grf" "${srcdir}/${pkgname}/bin/baseset/"

}

check() {
    cd "${srcdir}/${pkgname}"
    make test
}

package() {

    cd "${srcdir}/${pkgname}"
    make DESTDIR=$pkgdir install

    # Remove unnecessary languages
    cp $pkgdir/usr/share/openttd/lang/{english,german}.lng $srcdir
    rm $pkgdir/usr/share/openttd/lang/*
    install -m 644 $srcdir/{english,german}.lng $pkgdir/usr/share/openttd/lang

    # Remove junk
    rm -rf $pkgdir/usr/share/doc
    rm -rf $pkgdir/usr/share/openttd/scripts

}
