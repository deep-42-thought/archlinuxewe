# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: "Amhairghin" Oscar Garcia Amor (https://ogarcia.me)

pkgname=teams
pkgver=1.4.00.13653
pkgrel=11
pkgdesc="Microsoft Teams for Linux is your chat-centered workspace in Office 365"
arch=('x86_64')
url="https://teams.microsoft.com/downloads"
license=('custom')
_pinned_dependencies=(
  'at-spi2-atk=2.38.0'
  'at-spi2-core=2.40.3'
  'cairo=1.17.4'
  'gcc-libs=11.1.0'
  'glibc>=2.33'
  'libasound.so=2'
  'libatk-1.0.so=0'
  'libcups=1:2.3.3op2'
  'libdbus-1.so=3'
  'libdrm=2.4.107'
  'libexpat.so=1'
  'libgdk-3.so=0'
  'libgdk_pixbuf-2.0.so=0'
  'libgio-2.0.so=0'
  'libglib-2.0.so=0'
  'libgobject-2.0.so=0'
  'libgtk-3.so=0'
  'libpango-1.0.so=0'
  'libpangocairo-1.0.so=0'
  'libsecret-1.so=0'
  'libx11=1.7.2'
  'libxcb=1.14'
  'libxcomposite=0.4.5'
  'libxcursor=1.2.0'
  'libxdamage=1.1.5'
  'libxext=1.3.4'
  'libxfixes=6.0.0'
  'libxi=1.7.10'
  'libxrandr=1.5.2'
  'libxrender=0.9.10'
  'libxtst=1.2.3'
  'mesa=21.2.1'
  'nspr=4.32'
  'nss=3.70'
)
depends=("gtk3" "libxss" "alsa-lib" "libxtst" "libsecret" "nss" "glibc>=2.28-4" "${_pinned_dependencies[@]}")
optdepends=("libappindicator-gtk3: Systray indicator support"
            "org.freedesktop.secrets: Keyring password store support")
conflicts=("ms-teams")
replaces=("ms-teams")
source=("https://packages.microsoft.com/repos/ms-teams/pool/main/t/teams/${pkgname}_${pkgver}_amd64.deb")
noextract=("${pkgname}_${pkgver}_amd64.deb")
sha256sums=('5b4217fab3df145b1a37c53d138d87713786f290805a90fc276434770692a4cf')

package() {
  bsdtar -O -xf "${pkgname}_${pkgver}_amd64.deb" data.tar.xz | bsdtar -C "${pkgdir}" -xJf -

  # Permissions fix
  find "${pkgdir}" -type d -exec chmod 755 {} \;

  # Move license
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  mv "${pkgdir}/usr/share/${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "/usr/share/licenses/${pkgname}/LICENSE" "${pkgdir}/usr/share/${pkgname}/LICENSE"
}
