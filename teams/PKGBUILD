# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: "Amhairghin" Oscar Garcia Amor (https://ogarcia.me)

pkgname=teams
pkgver=1.5.00.10453
pkgrel=7
pkgdesc="Microsoft Teams for Linux is your chat-centered workspace in Office 365"
arch=('x86_64')
url="https://teams.microsoft.com/downloads"
license=('custom')
_pinned_dependencies=(
  'at-spi2-atk=2.38.0'
  'at-spi2-core=2.44.1'
  'cairo=1.17.6'
  'gcc-libs=12.1.0'
  'glibc>=2.35'
  'libasound.so=2'
  'libatk-1.0.so=0'
  'libcups=1:2.4.2'
  'libdbus-1.so=3'
  'libdrm=2.4.110'
  'libexpat.so=1'
  'libgdk-3.so=0'
  'libgdk_pixbuf-2.0.so=0'
  'libgio-2.0.so=0'
  'libglib-2.0.so=0'
  'libgobject-2.0.so=0'
  'libgtk-3.so=0'
  'libpango-1.0.so=0'
  'libpangocairo-1.0.so=0'
  'libsecret-1.so=0'
  'libx11=1.8'
  'libxcb=1.15'
  'libxcomposite=0.4.5'
  'libxcursor=1.2.1'
  'libxdamage=1.1.5'
  'libxext=1.3.4'
  'libxfixes=6.0.0'
  'libxi=1.8'
  'libxrandr=1.5.2'
  'libxrender=0.9.10'
  'libxtst=1.2.3'
  'mesa=22.1.0'
  'nspr=4.33'
  'nss=3.78'
)
depends=("gtk3" "libxss" "alsa-lib" "libxtst" "libsecret" "nss" "glibc>=2.28-4" "${_pinned_dependencies[@]}")
makedepends=(nodejs-lts-fermium)
optdepends=("libappindicator-gtk3: Systray indicator support"
            "org.freedesktop.secrets: Keyring password store support")
conflicts=("ms-teams")
replaces=("ms-teams")
source=("https://packages.microsoft.com/repos/ms-teams/pool/main/t/teams/${pkgname}_${pkgver}_amd64.deb")
noextract=("${pkgname}_${pkgver}_amd64.deb")
sha512sums=('69d95e06c67265358f1d995ad9faa21a1af753241031569e7ac88d0cc9c4781985a0839b4fa7e58d176472c7ab15bc8d0876733a2eb710d5d087d2662bc04c30')

package() {
  depends+=('nodejs>=14' 'nodejs<17')
  bsdtar -O -xf "${pkgname}_${pkgver}_amd64.deb" data.tar.xz | bsdtar -C "${pkgdir}" -xJf -

  # Permissions fix
  find "${pkgdir}" -type d -exec chmod 755 {} \;

  # Move license
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  mv "${pkgdir}/usr/share/${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "/usr/share/licenses/${pkgname}/LICENSE" "${pkgdir}/usr/share/${pkgname}/LICENSE"
}
