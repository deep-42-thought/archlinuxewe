# Maintainer:  Erich Eckner <arch at eckner dot net>
# Contributor: brent s. <bts[at]square-r00t[dot]net>
# Past maintainer: Joris Steyn <jorissteyn@gmail.com>
# Contributor: TDY <tdy@gmx.com>
pkgname=iozone
pkgver=3.492
pkgrel=1
pkgdesc="A filesystem benchmark tool"
arch=('i686' 'pentium4' 'x86_64')
url="http://www.iozone.org/"
license=('custom')
_pinned_dependencies=(
  'glibc>=2.31'
)
depends=('sh' "${_pinned_dependencies[@]}")
optdepends=('gnuplot: for generating graph reports')
source=("https://www.${pkgname}.org/src/current/${pkgname}${pkgver/./_}.tar")
sha512sums=('171bf02cb7b9fd121fd229ff551d24b2f70bbc22e051b5ad1792a9b617e8fb672dd17120658987115dea1017dddd56ccdf61f14c5930a09a6bd65b9ac0c3249f')

build() {
  cd "${srcdir}/${pkgname}${pkgver/./_}/src/current"

  if [[ "${CARCH}" == 'x86_64' ]];
  then
    TARGET='-AMD64'
  else
    TARGET=''
  fi

  make linux${TARGET} CFLAGS="${CFLAGS/-O2/}"
}

package() {
  cd "${srcdir}/${pkgname}${pkgver/./_}/src/current"
  install -D -m 755 -t ${pkgdir}/usr/bin/ iozone fileop pit_server

  cd "${srcdir}/${pkgname}${pkgver/./_}/docs"
  install -D -m 644 -t ${pkgdir}/usr/share/man/man1/ iozone.1
}
