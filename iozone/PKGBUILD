# Maintainer:  Erich Eckner <arch at eckner dot net>
# Contributor: brent s. <bts[at]square-r00t[dot]net>
# Past maintainer: Joris Steyn <jorissteyn@gmail.com>
# Contributor: TDY <tdy@gmx.com>
pkgname=iozone
pkgver=3.477
pkgrel=1
pkgdesc="A filesystem benchmark tool"
arch=('i686' 'x86_64')
url="http://www.iozone.org/"
license=('custom')
depends=('sh')
optdepends=('gnuplot: for generating graph reports')
source=("http://www.${pkgname}.org/src/current/${pkgname}${pkgver/./_}.tar")
sha512sums=('3438bb626e718e5c27a7292361edf287c5cedbeea4fcb8dda636376a8d96c77b0b6dd08c2d8f67202732fe0f5682fbf1e5a4299c6c8e022479efc865c48e96b6')

build() {
  cd "${srcdir}/${pkgname}${pkgver/./_}/src/current"

  if [[ "${CARCH}" == 'x86_64' ]];
  then
    TARGET='-AMD64'
  else
    TARGET=''
  fi

  make linux${TARGET} CFLAGS="${CFLAGS}"
}

package() {
  cd "${srcdir}/${pkgname}${pkgver/./_}/src/current"
  install -D -m 755 -t ${pkgdir}/usr/bin/ iozone fileop pit_server

  cd "${srcdir}/${pkgname}${pkgver/./_}/docs"
  install -D -m 644 -t ${pkgdir}/usr/share/man/man1/ iozone.1
}
