# Maintainer:  Erich Eckner <arch at eckner dot net>
# Contributor: brent s. <bts[at]square-r00t[dot]net>
# Past maintainer: Joris Steyn <jorissteyn@gmail.com>
# Contributor: TDY <tdy@gmx.com>
pkgname=iozone
pkgver=3.458
pkgrel=1
pkgdesc="A filesystem benchmark tool"
arch=('i686' 'x86_64')
url="http://www.iozone.org/"
license=('custom')
depends=('sh')
optdepends=('gnuplot: for generating graph reports')
source=("http://www.${pkgname}.org/src/current/${pkgname}${pkgver/./_}.tar")
sha512sums=('2852961ba361f57c895afeedb314beb38f89fd943d59f490b3acc197729db7b012a934c092842247a176adbe5c39068e7548033089ed83464e07cf9f58f8d0b6')

build() {
  cd "${srcdir}/${pkgname}${pkgver/./_}/src/current"

  if [[ "${CARCH}" == 'x86_64' ]];
  then
    TARGET='-AMD64'
  else
    TARGET=''
  fi

  make linux${TARGET} CFLAGS="${CFLAGS}"
}

package() {
  cd "${srcdir}/${pkgname}${pkgver/./_}/src/current"
  install -D -m 755 -t ${pkgdir}/usr/bin/ iozone fileop pit_server

  cd "${srcdir}/${pkgname}${pkgver/./_}/docs"
  install -D -m 644 -t ${pkgdir}/usr/share/man/man1/ iozone.1
}
