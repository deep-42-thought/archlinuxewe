# Maintainer:  Erich Eckner <arch at eckner dot net>
# Contributor: brent s. <bts[at]square-r00t[dot]net>
# Past maintainer: Joris Steyn <jorissteyn@gmail.com>
# Contributor: TDY <tdy@gmx.com>
pkgname=iozone
pkgver=3.491
pkgrel=1
pkgdesc="A filesystem benchmark tool"
arch=('i686' 'pentium4' 'x86_64')
url="http://www.iozone.org/"
license=('custom')
_pinned_dependencies=(
  'glibc>=2.31'
)
depends=('sh' "${_pinned_dependencies[@]}")
optdepends=('gnuplot: for generating graph reports')
source=("http://www.${pkgname}.org/src/current/${pkgname}${pkgver/./_}.tgz")
sha512sums=('16697ee4ed245bff2cc41bd3ffe73bfef351e1d3dc0fc83412d8e52f6a53da2edd708f50e1742788ff3f50aecf409730a1d6d4fd4ebc656a69ea7efd41c7386a')

build() {
  cd "${srcdir}/${pkgname}${pkgver/./_}/src/current"

  if [[ "${CARCH}" == 'x86_64' ]];
  then
    TARGET='-AMD64'
  else
    TARGET=''
  fi

  make linux${TARGET} CFLAGS="${CFLAGS/-O2/}"
}

package() {
  cd "${srcdir}/${pkgname}${pkgver/./_}/src/current"
  install -D -m 755 -t ${pkgdir}/usr/bin/ iozone fileop pit_server

  cd "${srcdir}/${pkgname}${pkgver/./_}/docs"
  install -D -m 644 -t ${pkgdir}/usr/share/man/man1/ iozone.1
}
