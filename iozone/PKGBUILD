# Maintainer:  Erich Eckner <arch at eckner dot net>
# Contributor: brent s. <bts[at]square-r00t[dot]net>
# Past maintainer: Joris Steyn <jorissteyn@gmail.com>
# Contributor: TDY <tdy@gmx.com>
pkgname=iozone
pkgver=3.482
pkgrel=1
pkgdesc="A filesystem benchmark tool"
arch=('i686' 'x86_64')
url="http://www.iozone.org/"
license=('custom')
depends=('sh')
optdepends=('gnuplot: for generating graph reports')
source=("http://www.${pkgname}.org/src/current/${pkgname}${pkgver/./_}.tar")
sha512sums=('05aec9a6be296340a243cc5beb848dd5405a197fe7c2c4ba0d0a209f907c18f372cef259f55303b013d10ccef5370f2ff76f524840ee155000efaeea86136a5d')

build() {
  cd "${srcdir}/${pkgname}${pkgver/./_}/src/current"

  if [[ "${CARCH}" == 'x86_64' ]];
  then
    TARGET='-AMD64'
  else
    TARGET=''
  fi

  make linux${TARGET} CFLAGS="${CFLAGS}"
}

package() {
  cd "${srcdir}/${pkgname}${pkgver/./_}/src/current"
  install -D -m 755 -t ${pkgdir}/usr/bin/ iozone fileop pit_server

  cd "${srcdir}/${pkgname}${pkgver/./_}/docs"
  install -D -m 644 -t ${pkgdir}/usr/share/man/man1/ iozone.1
}
