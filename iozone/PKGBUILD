# Maintainer:  Erich Eckner <arch at eckner dot net>
# Contributor: brent s. <bts[at]square-r00t[dot]net>
# Past maintainer: Joris Steyn <jorissteyn@gmail.com>
# Contributor: TDY <tdy@gmx.com>
pkgname=iozone
pkgver=3.478
pkgrel=1
pkgdesc="A filesystem benchmark tool"
arch=('i686' 'x86_64')
url="http://www.iozone.org/"
license=('custom')
depends=('sh')
optdepends=('gnuplot: for generating graph reports')
source=("http://www.${pkgname}.org/src/current/${pkgname}${pkgver/./_}.tar")
sha512sums=('69f678a28df3ff1ece9bbb9d1af777f9516b1d60b3e62617b69599ad3f3bc6baecfa31c58f17fd54f2d247d8d59174a635e91aa3ad4ff60a289f4e1b65e11934')

build() {
  cd "${srcdir}/${pkgname}${pkgver/./_}/src/current"

  if [[ "${CARCH}" == 'x86_64' ]];
  then
    TARGET='-AMD64'
  else
    TARGET=''
  fi

  make linux${TARGET} CFLAGS="${CFLAGS}"
}

package() {
  cd "${srcdir}/${pkgname}${pkgver/./_}/src/current"
  install -D -m 755 -t ${pkgdir}/usr/bin/ iozone fileop pit_server

  cd "${srcdir}/${pkgname}${pkgver/./_}/docs"
  install -D -m 644 -t ${pkgdir}/usr/share/man/man1/ iozone.1
}
