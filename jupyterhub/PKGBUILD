# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=jupyterhub
pkgver=1.2.1
pkgrel=1
pkgdesc="Multi-user server for Jupyter notebooks "
url="http://jupyter.org/"
arch=(any)
license=('BSD')
depends=(
  'python>=3.8'
  'python<3.9'
  'python-alembic'
  'python-async_generator>=1.8'
  'python-certipy'
  'python-traitlets>=4.3.2'
  'python-tornado>=5.0'
  'python-jinja'
  'python-jupyter_telemetry'
  'python-pamela'
  'python-oauth2>=1.0'
  'python-dateutil'
  'python-sqlalchemy>=1.1'
  'python-requests'
  'python-prometheus_client>=0.0.21'
)
makedepends=('bower' 'npm')
source=("jupyterhub-${pkgver}.tar.gz::https://github.com/jupyterhub/jupyterhub/archive/${pkgver}.tar.gz")
sha512sums=('3364240a8822bed4128a464e38283151070bbba1420dd23194e4fc9800dd34097632ac9196db2e497878c4036898258fa5a8281543a288e766a9b20e9bfff77b')
install=${pkgname}.install

build() {
  cd "${srcdir}/jupyterhub-$pkgver"
  python setup.py build
} 

package() {
  cd "${srcdir}/jupyterhub-$pkgver"
  python setup.py install --root="${pkgdir}"
  install -Dm644 COPYING.md "${pkgdir}"/usr/share/licenses/$pkgname/COPYING.md
  find ${pkgdir} -name SOURCES.txt -exec sed -i "s|^${srcdir}/||" {} \;
}
