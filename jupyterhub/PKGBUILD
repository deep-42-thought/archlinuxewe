# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=jupyterhub
pkgver=0.4.1
pkgrel=1
pkgdesc="Multi-user server for Jupyter notebooks "
url="http://jupyter.org/"
arch=(any)
license=('BSD')
depends=('python-tornado' 'python-jinja' 'ipython' 'python-pamela' 'python-sqlalchemy' 'python-requests' 'nodejs-configurable-http-proxy')
makedepends=('bower' 'npm')
source=("https://github.com/jupyterhub/jupyterhub/archive/${pkgver}.tar.gz")
sha512sums=('6f37f1ebf1306a08516072fb78ab006a35bbdd84936945bf734bc9e8ae1d4d41f958e9e11548cdeaa4ce5e4b7accf2b4dd42f4406ec50b40c96b3140c7e208a9')

build() {
  cd "${srcdir}/jupyterhub-$pkgver"
  python setup.py build
} 

package() {
  cd "${srcdir}/jupyterhub-$pkgver"
  python setup.py install --root="${pkgdir}"
  install -Dm644 COPYING.md "${pkgdir}"/usr/share/licenses/$pkgname/COPYING.md
}

