# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=jupyterhub
pkgver=0.9.0
pkgrel=1
pkgdesc="Multi-user server for Jupyter notebooks "
url="http://jupyter.org/"
arch=(any)
license=('BSD')
depends=(
  'python>=3.6'
  'python<3.7'
  'python-alembic'
  'python-async_generator>=1.8'
  'python-traitlets>=4.3.2'
  'python-tornado>=5.0'
  'python-jinja'
  'python-pamela'
  'python-oauth2>=1.0'
  'python-dateutil'
  'python-sqlalchemy>=1.1'
  'python-requests'
  'python-prometheus_client>=0.0.21'
)
makedepends=('bower' 'npm')
source=("jupyterhub-${pkgver}.tar.gz::https://github.com/jupyterhub/jupyterhub/archive/${pkgver}.tar.gz")
sha512sums=('895322be725691ad9f7cc41418efbd3ca5ffe3cca33d27c3e1d0ca9b8acd0170e0c8dfed62a113beedfa52c50a1f63911a1216cce8e559939da7d5e4c4cb3a55')
install=${pkgname}.install

build() {
  cd "${srcdir}/jupyterhub-$pkgver"
  python setup.py build
} 

package() {
  cd "${srcdir}/jupyterhub-$pkgver"
  python setup.py install --root="${pkgdir}"
  install -Dm644 COPYING.md "${pkgdir}"/usr/share/licenses/$pkgname/COPYING.md
  find ${pkgdir} -name SOURCES.txt -exec sed -i "s|^${srcdir}/||" {} \;
}
