# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=jupyterhub
pkgver=1.1.0
pkgrel=2
pkgdesc="Multi-user server for Jupyter notebooks "
url="http://jupyter.org/"
arch=(any)
license=('BSD')
depends=(
  'python>=3.8'
  'python<3.9'
  'python-alembic'
  'python-async_generator>=1.8'
  'python-certipy'
  'python-traitlets>=4.3.2'
  'python-tornado>=5.0'
  'python-jinja'
  'python-jupyter_telemetry'
  'python-pamela'
  'python-oauth2>=1.0'
  'python-dateutil'
  'python-sqlalchemy>=1.1'
  'python-requests'
  'python-prometheus_client>=0.0.21'
)
makedepends=('bower' 'npm')
source=("jupyterhub-${pkgver}.tar.gz::https://github.com/jupyterhub/jupyterhub/archive/${pkgver}.tar.gz")
sha512sums=('4d7562d9d37bf5eac5988c0ad8d8887d0815c6bd249c42074ee629531de63e8820a65533421b341fa6664ce888c46bb6e7446a4bd2709c4110379e8586569d27')
install=${pkgname}.install

build() {
  cd "${srcdir}/jupyterhub-$pkgver"
  python setup.py build
} 

package() {
  cd "${srcdir}/jupyterhub-$pkgver"
  python setup.py install --root="${pkgdir}"
  install -Dm644 COPYING.md "${pkgdir}"/usr/share/licenses/$pkgname/COPYING.md
  find ${pkgdir} -name SOURCES.txt -exec sed -i "s|^${srcdir}/||" {} \;
}
