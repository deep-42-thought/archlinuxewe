# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=jupyterhub
pkgver=1.4.2
pkgrel=29
pkgdesc="Multi-user server for Jupyter notebooks "
url="http://jupyter.org/"
arch=(any)
license=('BSD')
depends=(
  'python>=3.9'
  'python<3.10'
  'python-alembic'
  'python-async_generator>=1.8'
  'python-certipy'
  'python-traitlets>=4.3.2'
  'python-tornado>=5.0'
  'python-jinja'
  'python-jupyter_telemetry'
  'python-pamela'
  'python-oauth2>=1.0'
  'python-dateutil'
  'python-sqlalchemy>=1.1'
  'python-requests'
  'python-prometheus_client>=0.0.21'
)
makedepends=('bower' 'npm')
source=("jupyterhub-${pkgver}.tar.gz::https://github.com/jupyterhub/jupyterhub/archive/${pkgver}.tar.gz")
sha512sums=('92a136949450215ba1b833f8299443f31f43e229e2c2bfc18b5771ef1026a19f777e22a180d17e0a4c45b55e0c98d1592c035f0db43b193f8ae01a533fd119d7')
install=${pkgname}.install

build() {
  cd "${srcdir}/jupyterhub-$pkgver"
  python setup.py build
} 

package() {
  cd "${srcdir}/jupyterhub-$pkgver"
  python setup.py install --root="${pkgdir}"
  install -Dm644 COPYING.md "${pkgdir}"/usr/share/licenses/$pkgname/COPYING.md
  find ${pkgdir} -name SOURCES.txt -exec sed -i "s|^${srcdir}/||" {} \;
}
