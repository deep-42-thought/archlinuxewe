# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=jupyterhub
pkgver=0.8.0
pkgrel=1
pkgdesc="Multi-user server for Jupyter notebooks "
url="http://jupyter.org/"
arch=(any)
license=('BSD')
depends=('python>=3.6' 'python<3.7' 'python-alembic' 'python-tornado' 'python-jinja' 'ipython' 'python-pamela' 'python-sqlalchemy' 'python-requests' 'nodejs-configurable-http-proxy')
makedepends=('bower' 'npm')
source=("https://github.com/jupyterhub/jupyterhub/archive/${pkgver}.tar.gz")
sha512sums=('72874a311fb4e413ca53a91a31942f20fc798eacf5eb7b6673d566697cf8fb094caecf2e58efa33a0c48bbf75d64e7fd814c8768cd89e64c8a14e06dbfa3dbe9')
install=${pkgname}.install

build() {
  cd "${srcdir}/jupyterhub-$pkgver"
  python setup.py build
} 

package() {
  cd "${srcdir}/jupyterhub-$pkgver"
  python setup.py install --root="${pkgdir}"
  install -Dm644 COPYING.md "${pkgdir}"/usr/share/licenses/$pkgname/COPYING.md
  find ${pkgdir} -name SOURCES.txt -exec sed -i "s|^${srcdir}/||" {} \;
}
