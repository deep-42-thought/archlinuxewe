# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
#
# namcap warns that 'libcups' is an unnecessary dependency. This is untrue.
# Splitter will produce the following exception if libcups is not installed:
#
#     Exception in thread "main" java.lang.UnsatisfiedLinkError:
#     /usr/lib/jvm/java-7-openjdk/jre/lib/amd64/headless/libmawt.so:
#     libcups.so.2: cannot open shared object file: No such file or directory
#
# namcap also warns that 'java-environment' is a dependency. This is untrue:
# java-runtime suffices.
#

pkgname=splitter
pkgver=r651
pkgrel=1
pkgdesc='Utility for splitting OpenStreetMap maps into tiles.'
url='http://www.mkgmap.org.uk/'
license=(GPL)
arch=(any)
depends=(java-runtime sh libcups)
source=("https://www.mkgmap.org.uk/download/${pkgname}-${pkgver}.zip"
        "${pkgname}")
sha512sums=('7286c13345b068bd22517664ad52720726ce3a143022f82de7b8076875a79eb4f04de17e3888fb339f3aec0238a5c4b4ee7203f983952ac97e88e696c972faec'
            '0b11b6024ac932b27d587a6322722af47a1d05eeebeee38765692e24c89f7bc94431b222c7e6af8d3aa5cc4f99c90036669c7b6acdca2a8779e5b7824c64d1cf')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install the executable and jar file.
  install -Dm755 "${srcdir}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "${pkgname}.jar" \
    "${pkgdir}/usr/share/java/${pkgname}/${pkgname}.jar"

  # Install libraries. Destination dir created when installing jar file.
  cp -r lib/ "${pkgdir}/usr/share/java/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
