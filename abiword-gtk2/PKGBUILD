# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Richard Jackson <rj@iinet.net.au>
pkgname=abiword-gtk2
_pkgname=abiword
pkgver=3.0.5
pkgrel=18
pkgdesc='Fully-featured word processor, GTk2, No plugins, Lite version'
arch=('i686' 'pentium4' 'x86_64')
license=('GPL')
_pinned_dependencies=(
  'cairo=1.17.6'
  'gcc-libs=11.2.0'
  'glibc>=2.31'
  'libatk-1.0.so=0'
  'libfontconfig.so=1'
  'libfreetype.so=6'
  'libfribidi.so=0'
  'libgdk_pixbuf-2.0.so=0'
  'libgdk-x11-2.0.so=0'
  'libgio-2.0.so=0'
  'libglib-2.0.so=0'
  'libgobject-2.0.so=0'
  'libgsf-1.so=114'
  'libgthread-2.0.so=0'
  'libgtk-x11-2.0.so=0'
  'libharfbuzz.so=libharfbuzz.so'
  'libjpeg.so=8'
  'libpango-1.0.so=0'
  'libpangocairo-1.0.so=0'
  'libpangoft2-1.0.so=0'
  'libpng=1.6.37'
  'librsvg-2.so=2'
  'libx11=1.8'
  'libxml2.so=2'
  'libxslt.so=1'
  'zlib=1:1.2.12'
)
depends=('wv' 'enchant' 'desktop-file-utils' "${_pinned_dependencies[@]}")
makedepends=('pkgconfig' 'boost' 'autoconf-archive')
conflicts=('abiword' 'abiword-plugins')
url='http://www.abisource.com'
source=("$_pkgname-$pkgver.tar.gz::https://www.abisource.com/downloads/$_pkgname/$pkgver/source/$_pkgname-$pkgver.tar.gz"
        enchant-2.1.patch)
sha512sums=('a2484268901ff47307c9d1f1928622e364f1006f22ce38257c585144df9411dfe3c2dea28c1f1f50a6e545e8cc579cce34117a89dfa771e20312e3ea1a9989d6'
            '323d4695b2b63ecb62ac16399853bdcacc96f67deb4e58408eab727e113ed593d2d3c5b90951c7653c2ddac6a14950162e314a461b592e2c95df0294459cbe96')

prepare() {
  cd $_pkgname-$pkgver

  # Replace deprecated enchant functions
  patch -Np1 -i ../enchant-2.1.patch
}

build() {
  cd $_pkgname-$pkgver
#  libtoolize --force
#  aclocal
#  autoheader
#  automake --add-missing --force-missing
#  autoconf || autoconf
#  autoreconf -fi || autoreconf -i || autoreconf -i 
  autoreconf -fi
  ./configure --prefix=/usr \
    --enable-shared \
    --disable-static \
    --with-gtk2 \
    --enable-clipart \
    --enable-templates \
    --without-redland \
    --without-libical \
    --disable-builtin-plugins \
    --disable-default-plugins \
    --disable-collab-backend-service \
    --disable-collab-backend-tcp
  make -C goffice-bits2
  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
