# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor:Jason St. John <jstjohn .. purdue . edu>
# Contributor: AUR Perl <aurperl@juster.info>

_perlmod=File-ShareDir-Install
pkgname=perl-file-sharedir-install
pkgver=0.10
pkgrel='2'
pkgdesc="File::ShareDir::Install - Install read-only data files from a distribution"
arch=('any')
url="http://search.cpan.org/dist/${_perlmod}"
license=('GPL' 'PerlArtistic')
depends=(
  'perl-extutils-makemaker>=6.11'
  'perl-io'
  'perl-pathtools')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
_distdir="${_perlmod}-${pkgver}"
source=("http://cpan.org/modules/by-module/${_perlmod%%-*}/${_distdir}.tar.gz")
sha512sums=('96c3a94da78e51c97223f16892c6bdb6a93f9602643aca6ee73fd661aefb0a018938f03b8265f255dc3daba467e01a2d2a924f33a5e1e4d2e9c9fe2698a3d09f')

build() {
	cd "${_distdir}"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "${_distdir}"
	make test
}

package() {
	cd "${_distdir}"
	make install DESTDIR="${pkgdir}"
}
