# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Leonid B <leonid dot bloch at esrf dot fr>
# Upstream contact: silx at esrf dot fr
pkgname=python-silx
_pkgname=silx
pkgver=0.13.1
pkgrel=1
pkgdesc="A collection of Python packages for data analysis at synchrotron radiation facilities."
arch=('i686' 'pentium4' 'x86_64')
url="http://www.silx.org"
license=('MIT' 'LGPL')
_pinned_dependencies=(
  'gcc-libs=10.1.0'
  'glibc>=2.31'
)
depends=(
  "${_pinned_dependencies[@]}"
  'cython>=0.21.1'
  'python>=3.8'
  'python<3.9'
  'python-pyopencl'
  'python-fabio>=0.7'
  'python-h5py'
  'python-mako'
  'python-matplotlib>=1.2.0'
  'python-numpy>=1.12'
  'python-pillow'
  'python-pyqt5'
  'python-dateutil'
  'python-qtconsole'
  'python-scipy'
  'python-setuptools'
  'python-six')
makedepends=(
  'python-setuptools')
optdepends=('python-h5py: for HDF5 input/output'
            'ipython: for interactive console'
            'python-qtconsole: for GUI console'
            'python-pyopencl: for sift - OpenCL implementation'
            'opencl-driver: for sift - OpenCL implementation')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/silx-kit/${_pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('5a9add4c5e7502ff5ec00a57bf256aee26e91d1597562caf9fb994cf6e7a1c384f1f88f6cb02bea245ffec14ce6d8fedbb083bf5ea84da0a515036f7c8f5d90e')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

package_python-silx() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1
  install -D copyright "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
