# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Leonid B <leonid dot bloch at esrf dot fr>
# Upstream contact: silx at esrf dot fr
pkgbase=python-silx
pkgname=(python2-silx python-silx)
_pkgname=silx
pkgver=0.10.0
pkgrel=1
pkgdesc="A collection of Python packages for data analysis at synchrotron radiation facilities."
arch=('i686' 'x86_64')
url="http://www.silx.org"
license=('MIT' 'LGPL')
_deppy2=(
  'python2>=2.7'
  'python2<2.8'
  'python2-enum34'
  'python2-futures'
)
_deppy=(
  'python>=3.7'
  'python<3.8'
)
_depends=(
  'python-fabio>=0.7'
  'python-h5py'
  'python-mako'
  'python-matplotlib>=1.2.0'
  'python-numpy>=1.8'
  'python-pillow'
  'python-pyopencl'
  'python-pyqt5'
  'python-dateutil'
  'python-qtconsole'
  'python-scipy'
  'python-setuptools'
  'python-six')
_makedepends=(
  'cython'
  'python-setuptools')
_optdepends=('python-h5py: for HDF5 input/output'
            'ipython: for interactive console'
            'python-qtconsole: for GUI console'
            'python-pyopencl: for sift - OpenCL implementation'
            'opencl-driver: for sift - OpenCL implementation')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/silx-kit/${_pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('ae284f2cf8be5439a9f2ff6e6221dfd6ded2737f7e9d8e324d685a8049def4a4bdd2bccbd0c85e22bf1a3b0a51ef128c61ed25cad8fa27b8b21740105db12f55')

makedepends=(
  "${_deppy[@]}"
  "${_deppy2[@]}"
  "${_depends[@]}"
  "${_depends[@]//python/python2}"
  "${_makedepends[@]}"
  "${_makedepends[@]//ython/ython2}"
)

prepare() {
  cp -r "${_pkgname}-${pkgver}" "py2-${_pkgname}-${pkgver}"
  mv "${_pkgname}-${pkgver}" "py-${_pkgname}-${pkgver}"
}

build() {
  cd "${srcdir}/py-${_pkgname}-${pkgver}"
  python setup.py build

  cd "${srcdir}/py2-${_pkgname}-${pkgver}"
  python2 setup.py build
}

package_python2-silx() {
  depends=(
    "${_deppy2[@]}"
    "${_depends[@]//python/python2}"
  )
  optdepends=(
    "${_optdepends[@]//python/python2}"
  )
  cd "${srcdir}/py2-${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -D copyright "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  mv "${pkgdir}/usr/bin/silx" "${pkgdir}/usr/bin/silx2"
  sed -i '1 s/python/python2/' "${pkgdir}/usr/bin/silx2"
  mv \
    "${pkgdir}/usr/silx/third_party/_local/scipy_spatial/qhull/COPYING.txt" \
    "${pkgdir}/usr/silx/third_party/_local/scipy_spatial/qhull/COPYING2.txt"
}

package_python-silx() {
  depends=(
    "${_deppy[@]}"
    "${_depends[@]}"
  )
  optdepenends=(
    "${_optdepends[@]}"
  )
  cd "${srcdir}/py-${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1
  install -D copyright "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
