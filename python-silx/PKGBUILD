# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Leonid B <leonid dot bloch at esrf dot fr>
# Upstream contact: silx at esrf dot fr
pkgname=python-silx
_pkgname=silx
pkgver=0.15.1
pkgrel=1
pkgdesc="A collection of Python packages for data analysis at synchrotron radiation facilities."
arch=('i686' 'pentium4' 'x86_64')
url="http://www.silx.org"
license=('MIT' 'LGPL')
_pinned_dependencies=(
  'gcc-libs=11.1.0'
  'glibc>=2.31'
)
depends=(
  "${_pinned_dependencies[@]}"
  'cython>=0.21.1'
  'python<3.10'
  'python>=3.9'
  'python-dateutil'
  'python-fabio>=0.9'
  'python-h5py'
  'python-mako'
  'python-matplotlib>=1.2.0'
  'python-numpy'
  'python-numpy>=1.12'
  'python-pillow'
  'python-pyopencl'
#  'python-pyopengl'
  'python-pyqt5'
  'python-qtconsole'
  'python-scipy'
  'python-setuptools'
  'python-six')
makedepends=(
  'python-setuptools')
optdepends=('python-h5py: for HDF5 input/output'
            'ipython: for interactive console'
            'python-qtconsole: for GUI console'
            'python-pyopencl: for sift - OpenCL implementation'
            'opencl-driver: for sift - OpenCL implementation')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/silx-kit/${_pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('f2b4f61413402b5684c8e410a146bbe5b7fca39b730530196822fd1dc1ec36a9624f841ee1fe2fb1a82ab72dfc56be735849e09f5a5a285a62c1861c37ced19f')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

package_python-silx() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1
  install -D copyright "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
