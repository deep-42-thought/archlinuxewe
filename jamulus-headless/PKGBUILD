# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Jose Riha <jose1711 gmail com>
# Contributor: Jujudusud <jujudusud gmail com>

pkgname=jamulus-headless
_pkgname=Jamulus
pkgver=3.8.1
pkgrel=28
pkgdesc="Internet jam session software (headless server)"
arch=('i686' 'pentium4' 'x86_64')
url='http://llcon.sourceforge.net/'
install=jamulus.install
license=('GPL2')
_pinned_dependencies=(
  'gcc-libs=11.2.0'
  'glibc>=2.32'
  'qt5-base=5.15.2+kde+r300'
)
depends=("${_pinned_dependencies[@]}")
provides=('jamulus')
source=("jamulus-${pkgver}.tar.gz::https://github.com/jamulussoftware/jamulus/archive/r${pkgver//./_}.tar.gz"
        "jamulus.service")

build() {
  cd "${srcdir}/jamulus-r${pkgver//./_}"
  qmake "CONFIG+=nosound headless" Jamulus.pro
  make clean
  make
}

package() {
  cd "${srcdir}/jamulus-r${pkgver//./_}"
  install -Dm755 Jamulus "${pkgdir}/usr/bin/Jamulus-headless"
  install -vDm 644 {ChangeLog,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 $srcdir/jamulus.service $pkgdir/usr/lib/systemd/system/jamulus.service
}

sha512sums=('fef0637427498634f8570073be45c9145e22f9a20ebbd4c2c3776dcb460151efdd634140fed7b4769c3cd9386a33e29950624135ea96cb528faa06a769f1978d'
            'f59901ad1fc9f3108beb0f70cbb4b5f9e49d3148727a5869cdf3ba878a88cae069a09135b7d976d4d5060a64f038d7720eba45058f96ee003b47383cc2ecdf3a')
