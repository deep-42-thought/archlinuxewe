# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Jose Riha <jose1711 gmail com>
# Contributor: Jujudusud <jujudusud gmail com>

pkgname=jamulus-headless
_pkgname=Jamulus
pkgver=3.8.2
pkgrel=22
pkgdesc="Internet jam session software (headless server)"
arch=('i686' 'pentium4' 'x86_64')
url='http://llcon.sourceforge.net/'
install=jamulus.install
license=('GPL2')
_pinned_dependencies=(
  'gcc-libs=12.1.0'
  'glibc>=2.32'
  'qt5-base=5.15.4+kde+r168'
)
depends=("${_pinned_dependencies[@]}")
provides=('jamulus')
source=("jamulus-${pkgver}.tar.gz::https://github.com/jamulussoftware/jamulus/archive/r${pkgver//./_}.tar.gz"
        "jamulus.service")

build() {
  cd "${srcdir}/jamulus-r${pkgver//./_}"
  qmake "CONFIG+=nosound headless" Jamulus.pro
  make clean
  make
}

package() {
  cd "${srcdir}/jamulus-r${pkgver//./_}"
  install -Dm755 Jamulus "${pkgdir}/usr/bin/Jamulus-headless"
  install -vDm 644 {ChangeLog,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 $srcdir/jamulus.service $pkgdir/usr/lib/systemd/system/jamulus.service
}

sha512sums=('8630cbcbfbdc2051ecbbd5158211efce2f2e47ae18413cb7e549bbe8909110ff13ab8ac2dbacd8c41a88b38d9988ea7c3955a6fec25a1eb18170ab6d792063c7'
            'f59901ad1fc9f3108beb0f70cbb4b5f9e49d3148727a5869cdf3ba878a88cae069a09135b7d976d4d5060a64f038d7720eba45058f96ee003b47383cc2ecdf3a')
