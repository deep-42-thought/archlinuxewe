# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=python-jira
_pkgname="${pkgname#*-}"
pkgver=3.2.0
pkgrel=1
pkgdesc="Python library to work with Jira APIs"
arch=(any)
url="https://github.com/pycontribs/jira"
license=('BSD')
depends=(
  'python>=3.10'
  'python<3.11')
makedepends=(
  'python-pip'
  'python-setuptools')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/pycontribs/jira/archive/refs/tags/$pkgver.tar.gz")
sha512sums=('16609a51c442a99fadd45e7d871cd0a0bd4f592c1a477b8cff64d53319eb0b270f1952080108de5f84e81d1b1edf75192bb66e78509ac8c7079d3e12360c19d6')

prepare() {
  cd "${srcdir}/jira-${pkgver}"
  sed -i '
    s@\(setuptools.setup(\)use_scm_version=True)@\1version='"'${pkgver}'"')@
  ' setup.py
}

package() {
  cd "${srcdir}/jira-${pkgver}"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
