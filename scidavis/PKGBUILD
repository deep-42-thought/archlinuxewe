# Maintainer:  Erich Eckner <arch at eckner dot net>
# Contributor: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Knut Franke <Knut.Franke ad gmx dot de>
# Contributor: Olaf Leidinger <leidola at newcon dot de>
# Contributor: Robal <nigrantis.tigris at gmail dot com>
# Contributor: Alexander <chronischzuspaet at gmail dot com> Kempen

pkgname=scidavis
pkgver=1.26
pkgrel=6
pkgdesc="Application for Scientific Data Analysis and Visualization, fork of QtiPlot"
arch=('i686' 'pentium4' 'x86_64')
url="http://scidavis.sourceforge.net/"
license=('GPL')
makedepends=('boost' 'cmake')
_pinned_dependencies=(
  'gcc-libs=10.1.0'
  'glibc>=2.31'
  'glu=9.0.1'
  'gsl=2.6'
  'libglvnd=1.3.1'
  'libQtCore.so=4'
  'libQtGui.so=4'
  'libQtOpenGL.so=4'
  'libQtSvg.so=4'
  'libqwt5.so=1'
  'libqwtplot3d.so=0'
  'muparser=2.2.6'
  'zlib=1:1.2.11'
)
depends=('mesa' 'python-qtpy' 'shared-mime-info' "${_pinned_dependencies[@]}")
source=("http://prdownloads.sourceforge.net/sourceforge/scidavis/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5f2ca9853a1f4b777520655caf07910e279e804a11569eef8ceee4d29e217f1b93d069d2d290ee9e0867fbeddbdd36969a2de43a9fad6ef5789abdae08f2a2cd')
install="${pkgname}.install"

build() {
  cd "${pkgname}-${pkgver}"
  qmake-qt4
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}" DESTDIR="${pkgdir}" install

  # remove liborigin files since it uses static library
  rm -rf "${pkgdir}/usr/local"

  # install desktop file and icon
  install -Dm644 "scidavis/scidavis.desktop" "${pkgdir}/usr/share/applications/scidavis.desktop"
  install -Dm644 "scidavis/icons/scidavis.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/scidavis.svg"
}

