# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Ronald van Haren <ronald@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=qwtplot3d
pkgver=0.2.7
pkgrel=10
pkgdesc="Qt/OpenGL-based C++ programming library containing 3d-widgets"
arch=('i686' 'pentium4' 'x86_64')
license=('custom:zlib')
url="http://qwtplot3d.sourceforge.net/"
depends=('qt5-base')
makedepends=('mesa' 'glu')
source=("https://downloads.sourceforge.net/sourceforge/qwtplot3d/qwtplot3d-$pkgver.tgz"
        qwtplot3d-gcc44.patch qwtplot3d-qt-4.8.0.patch)
sha512sums=('88e6d270695931775f3c1c0e718118e31118dbfe9f5f582834de09ab515fdf9e8a0f90d424f276653fdf1cf0b39e2060282385701a4ab48843420fe55a1dcf0c'
            '7524277d653dc723537ca71a335eb4109d94c18f5ee2447417e13aa639aafda0b85733fa86305759458f23f189b7a8476bc5db48abfd86607b1f69f9a74431b6'
            'fccdb603f8c0075353a817eb47851aa6929f70d066f37a1d1e314253ed38a730bd821d748852a2df271c81c235003bdf7cc8c03edff8ac211a21fd1dbb41fbe0')

prepare() {
    cd ${pkgname}
    patch -p1 -i "${srcdir}"/qwtplot3d-gcc44.patch
    patch -p1 -i "${srcdir}"/qwtplot3d-qt-4.8.0.patch
}

build() {
    cd ${pkgname}
    # build qwt:
    qmake qwtplot3d.pro
    make
}

package() {
    cd ${pkgname}

    # install qwtplot3d: (by hand, because the Makefile do not provide a "install:")
    install -d "${pkgdir}"/usr/{include/qwtplot3d,lib}

    for n in include/* ; do
     cp -d $n "${pkgdir}"/usr/include/qwtplot3d
    done

    for n in lib/libqwtplot3d.so* ; do
     cp -d $n "${pkgdir}"/usr/lib
    done

    # install custom license
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

