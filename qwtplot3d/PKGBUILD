# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Ronald van Haren <ronald@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=qwtplot3d
pkgver=0.2.7
pkgrel=104
pkgdesc="Qt/OpenGL-based C++ programming library containing 3d-widgets"
arch=('i686' 'pentium4' 'x86_64')
license=('custom:zlib')
url="http://qwtplot3d.sourceforge.net/"
_pinned_dependencies=(
  'gcc-libs=12.1.0'
  'glibc>=2.31'
  'libglvnd=1.4.0'
  'qt5-base=5.15.5+kde+r163'
  'qt5-base=5.15.5+kde+r163'
  'zlib=1:1.2.12'
)
depends=("${_pinned_dependencies[@]}")
makedepends=('mesa' 'glu')
source=("https://downloads.sourceforge.net/sourceforge/qwtplot3d/qwtplot3d-$pkgver.tgz"
        qwtplot3d-gcc44.patch.gz qwtplot3d-qt-4.8.0.patch.gz)
sha512sums=('88e6d270695931775f3c1c0e718118e31118dbfe9f5f582834de09ab515fdf9e8a0f90d424f276653fdf1cf0b39e2060282385701a4ab48843420fe55a1dcf0c'
            '76100928088662adc0cc9258844750cacac28b318262affae3f498183e4784fc94254c68bd25331addc90d537d3f6ee42938febae6b017acebdf7a7be7a3e81a'
            '0f2f15acee7fe408e976fa61663e68be0dfd1a6521aaf22863c17a3b3c5c4cc2c5bf0c1177bf0d0e32e0e0c4386b22d1dac8c35dbc312a23f021e9204b03a1d2')

prepare() {
    cd ${pkgname}
    zcat "${srcdir}"/qwtplot3d-gcc44.patch.gz | patch -p1
    zcat "${srcdir}"/qwtplot3d-qt-4.8.0.patch.gz | patch -p1
}

build() {
    cd ${pkgname}
    # build qwt:
    qmake qwtplot3d.pro
    make
}

package() {
    cd ${pkgname}

    # install qwtplot3d: (by hand, because the Makefile do not provide a "install:")
    install -d "${pkgdir}"/usr/{include/qwtplot3d,lib}

    for n in include/* ; do
     cp -d $n "${pkgdir}"/usr/include/qwtplot3d
    done

    for n in lib/libqwtplot3d.so* ; do
     cp -d $n "${pkgdir}"/usr/lib
    done

    # install custom license
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

