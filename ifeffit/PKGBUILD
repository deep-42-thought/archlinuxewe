# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='ifeffit'
pkgver='1.2.13'
pkgrel=5
pkgdesc="a suite of interactive programs for XAFS analysis"
arch=('x86_64' 'i686' 'pentium4')
license=('GPL')
options=('!emptydirs')
depends=('pgplot')
makedepends=('libx11' 'ncurses' 'libpng' 'wxgtk' 'gcc-fortran' 'xorg-server-devel')
url='http://cars.uchicago.edu/ifeffit/Ifeffit'
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/newville/ifeffit/archive/${pkgver}.tar.gz")
sha512sums=('c903228f4f20f161722e57d34631ceee785293604afd15647ef4a5c456207e8bd3425f72b46c1d2995f3df316f93a16cef8d65c2bba8c279b47896440b106cf4')

prepare() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --with-arraysize=huge \
    --with-pgplot-link='-L/usr/lib -lpgplot -lpng -lz -lX11'
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
