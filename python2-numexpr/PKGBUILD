# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Sebastien Binet <binet@lblbox>

pkgname=python2-numexpr
pkgver=2.7.1
pkgrel=2
pkgdesc="Fast numerical array expression evaluator for Python, NumPy, PyTables, pandas"
url="https://github.com/pydata/numexpr"
arch=('x86_64' 'i686' 'pentium4')
license=('MIT')
depends=(
  'python2>=2.7'
  'python2<2.8'
  'python2-numpy'
)
makedepends=('python2-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pydata/numexpr/archive/v$pkgver.tar.gz")
sha512sums=('ac6768b2968e565c8b7a63a3c7809df9dd6e6076e5c74cb50e5bac706aaed2731fcdef7216ae36bff5caf0cf2d11cb45f2df9cad56e6ab5d07d327f0f4a7185c')

build() {
  cd "$srcdir"/numexpr-$pkgver
  python2 setup.py build
}
package() {
  cd "$srcdir"/numexpr-$pkgver
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
