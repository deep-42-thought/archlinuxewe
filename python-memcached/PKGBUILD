# Maintainer: Erich Eckner <arch at eckner dot net>
pkgbase=python-memcached
pkgname=(python-memcached python2-memcached)
pkgver=1.59
pkgrel=1
pkgdesc='100% Python interface to the memcached memory cache daemon'
url='https://github.com/linsomniac/python-memcached'
arch=('any')
license=('MIT')
_depends=(
  'python-six>=1.4.0'
)
_mdepends=(
  'python'
  'python-nose'
  'python-coverage'
  'python-hacking'
)
_cdepends=(
  'python-mock'
  'python-nose'
  'python-coverage'
  'python-hacking'
  'memcached'
)
_optdepends=(
  'python-memcached'
  'python-pymongo'
  'python-redis'
)
makedepends=(
  "${_depends[@]}"
  "${_depends[@]/python/python2}"
  "${_mdepends[@]}"
  "${_mdepends[@]/python/python2}"
)
checkdepends=(
  "${_cdepends[@]}"
  "${_cdepends[@]/python/python2}"
)
source=("https://github.com/linsomniac/python-memcached/archive/${pkgver}.tar.gz")
sha512sums=('d7ff45a329f2a9bf97fdc7c0268c2c67046c3501270fcf03578b955c2da35904d7bdecd4239924d390797ddff8f4cc69fc5743f4d4f663cdb9f2f8c7e8159512')

prepare() {
  cp -a python-memcached-${pkgver}{,-py2}
}

build() {
  msg2 "Building python..."
  (
    cd python-memcached-${pkgver}
    python setup.py build
  )
  msg2 "Building python2..."
  (
    cd python-memcached-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  msg2 "starting memcached ..."
  memcached -l 127.0.0.1 -o modern &
  _memcached_pid=$!
  sleep 1
  msg2 "Checking python..."
  (
    cd python-memcached-${pkgver}
    python setup.py test
  )
  msg2 "Checking python2..."
  (
    cd python-memcached-${pkgver}-py2
    python2 setup.py test
  )
  msg2 "killing memcached ..."
  kill ${_memcached_pid}
}

package_python-memcached() {
  _pyVer=$(python -V 2>&1 | cut -d' ' -f2 | cut -d. -f2)
  depends=(
    "python>=3.${_pyVer}"
    "python<3.$((_pyVer+1))"
    "${_depends[@]}"
  )
  optdepends=("${_optdepends[@]}")

  cd python-memcached-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build
}

package_python2-memcached() {
  _pyVer=$(python2 -V 2>&1 | cut -d' ' -f2 | cut -d. -f2)
  depends=(
    "python2>=2.${_pyVer}"
    "python2<2.$((_pyVer+1))"
    "${_depends[@]}"
    "${_depends[@]/python/python2}"
  )
  optdepends=("${_optdepends[@]/python/python2}")

  cd python-memcached-${pkgver}-py2
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build
}
