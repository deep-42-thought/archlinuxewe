# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Charles Bos <charlesbos1 AT gmail>
# Contributor: Fabian Kosmale <0inkane@googlemail.com>
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>
# Contributor: alejandronova

pkgname=strigi
pkgver=0.7.8
pkgrel=10
pkgdesc="Fast crawling desktop search engine with Qt4 GUI"
arch=('i686' 'x86_64')
url="https://www.vandenoever.info/software/strigi/"
license=('GPL2')
depends=('bzip2' 'exiv2' 'libxml2' 'boost-libs' 'ffmpeg')
makedepends=('qt4' 'cmake' 'boost')
source=("https://www.vandenoever.info/software/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        "gcc48.patch"
        "ffmpeg3.patch")
sha512sums=('536f5bd5ed13d87d30c2fa31b6dcb2a883df173e5d60deefe0fdeb843f40f29f7edd0041029dc013bb31a205572087ed95ca661bcdf6d989d9806931716b1043'
            '75638d26f5f01e025a9f940fce5040cde8deb0956d1124820db575bb984f5eec7e24e3657737ab0b96c0ce5fd20604bd9d08f21227fce971cc3ac67d4d8c8399'
            '94a5217553cac987ba4159f1d70774f382e1c96bcc9a6f14275ec1a7bcf59f6a699cc86253babd11b81af515462bba9384429ee07cce17d0ac66462826245531')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/ffmpeg3.patch"
  cd libstreams
  patch -p1 -i "${srcdir}"/gcc48.patch
}

build() {
  mkdir build; cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_INOTIFY=ON \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DENABLE_LOG4CXX=OFF \
    -DENABLE_FAM=OFF \
    -DENABLE_CLUCENE=OFF \
    -DENABLE_CLUCENE_NG=OFF \
    -DENABLE_FFMPEG=ON \
    -DFORCE_DEPS=ON
  make
}

package() {
  cd build/
  make DESTDIR="${pkgdir}" install
}
