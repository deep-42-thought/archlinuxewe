# $Id$
# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Partha Chowdhury <kira.laucas@gmail.com>

pkgname=conky-irc
pkgver=1.11.6
provides=("conky=${pkgver}")
conflicts=('conky')
pkgrel=9
pkgdesc='Lightweight system monitor for X - with irc-client enabled'
url='https://github.com/brndnmtthws/conky'
license=('BSD' 'GPL')
arch=('i686' 'pentium4' 'x86_64')
makedepends=('cmake' 'docbook2x' 'docbook-xml' 'man-db' 'git')
_pinned_dependencies=(
  'gcc-libs=10.2.0'
  'glibc>=2.31'
  'imlib2=1.7.0'
  'libcurl.so=4'
  'libircclient.so=1'
  'libncursesw.so=6'
  'libpulse.so=0'
  'libsystemd.so=0'
  'libx11=1.7.0'
  'libxdamage=1.1.5'
  'libxext=1.3.4'
  'libxfixes=5.0.3'
  'libxft=2.3.3'
  'libxinerama=1.1.4'
  'libxml2=2.9.10'
  'lua=5.4.1'
  'wireless_tools=30.pre9'
)
depends=('glib2' "${_pinned_dependencies[@]}")
optdepends=('mounted')
source=("${pkgname%-irc}-${pkgver}.tar.gz::https://github.com/brndnmtthws/conky/archive/v${pkgver}.tar.gz"
        'MAX_SP.patch')
sha512sums=('f7bffcef33680d61d0171ba50b29ddda011c1c1989fc847cd4b5b60d46bed4772294eec34fd27c4d642a9c03e3cedaa31855ffc0f3b60791f4bed14ffdf7490a'
            '1ea39f88b5f2620fc0c4485cc16645dbad91f97be17b4b13a732205dde6265d621c7f2bc92be33b65f9db2815312beb42b516ddebbba90cfe947104e1a410a52')

# options=('!strip' 'debug')

prepare() {
	cd "${srcdir}/${pkgname%-irc}-${pkgver}"
	patch -p1 -i ../MAX_SP.patch
	sed '/^#include / s,<libircclient\.h>,<libircclient/libircclient.h>,' -i 'src/irc.cc'
	sed '/check_include_files(/ s,libircclient\.h,libircclient/libircclient.h,' -i 'cmake/ConkyPlatformChecks.cmake'
}

build() {
	cd "${srcdir}/${pkgname%-irc}-${pkgver}"

	cmake \
		-D CMAKE_BUILD_TYPE=Release \
		-D MAINTAINER_MODE=ON \
		-D BUILD_WLAN=ON \
		-D BUILD_XDBE=ON \
		-D BUILD_XSHAPE=ON \
		-D BUILD_IMLIB2=ON \
		-D BUILD_CURL=ON \
		-D BUILD_RSS=ON \
		-D BUILD_WEATHER_METAR=ON \
		-D BUILD_WEATHER_XOAP=ON \
		-D BUILD_PULSEAUDIO=ON \
		-D BUILD_JOURNAL=ON \
		-D BUILD_IRC=ON \
		-D CMAKE_INSTALL_PREFIX=/usr \
		.

	make
}

package() {
	cd "${srcdir}/${pkgname%-irc}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 extras/vim/syntax/conkyrc.vim "${pkgdir}"/usr/share/vim/vimfiles/syntax/conkyrc.vim
	install -Dm644 extras/vim/ftdetect/conkyrc.vim "${pkgdir}"/usr/share/vim/vimfiles/ftdetect/conkyrc.vim
}
