# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Chris Severance aur.severach aATt spamgourmet dott com
# Contributor: RunningDroid <runningdroid AT zoho.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Soup <soup a.t soultrap d.o.t n.e.t>
# Contributor: Andres Perera <aepd87@gmail.com>

pkgname='checkbashisms'
pkgver=2.17.8
pkgrel=1
pkgdesc='Debian script that checks for bashisms'
arch=('any')
url='http://packages.qa.debian.org/d/devscripts.html'
license=('GPL')
depends=('perl')
makedepends=('git')
conflicts=('devscripts')
source=("git://anonscm.debian.org/collab-maint/devscripts.git#tag=v${pkgver}")
sha512sums=('SKIP')

build () {
  cd "${srcdir}/devscripts/scripts"

  sed -i -e "s,###VERSION###,${pkgver},g" checkbashisms.pl
}

package() {
  cd "${srcdir}/devscripts/scripts"

  install -Dpm755 checkbashisms.pl "${pkgdir}/usr/bin/checkbashisms"
  install -Dpm644 -t "${pkgdir}/usr/share/man/man1" checkbashisms.1
  install -Dpm644 checkbashisms.bash_completion "${pkgdir}/usr/share/bash-completion/completions/checkbashisms"
}
