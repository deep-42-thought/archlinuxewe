# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor:Stefan Göbel <aur —at— subtype —dot— de>

pkgname='perl-moosex-aliases'
_module='MooseX-Aliases'
_author='D/DO/DOY'
pkgver='0.11'
pkgrel=13
_distdir="${_module}-${pkgver}"
pkgdesc='Easy aliasing of methods and attributes in Moose.'
arch=('any')
url="https://metacpan.org/release/$_module"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl>=5.36'
  'perl<5.37'
  'perl-moose>=2.0000'
  'perl-moose-exporter'
  'perl-moose-role'
  'perl-moose-util-typeconstraints'
  'perl-scalar-list-utils')
makedepends=(
  'perl-extutils-makemaker>=6.30'
  'perl-file-temp'
  'perl-test-fatal'
  'perl-test-moose'
  'perl-test-simple>=0.88')
checkdepends=('perl-test-fatal' 'perl-test-output')
source=("http://search.cpan.org/CPAN/authors/id/$_author/${_distdir}.tar.gz")
sha512sums=('5a999c417ea59aa609ad6f0f3e9d9f18aaeeb683f2e05255d8577608f4699a49befcd42ecb968064f9164e8355edf460be220f1b92415d020d4ce122d580ffcc')

build() {
   (
      unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
      export PERL_MM_USE_DEFAULT='1' PERL_AUTOINSTALL='--skipdeps'
      cd "${_distdir}"
      /usr/bin/perl Makefile.PL
      make
   )
}

check() {
   (
      cd "${_distdir}"
      unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
      export PERL_MM_USE_DEFAULT='1'
      make test
   )
}

package() {
   (
      cd "${_distdir}"
      unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
      make install INSTALLDIRS='vendor' DESTDIR="$pkgdir"
      find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
   )
}

#:indentSize=3:tabSize=3:noTabs=true:mode=shellscript:maxLineLen=87:
