# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Leonid B <leonid dot bloch at esrf dot fr>
# Upstream contact: silx at esrf dot fr
pkgname=python-fabio
_pkgname=fabio
pkgver=0.10.1
pkgrel=1
pkgdesc="I/O library for images produced by 2D X-ray detectors."
arch=('i686' 'pentium4' 'x86_64')
url="http://www.silx.org"
license=('MIT' 'LGPL' 'Apache')
depends=(
  'cython'
  'python>=3.8'
  'python<3.9'
  'python-lxml'
  'python-numpy'
  'python-sphinx'
  'python-sphinxcontrib-programoutput')
makedepends=(
  'python-setuptools')
optdepends=('python-pyqt4: for the fabio_viewer program')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/silx-kit/${_pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('f22212c45a30013408a42f0f7359b07335d765c3393b52db766aa5ca2c56bd4414c8db147c7f125f346a0d1e6fc5a723457ff4a09ff17b8ba34b30c955f90ef7')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

package_python-fabio() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1
  install -D copyright "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
