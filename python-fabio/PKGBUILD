# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Leonid B <leonid dot bloch at esrf dot fr>
# Upstream contact: silx at esrf dot fr
pkgbase=python-fabio
pkgname=(python2-fabio python-fabio)
_pkgname=fabio
pkgver=0.9.0
pkgrel=4
pkgdesc="I/O library for images produced by 2D X-ray detectors."
arch=('i686' 'pentium4' 'x86_64')
url="http://www.silx.org"
license=('MIT' 'LGPL' 'Apache')
_deppy2=(
  'python2>=2.7'
  'python2<2.8'
)
_deppy=(
  'python>=3.7'
  'python<3.8'
)
_depends=(
  'cython'
  'python-lxml'
  'python-numpy'
  'python-sphinx'
  'python-sphinxcontrib-programoutput')
_makedepends=(
  'cython'
  'python-setuptools')
_optdepends=('python-pyqt4: for the fabio_viewer program')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/silx-kit/${_pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('feb81c076ac9f32553beaa1a2c9edf8cadb6dd6ef80cef70b5159014eb2ae0d1b0a5a57c3baaf942876c52b1fd38d9335b9a1ad6f7af38a8836ea31e3055d45c')

makedepends=(
  "${_deppy[@]}"
  "${_deppy2[@]}"
  "${_depends[@]}"
  "${_depends[@]//python/python2}"
  "${_makedepends[@]}"
  "${_makedepends[@]//ython/ython2}"
)

prepare() {
  cp -r "${_pkgname}-${pkgver}" "py2-${_pkgname}-${pkgver}"
  mv "${_pkgname}-${pkgver}" "py-${_pkgname}-${pkgver}"
}

build() {
  cd "${srcdir}/py-${_pkgname}-${pkgver}"
  python setup.py build

  cd "${srcdir}/py2-${_pkgname}-${pkgver}"
  python2 setup.py build
}

package_python2-fabio() {
  depends=(
    "${_deppy2[@]}"
    "${_depends[@]//python/python2}"
  )
  optdepends=(
    "${_optdepends[@]//python/python2}"
  )
  cd "${srcdir}/py2-${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -D copyright "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  for suffix in '-convert' '_viewer'; do
    mv "${pkgdir}/usr/bin/fabio${suffix}" "${pkgdir}/usr/bin/fabio2${suffix}"
    sed -i '1 s/python/python2/' "${pkgdir}/usr/bin/fabio2${suffix}"
  done
}

package_python-fabio() {
  depends=(
    "${_deppy[@]}"
    "${_depends[@]}"
  )
  optdepenends=(
    "${_optdepends[@]}"
  )
  cd "${srcdir}/py-${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1
  install -D copyright "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
