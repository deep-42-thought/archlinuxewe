# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Leonid B <leonid dot bloch at esrf dot fr>
# Upstream contact: silx at esrf dot fr
pkgbase=python-fabio
pkgname=(python2-fabio python-fabio)
_pkgname=fabio
pkgver=0.7.0
pkgrel=1
pkgdesc="I/O library for images produced by 2D X-ray detectors."
arch=('i686' 'x86_64')
url="http://www.silx.org"
license=('MIT' 'LGPL' 'Apache')
_depends=(
  'python-numpy'
  'python-setuptools')
_makedepends=(
  'cython')
_optdepends=('python-pyqt4: for the fabio_viewer program')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/silx-kit/${_pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('1ef84eb628f950d933db35335d2656c3385e07ede9bb833052661b8b6a5d39889889c9e58ddd3a5ccc3d3d0e7989c2b57942270142b6acca87d67ce7008a6fb4')

makedepends=(
  "${_depends[@]}"
  "${_depends[@]//python/python2}"
  "${_makedepends[@]}"
  "${_makedepends[@]//ython/ython2}"
)

prepare() {
  cp -r "${_pkgname}-${pkgver}" "py2-${_pkgname}-${pkgver}"
  mv "${_pkgname}-${pkgver}" "py-${_pkgname}-${pkgver}"
}

build() {
  cd "${srcdir}/py-${_pkgname}-${pkgver}"
  python setup.py build

  cd "${srcdir}/py2-${_pkgname}-${pkgver}"
  python2 setup.py build
}

package_python2-fabio() {
  depends=(
    "${_depends[@]//python/python2}"
  )
  optdepends=(
    "${_optdepends[@]//python/python2}"
  )
  cd "${srcdir}/py2-${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -D copyright "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  for suffix in '-convert' '_viewer'; do
    mv "${pkgdir}/usr/bin/fabio${suffix}" "${pkgdir}/usr/bin/fabio2${suffix}"
    sed -i '1 s/python/python2/' "${pkgdir}/usr/bin/fabio2${suffix}"
  done
}

package_python-fabio() {
  depends=(
    "${_depends[@]}"
  )
  optdepenends=(
    "${_optdepends[@]}"
  )
  cd "${srcdir}/py-${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1
  install -D copyright "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
