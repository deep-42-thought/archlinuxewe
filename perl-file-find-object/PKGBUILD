# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Piotr Rogo≈ºa <piotr dot r dot public at gmail dot com>

pkgname=perl-file-find-object
pkgver=0.3.6
pkgrel=3
_author="S/SH/SHLOMIF"
_distdir="File-Find-Object-${pkgver}"
pkgdesc="File::Find::Object - An object oriented File::Find replacement"
arch=('any')
url="http://search.cpan.org/dist/File-Find-Object/"
license=('GPL' 'PerlArtistic')
provides=(
  'perl-file-find-object-base=0.3.6'
  'perl-file-find-object-deeppath=0.3.6'
  'perl-file-find-object-pathcomp=0.3.6'
  'perl-file-find-object-result=0.3.6'
  'perl-file-find-object-toppath=0.3.6')
depends=(
  'perl>=5.36'
  'perl<5.37'
  'perl-carp'
  'perl-class-xsaccessor'
  'perl-parent'
  'perl-pathtools'
  'perl-scalar-list-utils')
makedepends=(
  'perl-extutils-makemaker'
  'perl-file-path'
  'perl-file-treecreate'
  'perl-io'
  'perl-lib'
  'perl-module-build>=0.28'
  'perl-test-simple>=0.88')
checkdepends=(
  'perl'
  'perl-cpan-changes'
  'perl-pod-coverage-trustpod'
  'perl-test-kwalitee-extra'
  'perl-test-pod'
  'perl-test-pod-coverage'
  'perl-test-simple'
  'perl-test-trailingspace'
)
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/$_author/${_distdir}.tar.gz")
sha512sums=('62aec37a3f23b05396cf9786bd884383a621a214b2d7b1c7689c281391c81bb26ad5648f62942b6a08398930f9d74f3e526cd3139ca3d907c364475b207d6822')

build(){
  cd "$srcdir/${_distdir}"

  # Setting these env variables overwrites any command-line-options we don't want...
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  # If using Makefile.PL
  if [ -r Makefile.PL ]; then
    /usr/bin/perl Makefile.PL
    make
  # If using Build.PL
  elif [ -r Build.PL ]; then
    /usr/bin/perl Build.PL
    perl Build
  fi
}
check(){
  cd "$srcdir/${_distdir}"

  export AUTHOR_TESTING=1
  export RELEASE_TESTING=1

  # If using Makefile.PL
  if [ -r Makefile.PL ]; then
    make test
  # If using Build.PL
  elif [ -r Build.PL ]; then
    perl Build test
  fi
}
package(){
  cd "$srcdir/${_distdir}"

  # If using Makefile.PL
  if [ -r Makefile.PL ]; then
    make install
  # If using Build.PL
  elif [ -r Build.PL ]; then
    perl Build install
  fi

  # remove perllocal.pod and .packlist
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
