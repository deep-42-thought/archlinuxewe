# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-file-find-object-rule'
pkgver=0.0307
pkgrel=1
pkgdesc="Alternative interface to File::Find::Object"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl>=5.8.0'
  'perl-carp'
  'perl-class-xsaccessor'
  'perl-file-find-object'
  'perl-number-compare'
  'perl-pathtools'
  'perl-text-glob')
makedepends=(
  'perl-base'
  'perl-file-path'
  'perl-io'
  'perl-lib'
  'perl-module-build>=0.28'
  'perl-test-simple>=0.88')
checkdepends=(
  'perl-test-pod'
  'perl-test-pod-coverage'
  'perl-test-trailingspace')
url='http://search.cpan.org/~shlomif/File-Find-Object-Rule-0.0305/'
_distdir="File-Find-Object-Rule-${pkgver}"
source=("http://search.cpan.org/CPAN/authors/id/S/SH/SHLOMIF/${_distdir}.tar.gz")
sha512sums=('7e6f76d4d28b38dcfad44d6a7caac44cc24e9d6d160d8e1e10ce4a03e9498328044fcc73760fc042e0d16ec44e483848bc368dc480464fd36bf729afc24fc49c')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    perl Build.PL
    perl Build
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    export RELEASE_TESTING=1
    perl Build test
  )
}

package() {
  cd "$srcdir/$_distdir"
  perl Build install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
