# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Michal Kaliszka <desmont@gmail.com>
# Contributor: Zsolt Varadi <sysop_xxl@fibermail.hu>
# Contributor: Holger Rauch < holger dot rauch at posteo dot de >

pkgname=tea-qt
pkgver=49.0.0
pkgrel=1
pkgdesc="A Qt-based text editor for Linux and *BSD with an ultimate small size, providing hundreds of functions"
arch=('i686' 'pentium4' 'x86_64')
url="http://semiletov.org/tea/"
license=('GPL')
depends=('qt5-base' 'qt5-declarative' 'gcc-libs' 'aspell' 'hunspell' 'poppler-qt5' 'djvulibre' 'desktop-file-utils')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/psemiletov/tea-qt/archive/${pkgver}.tar.gz")
sha512sums=('b5e767cd91abe7ba4083830cd19f125bd0d362c4eabb5efd670e647fab7b5f052298174b09d251fee431cf650b5d851e10bd652532ffa91fafe4791ef92ef222')
provides=("tea=${pkgver}")
conflicts=('tea')
replaces=('tea')

build() {
  mkdir -p "${srcdir}/${pkgname}-${pkgver}/b"
  cd "${srcdir}/${pkgname}-${pkgver}/b"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}/b"
  make DESTDIR="${pkgdir}" install
}
