# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Michal Kaliszka <desmont@gmail.com>
# Contributor: Zsolt Varadi <sysop_xxl@fibermail.hu>
# Contributor: Holger Rauch < holger dot rauch at posteo dot de >

pkgname=tea-qt
pkgver=60.5.1
pkgrel=4
pkgdesc="A Qt-based text editor for Linux and *BSD with an ultimate small size, providing hundreds of functions"
arch=('i686' 'pentium4' 'x86_64')
url="http://semiletov.org/tea/"
license=('GPL')
_pinned_dependencies=(
  'aspell=0.60.8'
  'djvulibre=3.5.28'
  'gcc-libs=11.1.0'
  'glibc>=2.31'
  'hunspell=1.7.0'
  'libpoppler-qt5.so=1'
  'qt5-base=5.15.2+kde+r236'
  'zlib=1:1.2.11'
)
depends=('qt5-declarative' 'desktop-file-utils' "${_pinned_dependencies[@]}")
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/psemiletov/tea-qt/archive/${pkgver}.tar.gz")
sha512sums=('86be61a8acff29e40151eea69ce790a16fce52082b0ebf356d19ebd32f512c33bee4e0f2d23911b31bd06353e067cce10a39e26bac773832ab2996b5bbe3eda2')
provides=("tea=${pkgver}")
conflicts=('tea')
replaces=('tea')

build() {
  mkdir -p "${srcdir}/${pkgname}-${pkgver}/b"
  cd "${srcdir}/${pkgname}-${pkgver}/b"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}/b"
  make DESTDIR="${pkgdir}" install
}
