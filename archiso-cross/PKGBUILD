# Maintainer:  Erich Eckner <arch at eckner dot net>
# Contributor: Gerardo Exequiel Pozzi <djgera@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=archiso
pkgver=43
pkgrel=1
pkgdesc='Tools for creating Arch Linux live and install iso images - modified to make cross compiling easier'
arch=('any')
license=('GPL')
url='https://projects.archlinux.org/archiso.git/'
depends=('make' 'arch-install-scripts' 'squashfs-tools' 'libisoburn' 'dosfstools' 'lynx')
source=("https://sources.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "https://sources.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.gz.sig"
        'default-arch-value.patch')
sha256sums=('4d2274cc5f1c48883941c3e37dfe7882632f9c455f97635aa079c1812571cb13'
            'SKIP'
            '271174ac2e0b43c3cf78cc6bd928eee52ebfa3b2be10d75e2af1a98ee5318661')
validpgpkeys=('0F334D8698881578F65D2AE55ED514A45BD5C938'  # Gerardo Exequiel Pozzi
              '4AA4767BBC9C4B1D18AE28B77F2D434B9741E8AC') # Pierre Schmitz

prepare() {
    cd "${pkgname}-${pkgver}"
    patch -p1 -i "${srcdir}/default-arch-value.patch"
}

package() {
    make -C "${pkgname}-${pkgver}" DESTDIR="${pkgdir}" install
}
