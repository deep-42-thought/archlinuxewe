# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-encoding-fixlatin-xs'
pkgver=1.02
pkgrel=5
pkgdesc='XS implementation layer for Encoding::FixLatin'
arch=('i686' 'pentium4' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
makedepends=(
  'perl-encoding-fixlatin>=1.03'
  'perl-extutils-makemaker'
  'perl-test-simple>=0.90')
_pinned_dependencies=(
  'glibc>=2.31'
)
depends=(
  'perl>=5.34'
  'perl<5.35'
  "${_pinned_dependencies[@]}"
  'perl>=5.14.0')
url='http://github.com/grantm/encoding-fixlatin-xs'
checkdepends=(
  'perl-test-pod'
  'perl-test-simple'
)
_distdir="Encoding-FixLatin-XS-${pkgver}"
source=("http://search.cpan.org/CPAN/authors/id/G/GR/GRANTM/${_distdir}.tar.gz")
sha512sums=('4a786f6d881a1067ede88893db23a55eb55512a05ca2948543a52a6d655a07b6f56dac11599e3e94999461a521683b04e91ab19cd2dfcb8b9e86092d86fc471f')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    export RELEASE_TESTING=1
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
