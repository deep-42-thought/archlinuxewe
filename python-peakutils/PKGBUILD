# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Jakob Nixdorf <flocke@shadowice.org>

pkgbase=python-peakutils
pkgname=('python-peakutils' 'python2-peakutils')
_pkgname='PeakUtils'
pkgver=1.3.2
pkgrel=1
pkgdesc="Peak detection utilities for 1D data"
arch=('any')
url="https://pypi.python.org/pypi/PeakUtils"
license=('MIT')
_deppy2=(
  'python2>=2.7'
  'python2<2.8'
)
_deppy=(
  'python>=3.7'
  'python<3.8'
)
_depends=(
  'python-sphinx-better-theme')
_makedepends=(
  'python-setuptools')
makedepends=(
  "${_deppy[@]}"
  "${_deppy2[@]}"
  "${_depends[@]}"
  "${_depends[@]/python/python2}"
  "${_makedepends[@]}"
  "${_makedepends[@]/python/python2}"
)
source=("https://pypi.python.org/packages/2a/e0/a45948450946a87dae44d936ea7646d862e1014753c496468a05f20e95c5/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('156982b48f174f578660d1213237811b3c95f2c5dc06c40915e87b96c78827fd2fe64d1fff08870d652d7103f7048ebf154532c105b313f74fd7fab88f94bc35')

prepare() {
  cd "${srcdir}"

  cp -a "${_pkgname}-${pkgver}" "${pkgname[0]}"
  mv "${_pkgname}-${pkgver}" "${pkgname[1]}"
}

build() {
  cd "${srcdir}/${pkgname[0]}"
  python setup.py build

  cd "${srcdir}/${pkgname[1]}"
  python2 setup.py build
}

package_python-peakutils() {
  depends=(
    "${_deppy[@]}"
    "${_depends[@]}"
  )

  cd "${srcdir}/${pkgname[0]}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}

package_python2-peakutils() {
  depends=(
    "${_deppy2[@]}"
    "${_depends[@]/python/python2}"
  )

  cd "${srcdir}/${pkgname[1]}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}

