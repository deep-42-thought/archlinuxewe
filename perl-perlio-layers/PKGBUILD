# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=perl-perlio-layers
_pkgname='PerlIO-Layers'
pkgver=0.012
pkgrel=5
_distdir="${_pkgname}-${pkgver}"
pkgdesc="Querying your filehandle's capabilities"
arch=('x86_64' 'i686' 'pentium4')
url="https://github.com/Leont/perlio-layers"
license=('GPL' 'PerlArtistic')
makedepends=(
  'perl-data-dumper'
  'perl-module-build>=0.28'
  'perl-test-simple>=0.82')
_pinned_dependencies=(
  'glibc>=2.31'
)
depends=(
  'perl>=5.34'
  'perl<5.35'
  "${_pinned_dependencies[@]}"
  'perl-carp'
  'perl-exporter>=5.57'
  'perl-scalar-list-utils'
  'perl-xsloader')
checkdepends=(
  'perl-pod-coverage-trustpod'
  'perl-test-pod'
  'perl-test-pod-coverage'
  'perl-test-simple'
)
source=("http://search.cpan.org/CPAN/authors/id/L/LE/LEONT/${_distdir}.tar.gz")
sha512sums=('3b8c0103faa49227b68cc6bb835e34c7b3f9731f83421a9b58ad68e469c55795ab977e6983ece8fe6db5b9f0ff4d615affbbea00716cd87a1af28571707dcf6e')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_distdir}"
    perl Build.PL
    perl Build
  )
}

check() {
  cd "${srcdir}/${_distdir}"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    export RELEASE_TESTING=1
    perl Build test
  )
}

package() {
  cd "${srcdir}/${_distdir}"
  perl Build install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
