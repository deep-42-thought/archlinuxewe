# Maintainer:  Erich Eckner <arch at eckner dot net>
# Contributor: Daniel Bermond <dbermond@archlinux.org>
# Contributor: Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: Simon Boulay <simon.boulay@alkeona.net>

pkgname=python2-breathe
pkgver=4.14.1
pkgrel=1
pkgdesc="An extension to reStructuredText and Sphinx to be able to read and render Doxygen xml output."
arch=('any')
url="https://breathe.readthedocs.org/en/latest/"
license=('BSD')
depends=(
  'python2>=2.7'
  'python2<2.8'
  'python2-setuptools'
  'python2-sphinx'
  'python2-docutils'
  'python2-six')
makedepends=('git')
checkdepends=('python2-nose' 'python2-sphinx')
source=("git+https://github.com/michaeljones/breathe#tag=v${pkgver}?signed")
sha512sums=('SKIP')
validpgpkeys=('79DC0FAF18C31807E82E1A6B9EF1DC92B5F301BD') # Melvin Vermeeren <mail@mel.vin>

prepare() {
  sed -i '
    1 s/python$/python2/
  ' breathe/breathe-apidoc.py
  sed -i '
    /sys\.version_info/ {
      s/3/2/
      s/5/7/
    }
  ' breathe/setup.py
}

build() {
  cd breathe
  python2 setup.py build
}

package_python2-breathe() {
  cd "breathe"
  python2 setup.py install --root="${pkgdir}" --skip-build --optimize=1
  mv "${pkgdir}/usr/bin/breathe-apidoc"{,2}

  install -D -m644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
