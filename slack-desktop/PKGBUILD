# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: "Amhairghin" Oscar Garcia Amor (https://ogarcia.me)
# Contributor: Simon Gomizelj <simongmzlj(at)gmail(dot)com>
# Contributor: Kyle Manna <kyle(at)kylemanna(dot)com>

pkgname=slack-desktop
pkgver=4.17.0
pkgrel=6
pkgdesc="Slack Desktop (Beta) for Linux"
arch=('x86_64')
url="https://slack.com/downloads"
license=('custom')
_pinned_dependencies=(
  'at-spi2-atk=2.38.0'
  'at-spi2-core=2.40.3'
  'cairo=1.17.4'
  'gcc-libs=11.1.0'
  'glibc>=2.33'
  'libasound.so=2'
  'libatk-1.0.so=0'
  'libcups=1:2.3.3op2'
  'libdbus-1.so=3'
  'libdrm=2.4.107'
  'libexpat.so=1'
  'libgdk-3.so=0'
  'libgdk_pixbuf-2.0.so=0'
  'libgio-2.0.so=0'
  'libglib-2.0.so=0'
  'libgobject-2.0.so=0'
  'libgtk-3.so=0'
  'libpango-1.0.so=0'
  'libx11=1.7.2'
  'libxcb=1.14'
  'libxcomposite=0.4.5'
  'libxdamage=1.1.5'
  'libxext=1.3.4'
  'libxfixes=6.0.0'
  'libxkbcommon.so=0'
  'libxrandr=1.5.2'
  'libxshmfence=1.3'
  'mesa=21.1.5'
  'nspr=4.32'
  'nss=3.68'
)
depends=('alsa-lib' 'gtk3' 'libsecret' 'libxss' 'libxtst' 'nss' 'xdg-utils' "${_pinned_dependencies[@]}")
optdepends=('libappindicator-gtk3: Systray indicator support'
            'org.freedesktop.secrets: Keyring password store support')
source_x86_64=("https://downloads.slack-edge.com/linux_releases/${pkgname}-${pkgver}-amd64.deb")
source=("${pkgname}.patch")
sha512sums=('fd7e9681887639a2a9117c6bf537c25918f02985588366aaf3cd5b2e07b842d773aaf27354b7c149df1cc003e645b77bd36ec753992d50e02f5eb87e59009264')
sha512sums_x86_64=('95be9b9023644befa3289bef8c99d4466fed6be9c7fffe52c0acc546d316fdd9ee87079a30e641b9ede413a07762eed59872519b44b41a1afca3663d6fea4eb8')

package() {
    cd "${pkgdir}"
    bsdtar -xJf "${srcdir}/data.tar.xz"

    # Fix hardcoded icon path in .desktop file
    patch -p1 -i "${srcdir}/${pkgname}.patch"

    # Remove all unnecessary stuff
    rm -rf --one-file-system \
      "etc" \
      "usr/lib/slack/src" \
      "usr/share/lintian" \
      "usr/share/doc"

    # Move license
    install -dm755 "usr/share/licenses/${pkgname}"
    mv "usr/lib/slack/LICENSE" "usr/share/licenses/${pkgname}"
    ln -s "/usr/share/licenses/${pkgname}/LICENSE" "usr/lib/slack/LICENSE"
}
