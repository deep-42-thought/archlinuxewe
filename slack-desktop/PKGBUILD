# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: "Amhairghin" Oscar Garcia Amor (https://ogarcia.me)
# Contributor: Simon Gomizelj <simongmzlj(at)gmail(dot)com>
# Contributor: Kyle Manna <kyle(at)kylemanna(dot)com>

pkgname=slack-desktop
pkgver=4.20.0
pkgrel=60
pkgdesc="Slack Desktop (Beta) for Linux"
arch=('x86_64')
url="https://slack.com/downloads"
license=('custom')
_pinned_dependencies=(
  'at-spi2-atk=2.38.0'
  'at-spi2-core=2.44.1'
  'cairo=1.17.6'
  'gcc-libs=12.1.0'
  'glibc>=2.33'
  'libasound.so=2'
  'libatk-1.0.so=0'
  'libcups=1:2.4.2'
  'libdbus-1.so=3'
  'libdrm=2.4.111'
  'libexpat.so=1'
  'libgdk_pixbuf-2.0.so=0'
  'libgio-2.0.so=0'
  'libglib-2.0.so=0'
  'libgmodule-2.0.so=0'
  'libgobject-2.0.so=0'
  'libgtk-3.so=0'
  'libpango-1.0.so=0'
  'libx11=1.8'
  'libxcb=1.15'
  'libxcomposite=0.4.5'
  'libxdamage=1.1.5'
  'libxext=1.3.4'
  'libxfixes=6.0.0'
  'libxkbcommon.so=0'
  'libxrandr=1.5.2'
  'libxshmfence=1.3'
  'mesa=22.1.1'
  'nspr=4.34'
  'nss=3.79'
)
depends=('alsa-lib' 'gtk3' 'libsecret' 'libxss' 'libxtst' 'nss' 'xdg-utils' "${_pinned_dependencies[@]}")
optdepends=('libappindicator-gtk3: Systray indicator support'
            'org.freedesktop.secrets: Keyring password store support')
source_x86_64=("https://downloads.slack-edge.com/releases/linux/${pkgver}/prod/x64/${pkgname}-${pkgver}-amd64.deb")
source=("${pkgname}.patch")
sha256sums=('d351d8ff3eee0006d1c66e3efec60a78ae8cc10fd8c3042e8d899125a1c4bf47')
sha256sums_x86_64=('b08207389246837536ea769425d458411c08dc6802ffaae9249092e387461ce5')

package() {
    cd "${pkgdir}"
    bsdtar -xJf "${srcdir}/data.tar.xz"

    # Fix hardcoded icon path in .desktop file
    patch -p1 -i "${srcdir}/${pkgname}.patch"

    # Remove all unnecessary stuff
    rm -rf --one-file-system \
      "etc" \
      "usr/lib/slack/src" \
      "usr/share/lintian" \
      "usr/share/doc"

    # Move license
    install -dm755 "usr/share/licenses/${pkgname}"
    mv "usr/lib/slack/LICENSE" "usr/share/licenses/${pkgname}"
    ln -s "/usr/share/licenses/${pkgname}/LICENSE" "usr/lib/slack/LICENSE"
}
