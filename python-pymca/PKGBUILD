# Maintainer: Erich Eckner <arch at eckner dot net>
pkgbase=python-pymca
pkgname=(python2-pymca python-pymca)
_pkgname="${pkgbase#*-}"
pkgver=5.5.1
pkgrel=1
pkgdesc='Stand-alone application and Python tools for interactive and/or batch processing analysis of X-Ray Fluorescence Spectra'
arch=('i686' 'pentium4' 'x86_64')
url='http://pymca.sourceforge.net/index.html'
license=('MIT License')
_deppy2=(
  'python2>=2.7'
  'python2<2.8'
)
_deppy=(
  'python>=3.7'
  'python<3.8'
)
_depends=(
  'python-fisx>=1.1.5'
  'python-h5py'
  'python-matplotlib>1.0'
  'python-numpy>=1.8'
#  'python-pyopengl'
#  'python-pyqt4'
  'python-pyqt5'
  'python-qtconsole')
_makedepends=(
  'python-setuptools')
_optdepends=(
  'python-pyqt5: gui'
  'python-matplotlib: gui')
makedepends=(
  "${_deppy[@]}"
  "${_deppy2[@]}"
  "${_depends[@]}"
  "${_depends[@]/python/python2}"
  "${_makedepends[@]}"
  "${_makedepends[@]/ython/ython2}"
)
source=(
    "${_pkgname}-v${pkgver}.tar.gz::https://github.com/vasole/${_pkgname}/archive/v${pkgver}.tar.gz"
)
sha512sums=('aa6dc95dacde5f6afb9ec18f032569c752d0d8f83c331c428e0182e46d1bf13fb2479cb3886ab39a8badbe1250671476c13cec648c223195ed8fd37c5df99a58')

prepare() {
  cp -a ${_pkgname}-${pkgver}{,-py2}
}

build() {
  echo "Building python..."
  (
    cd ${_pkgname}-${pkgver}
    python setup.py build
  )
  echo "Building python2..."
  (
    cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build
  )
}

package_python2-pymca() {
  depends=(
    "${_deppy2[@]}"
    "${_depends[@]/python/python2}"
  )
  optdepends=(
    "${_optdepends[@]/python/python2}"
  )
  cd ${_pkgname}-${pkgver}-py2
  python2 setup.py install --skip-build --root="${pkgdir}" --prefix=/usr
  find "${pkgdir}" -type f -exec sed -si '1 s#[/ ]python\s*$# python2#' {} +
}

package_python-pymca() {
  depends=(
    "${_deppy[@]}"
    "${_depends[@]}"
  )
  optdepends=(
    "${_optdepends[@]}"
  )
  cd ${_pkgname}-${pkgver}
  python setup.py install --skip-build --root="${pkgdir}" --prefix=/usr
}
