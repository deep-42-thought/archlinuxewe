# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-search-elasticsearch'
pkgver=6.00
pkgrel=8
pkgdesc='The official client for Elasticsearch'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl>=5.36'
  'perl<5.37'
  'perl')
checkdepends=(
  'perl-any-uri-escape'
  'perl-cpanel-json-xs'
  'perl-json-maybexs>=1.002002'
  'perl-json-xs'
  'perl-moo>=1.003'
  'perl-test-sharedfork'
  'perl-test-deep'
  'perl-test-eol'
  'perl-test-exception'
  'perl-test-notabs'
  'perl-test-pod'
  'perl-log-any-adapter-callback'
  'perl-namespace-clean'
  'perl-uri'
  'perl-io-socket-ssl'
)
url='https://metacpan.org/release/Search-Elasticsearch'
_distdir="Search-Elasticsearch-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/D/DR/DRTECH/${_distdir}.tar.gz")
sha512sums=('c25e31523f63915ac56d953ef601537f2e6a76fb82d20eda837950bf4043de1e48a16758c5c1391383e17784fd740d01a522271c34757500ff47de152552f621')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    export AUTHOR_TESTING=1
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
