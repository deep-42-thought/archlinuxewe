# Contributor: Patrick McCarty <pnorcks at gmail dot com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=lilypond-devel
pkgver=2.23.8
pkgrel=2
pkgdesc="An automated music engraving system (development version)"
arch=('i686' 'pentium4' 'x86_64')
url="http://lilypond.org/"
license=('GPL')
_pinned_dependencies=(
  'gc=8.2.0'
  'gcc-libs=12.1.0'
  'glibc>=2.31'
  'guile=2.2.7'
  'libfontconfig.so=1'
  'libfreetype.so=6'
  'libglib-2.0.so=0'
  'libgobject-2.0.so=0'
  'libharfbuzz.so=libharfbuzz.so'
  'libpango-1.0.so=0'
  'libpangoft2-1.0.so=0'
)
depends=('python2'
         "${_pinned_dependencies[@]}"
)
makedepends=('fontforge'
             'gsfonts'
             't1utils'
             'ghostscript'
             'texlive-core>=2011.23170'
             'tex-gyre-fonts'
             'texlive-langcyrillic'
             'dblatex'
             'xorg-server-xvfb'
)
optdepends=('extractpdfmark: for reducing the size of pdf output significantly'
            'tk: for the gui'
)
provides=("lilypond=$pkgver")
conflicts=('lilypond' 'lilypond-git')
source=("http://lilypond.org/downloads/sources/v${pkgver%.*}/lilypond-${pkgver}.tar.gz"
        'no_fontforge-versioncheck.patch')
sha512sums=('13fecd025a843776203a65c8d3d2cc66f91b912e9f2b59aa6943d441a91c416bc237e80d63226c3fe536cceceb838ee83ffffb9b534cae165833350ace1ebdd3'
            '343317fd0d88cae1ed35bba6038ed5d24a0664bd47acf27895e03f2e95466439d80bb780277236ef74bd929c29a16ae840d09c3d50dd2a994b035d0d63e015aa')

prepare() {
  cd "$srcdir/lilypond-$pkgver"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done
  
  patch -Np1 < $srcdir/no_fontforge-versioncheck.patch
  rm -rf python/out/
}

build() {
  cd "$srcdir/lilypond-$pkgver"

  export GUILE=/usr/bin/guile
  export GUILE_CONFIG=/usr/bin/guile-config
  export PYTHON="python2"
  export PYTHON_CONFIG="python2-config"
  export GUILE=/usr/bin/guile1.8
  export GUILE_CONFIG=/usr/bin/guile-config1.8

  ./autogen.sh --prefix=/usr \
              --disable-documentation 
	        
  # FIXME: the extra LDFLAG should not be needed;
  # this is a regression somewhere
  xvfb-run -a -s "-screen 0 1280x1024x24" make LDFLAGS+=" -pthread" all
}

package() {
  cd "$srcdir/lilypond-$pkgver"
  make DESTDIR="$pkgdir/" \
       vimdir="/usr/share/vim/vimfiles" install

  rm -rf "$pkgdir/usr/share/man"
}
