# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='perl-ipc-sharelite'
pkgver='0.17'
pkgrel=12
pkgdesc='Lightweight interface to shared memory'
arch=('i686' 'pentium4' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
_pinned_dependencies=(
  'glibc>=2.31'
)
depends=(
  'perl>=5.36'
  'perl<5.37'
  "${_pinned_dependencies[@]}"
  'perl-pathtools'
  'perl-test-simple')
makedepends=(
  'perl-extutils-makemaker')
checkdepends=(
  'perl-test-pod'
)
url='https://metacpan.org/release/IPC-ShareLite'
_distdir="IPC-ShareLite-${pkgver}"
source=("https://cpan.metacpan.org/authors/id/A/AN/ANDYA/${_distdir}.tar.gz")
sha512sums=('f796f6766df11205755de4672ff8a586690545ccaa2f91c3fc65f106f3b61e1cbd86ee3e97dadf787809e3be32f503670cb67dafc9b18eee30d59d8be03b5ee9')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
