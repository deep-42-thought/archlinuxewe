# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=python2-lmfit
pkgver=0.9.5
pkgrel=1
pkgdesc="Least-Squares Minimization with Constraints for Python"
arch=('any')
url="http://lmfit.github.io/lmfit-py/"
license=('BSD')
depends=('python2' 'python2-numpy' 'python2-scipy')
makedepends=('python2-setuptools')
checkdepends=('python2-nose')
source=(https://github.com/lmfit/lmfit-py/archive/${pkgver}.tar.gz)

build() {
  cd "$srcdir/lmfit-py-$pkgver"
  python2 setup.py build
}

check() {
  cd "$srcdir/lmfit-py-$pkgver"
  nosetests2 tests
}

package(){
  cd "$srcdir/lmfit-py-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:ts=2:sw=2:et:
sha512sums=('a81d07aa2028346333280f901f32e0c37056f627fc5536017a1e6d28a27843860df5289e8bce4862158e965a5609ad2ec32a75e26789b474f01d1ce8e8301c41')
