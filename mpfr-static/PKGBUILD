# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=mpfr-static
_pkgname=mpfr
pkgver=4.0.2
pkgrel=1
pkgdesc='Multiple-precision floating-point library'
arch=(x86_64)
url='https://www.mpfr.org/'
license=(LGPL)
depends=('gmp-static>=5.0')
source=(https://www.mpfr.org/mpfr-$pkgver/mpfr-${pkgver}.tar.xz{,.asc})
validpgpkeys=('07F3DBBECC1A39605078094D980C197698C3739D')
sha256sums=('1d3be708604eae0e42d578ba93b390c2a145f17743a744d8f3f8c2ad5855a38a'
            'SKIP')

build() {
  cd $_pkgname-$pkgver
  LDFLAGS+=' -static '
  ./configure --prefix=/usr --enable-thread-safe --enable-shared
  make
}

check() {
  cd $_pkgname-$pkgver
  make check
  make check-exported-symbols
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf --one-file-system "$pkgdir/usr/share/" "$pkgdir/usr/include/" "$pkgdir/usr/lib/pkgconfig"
}
