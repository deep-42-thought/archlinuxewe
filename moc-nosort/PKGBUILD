# $Id$
# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=moc-nosort
pkgver=2.5.2
provides=("moc=${pkgver}")
conflicts=('moc')
pkgrel=26
epoch=1
pkgdesc="An ncurses console audio player designed to be powerful and easy to use"
arch=('i686' 'pentium4' 'x86_64')
url="http://moc.daper.net/"
license=('GPL')
_pinned_dependencies=(
  'glibc>=2.31'
  'libasound.so=2'
  'libavcodec.so=59'
  'libavformat.so=59'
  'libavutil.so=57'
  'libcurl.so=4'
  'libfaad.so=2'
  'libFLAC.so=8'
  'libid3tag.so=0'
  'libjack.so=0'
  'libmad=0.15.1b'
  'libmagic.so=1'
  'libmodplug=0.8.9.0'
  'libmpcdec=1:0.1+r475'
  'libncursesw.so=6'
  'libogg.so=0'
  'libsamplerate.so=0'
  'libsndfile.so=1'
  'libtool=2.4.7'
  'libvorbisfile.so=3'
  'libvorbis.so=0'
  'speex=1.2.1'
  'taglib=1.12'
  'wavpack=5.4.0'
  'zlib=1:1.2.12'
)
depends=("${_pinned_dependencies[@]}")
makedepends=('speex' 'ffmpeg' 'taglib' 'libmpcdec' 'wavpack' 'libmodplug' 'faad2')
optdepends=('speex: for using the speex plugin'
	    'ffmpeg: for using the ffmpeg plugin'
	    'taglib: for using the musepack plugin'
	    'libmpcdec: for using the musepack plugin'
            'wavpack: for using the wavpack plugin'
            'faad2: for using the aac plugin'
	    'libmodplug: for using the modplug plugin')
source=(http://ftp.daper.net/pub/soft/moc/stable/${pkgname%-nosort}-${pkgver}.tar.bz2{,.sig}
        disable-sort.patch
	moc-ffmpeg4.patch)
sha512sums=('992d06809fb52e58dcee63704bde05795b99edbac46edd8381f314c564490013b18e1853926f419c2c3c7f1bb68ff35e97048de03ace3232b800cf4b3b7dbe0f'
            'SKIP'
            '7a760eebf22d84f1ac21e3b429c10f7fb77bc8c7642adb57c57c1b6dfb9d2fa464398475778813a803738ec7a6ffee2c3da857bd36a6b89eaf3345ae419dc7ab'
	    '270308ad7d2b300553a6cc0ddb9ef90441e32a20a3405ebcb710ff8f0a2cc036e0b59d01bf34f49144f22de5d24e25f9a66dd452a76b6a8994f2d8d75cd9795e')
validpgpkeys=('59359B80406D9E73E80599BEF3121E4F2885A7AA')

prepare() {
  cd "${pkgname%-nosort}-${pkgver}"
  patch -p0 -i ../moc-ffmpeg4.patch # Fix build with ffmpeg 4
  patch -p0 -i ../disable-sort.patch
}

build() {
  cd "${pkgname%-nosort}-${pkgver}"
  ./configure --prefix=/usr --without-rcc \
    --with-oss --with-alsa --with-jack --with-aac --with-mp3 \
    --with-musepack --with-vorbis --with-flac --with-wavpack \
    --with-sndfile --with-modplug --with-ffmpeg --with-speex \
    --with-samplerate --with-curl  --disable-cache --disable-debug
  make
}

package() {
  cd "${pkgname%-nosort}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
