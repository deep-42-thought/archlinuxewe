# Maintainer: Erich Eckner <arch at eckner dot net>
pkgbase=python-wxmplot
pkgname=(python2-wxmplot python-wxmplot)
_pkgname="${pkgbase#*-}"
pkgver=0.9.35
pkgrel=1
pkgdesc='A library for plotting in wxPython using matplotlib'
arch=('any')
url='https://pypi.python.org/pypi/wxmplot/0.9.18'
license=('OSI Approved :: MIT License')
_deppy2=(
  'python2>=2.7'
  'python2<2.8'
  'python2-wxpython3'
)
_deppy=(
  'python>=3.7'
  'python<3.8'
  'python-wxpython'
)
_depends=(
  'python')
_makedepends=(
  'python-setuptools')
makedepends=(
  "${_deppy[@]}"
  "${_deppy2[@]}"
  "${_depends[@]}"
  "${_depends[@]/python/python2}"
  "${_makedepends[@]}"
  "${_makedepends[@]/ython/ython2}"
)
source=(
    "https://pypi.python.org/packages/ac/4f/91122b8f11609653a7e8884d8022b8dcc5c60dde12487322027b71fdbcc4/${_pkgname}-${pkgver}.tar.gz"
)
sha512sums=('222666810c1f785658e80dc2eab5d06ebdaaee6eb65004a51d67246cfba73ba62b51d6acae05aedf2d68b6e7cd8d5fe1592de079ecd53e58df02014382cbffbf')

prepare() {
  cp -a ${_pkgname}-${pkgver}{,-py2}
}

build() {
  echo "Building python..."
  (
    cd ${_pkgname}-${pkgver}
    python setup.py build
  )
  echo "Building python2..."
  (
    cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build
  )
}

package_python2-wxmplot() {
  depends=(
    "${_deppy2[@]}"
    "${_depends[@]/python/python2}"
  )
  cd ${_pkgname}-${pkgver}-py2
  python2 setup.py install --skip-build --root="${pkgdir}" --prefix=/usr
  find "${pkgdir}" -type f -exec sed -si '1 s#[/ ]python\s*$# python2#' {} +
}

package_python-wxmplot() {
  depends=(
    "${_deppy[@]}"
    "${_depends[@]}"
  )
  cd ${_pkgname}-${pkgver}
  python setup.py install --skip-build --root="${pkgdir}" --prefix=/usr
}
