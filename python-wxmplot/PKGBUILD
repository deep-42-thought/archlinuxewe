# Maintainer: Erich Eckner <arch at eckner dot net>
pkgbase=python-wxmplot
pkgname=(python2-wxmplot python-wxmplot)
_pkgname="${pkgbase#*-}"
pkgver=0.9.40
pkgrel=1
pkgdesc='A library for plotting in wxPython using matplotlib'
arch=('any')
url='https://pypi.python.org/pypi/wxmplot/0.9.18'
license=('OSI Approved :: MIT License')
_deppy2=(
  'python2>=2.7'
  'python2<2.8'
  'python2-wxpython3'
)
_deppy=(
  'python>=3.8'
  'python<3.9'
  'python-wxpython>=4.0.3'
)
_depends=(
  'python-matplotlib>=2.0'
  'python-numpy>=1.12'
  'python-six>=1.10')
_makedepends=(
  'python-setuptools')
makedepends=(
  "${_deppy[@]}"
  "${_deppy2[@]}"
  "${_depends[@]}"
  "${_depends[@]/python/python2}"
  "${_makedepends[@]}"
  "${_makedepends[@]/ython/ython2}"
)
source=(
    "https://pypi.python.org/packages/70/22/21da36326256fc1ce97deafad3b5d8b466d9a73eab7b78cbab542ecb7574/${_pkgname}-${pkgver}.tar.gz"
)
sha512sums=('4edfa801014d6ad7b419e39c97d52ec77c8992b2fc069b2bd81b4fe9c055197bbdf676b273b224e82527a731c89154198ab8e2170df397d14690059b7ea615bf')

prepare() {
  cp -a ${_pkgname}-${pkgver}{,-py2}
}

build() {
  echo "Building python..."
  (
    cd ${_pkgname}-${pkgver}
    python setup.py build
  )
  echo "Building python2..."
  (
    cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build
  )
}

package_python2-wxmplot() {
  depends=(
    "${_deppy2[@]}"
    "${_depends[@]/python/python2}"
  )
  cd ${_pkgname}-${pkgver}-py2
  python2 setup.py install --skip-build --root="${pkgdir}" --prefix=/usr
  find "${pkgdir}" -type f -exec sed -si '1 s#[/ ]python\s*$# python2#' {} +
}

package_python-wxmplot() {
  depends=(
    "${_deppy[@]}"
    "${_depends[@]}"
  )
  cd ${_pkgname}-${pkgver}
  python setup.py install --skip-build --root="${pkgdir}" --prefix=/usr
}
