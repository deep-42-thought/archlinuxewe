# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=python-courier-pythonfilter
_pkgname="${pkgname#*-}"
pkgver=3.0.1
pkgrel=1
pkgdesc='collection of useful filters for the Courier MTA, and a framework for developing new filters in Python'
arch=('any')
url='https://bitbucket.org/gordonmessmer/courier-pythonfilter'
license=('GPL')
depends=(
  'python>=3.7'
  'python<3.8'
)
makedepends=(
  'python-setuptools')
source=("https://bitbucket.org/gordonmessmer/${_pkgname}/get/release-${pkgver//./-}.tar.gz")
sha512sums=('05635f0dc738de1ad4738cd67400fe4177e16f43269d22b54664d7feea166ec01a7b255e92a303dd2f3c8ff1a67997b91b955c32d367cd01916167a566667aa7')
backup=('etc/pythonfilter-modules.conf' 'etc/pythonfilter.conf')
optdepends=(
  'python-pyclamav:  Virus Scan'
  'python-pydns:     dialback'
  'python-spf:       spfcheck and whitelist_spf'
  'spamassassin:     Spam Detection'
)

build() {
  cd "${srcdir}/gordonmessmer-${_pkgname}-"*
  python setup.py build
}

package() {
  depends+=('courier-mta')
  cd "${srcdir}/gordonmessmer-${_pkgname}-"*
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  rm "${pkgdir}/usr/lib/python3"*"/site-packages/${_pkgname//-/_}-${pkgver}-py3"*".egg-info"
}

