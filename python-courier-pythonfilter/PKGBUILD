# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=python-courier-pythonfilter
_pkgname="${pkgname#*-}"
pkgver=3.0.3
pkgrel=7
pkgdesc='collection of useful filters for the Courier MTA, and a framework for developing new filters in Python'
arch=('any')
url='https://bitbucket.org/gordonmessmer/courier-pythonfilter'
license=('GPL')
depends=(
  'python>=3.9'
  'python<3.10'
)
makedepends=(
  'python-setuptools')
source=("${_pkgname}-${pkgver}.tar.gz::https://bitbucket.org/gordonmessmer/${_pkgname}/get/release-${pkgver//./-}.tar.gz")
sha512sums=('ab7819eb1c83a050f8653d32925758ce44ae115485b77faed040515aa3c8ff336225788ded39a9926b80932dcef72a622681aa046d8c2ab2c44266d3bcad2ca1')
backup=('etc/pythonfilter-modules.conf' 'etc/pythonfilter.conf')
optdepends=(
  'python-pyclamav:  Virus Scan'
  'python-pydns:     dialback'
  'python-spf:       spfcheck and whitelist_spf'
  'spamassassin:     Spam Detection'
)

build() {
  cd "${srcdir}/gordonmessmer-${_pkgname}-"*
  python setup.py build
}

package() {
  depends+=('courier-mta')
  cd "${srcdir}/gordonmessmer-${_pkgname}-"*
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}

