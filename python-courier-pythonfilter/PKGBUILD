# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=python-courier-pythonfilter
_pkgname="${pkgname#*-}"
pkgver=3.0.2
pkgrel=4
pkgdesc='collection of useful filters for the Courier MTA, and a framework for developing new filters in Python'
arch=('any')
url='https://bitbucket.org/gordonmessmer/courier-pythonfilter'
license=('GPL')
depends=(
  'python>=3.9'
  'python<3.10'
)
makedepends=(
  'python-setuptools')
source=("${_pkgname}-${pkgver}.tar.gz::https://bitbucket.org/gordonmessmer/${_pkgname}/get/release-${pkgver//./-}.tar.gz")
sha512sums=('8bbb0203eed6a66fffebc74d7db70ce76f07a6ac188b928becf0c18739d096ba97e1dbca868a919b7a9130680a895c14ae44893ccb031879253ac6fc04ee25b0')
backup=('etc/pythonfilter-modules.conf' 'etc/pythonfilter.conf')
optdepends=(
  'python-pyclamav:  Virus Scan'
  'python-pydns:     dialback'
  'python-spf:       spfcheck and whitelist_spf'
  'spamassassin:     Spam Detection'
)

build() {
  cd "${srcdir}/gordonmessmer-${_pkgname}-"*
  python setup.py build
}

package() {
  depends+=('courier-mta')
  cd "${srcdir}/gordonmessmer-${_pkgname}-"*
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}

