# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Gordian Edenhofer <gordian.edenhofer@gmail.com>

pkgname=zoom
pkgver=5.10.3.2778
_build=${pkgver#*.*.*.}
_pkgver=${pkgver%.${_build}}
pkgrel=8
pkgdesc="Video Conferencing and Web Conferencing Service"
arch=('x86_64')
license=('custom')
url="https://zoom.us/"
_pinned_dependencies=(
  'at-spi2-atk=2.38.0'
  'at-spi2-core=2.44.1'
  'cairo=1.17.6'
  'gcc-libs=11.2.0'
  'glibc>=2.33'
  'libasound.so=2'
  'libatk-1.0.so=0'
  'libcups=1:2.4.1'
  'libdbus-1.so=3'
  'libdrm=2.4.110'
  'libexpat.so=1'
  'libfontconfig.so=1'
  'libfreetype.so=6'
  'libgdk-3.so=0'
  'libgdk_pixbuf-2.0.so=0'
  'libgio-2.0.so=0'
  'libglib-2.0.so=0'
  'libglvnd=1.4.0'
  'libgobject-2.0.so=0'
  'libgthread-2.0.so=0'
  'libgtk-3.so=0'
  'libicudata.so=71'
  'libicui18n.so=71'
  'libicuuc.so=71'
  'libpango-1.0.so=0'
  'libpangocairo-1.0.so=0'
  'libpulse-mainloop-glib.so=0'
  'libpulse.so=0'
  'libwayland-client.so=0'
  'libwayland-cursor.so=0'
  'libwayland-egl.so=1'
  'libwayland-server.so=0'
  'libx11=1.8'
  'libxcb=1.14'
  'libxcomposite=0.4.5'
  'libxdamage=1.1.5'
  'libxext=1.3.4'
  'libxfixes=6.0.0'
  'libxkbcommon.so=0'
  'libxkbcommon-x11.so=0'
  'libxrandr=1.5.2'
  'libxrender=0.9.10'
  'libxshmfence=1.3'
  'libxtst=1.2.3'
  'mesa=22.0.2'
  'nspr=4.33'
  'nss=3.77'
  'xcb-util-image=0.4.0'
  'xcb-util-keysyms=0.4.0'
  'zlib=1:1.2.12'
)
depends=('fontconfig' 'glib2' 'libpulse' 'libsm' 'ttf-font' 'libx11' 'libxtst' 'libxcb'
	'libxcomposite' 'libxfixes' 'libxi' 'libxcursor' 'libxkbcommon-x11' 'libxrandr'
	'libxrender' 'libxshmfence' 'libxslt' 'mesa' 'nss' 'xcb-util-image'
	'xcb-util-keysyms' 'dbus' 'ibus' "${_pinned_dependencies[@]}")
optdepends=('pulseaudio-alsa: audio via PulseAudio'
	'qt5-webengine: SSO login support'
	'picom: extra compositor needed by some window managers for screen sharing'
	'xcompmgr: extra compositor needed by some window managers for screen sharing')
options=(!strip)
source=("${pkgname}-${pkgver}_orig_x86_64.pkg.tar.xz"::"https://zoom.us/client/${_pkgver}.${_build}/zoom_x86_64.pkg.tar.xz")
sha512sums=('82ebe0880a441a6f5a7f098007125f545b5eef7eace67074a97681586ffde9f9f9be0517c8730b369a80c84ee13a2ef449e9481c60e802e859323e0386db16cf')

prepare() {
	sed -i 's/Zoom\.png/Zoom/g' "${srcdir}/usr/share/applications/Zoom.desktop"
	sed -i 's/StartupWMClass=Zoom/StartupWMClass=zoom/g' "${srcdir}/usr/share/applications/Zoom.desktop"
}

package() {
	cp -dpr --no-preserve=ownership opt usr "${pkgdir}"
}
