# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Gordian Edenhofer <gordian.edenhofer@gmail.com>

pkgname=zoom
pkgver=5.7.0.25958.0621
_pkgver=${pkgver%.*.*}
_build=${pkgver#${_pkgver}.}
pkgrel=2
pkgdesc="Video Conferencing and Web Conferencing Service"
arch=('x86_64')
license=('custom')
url="https://zoom.us/"
_pinned_dependencies=(
  'cairo=1.17.4'
  'gcc-libs=11.1.0'
  'glibc>=2.33'
  'libasound.so=2'
  'libatk-1.0.so=0'
  'libdbus-1.so=3'
  'libdrm=2.4.107'
  'libfontconfig.so=1'
  'libfreetype.so=6'
  'libgdk-3.so=0'
  'libgdk_pixbuf-2.0.so=0'
  'libgio-2.0.so=0'
  'libglib-2.0.so=0'
  'libglvnd=1.3.3'
  'libgobject-2.0.so=0'
  'libgthread-2.0.so=0'
  'libgtk-3.so=0'
  'libicudata.so=69'
  'libicui18n.so=69'
  'libicuuc.so=69'
  'libpango-1.0.so=0'
  'libpangocairo-1.0.so=0'
  'libpulse-mainloop-glib.so=0'
  'libpulse.so=0'
  'libx11=1.7.2'
  'libxcb=1.14'
  'libxcomposite=0.4.5'
  'libxext=1.3.4'
  'libxfixes=6.0.0'
  'libxkbcommon.so=0'
  'libxkbcommon-x11.so=0'
  'libxrender=0.9.10'
  'libxtst=1.2.3'
  'wayland=1.19.0'
  'xcb-util-image=0.4.0'
  'xcb-util-keysyms=0.4.0'
  'zlib=1:1.2.11'
)
depends=('fontconfig' 'glib2' 'libpulse' 'libsm' 'ttf-font' 'libx11' 'libxtst' 'libxcb'
	'libxcomposite' 'libxfixes' 'libxi' 'libxcursor' 'libxkbcommon-x11' 'libxrandr'
	'libxrender' 'libxshmfence' 'libxslt' 'mesa' 'nss' 'xcb-util-image'
	'xcb-util-keysyms' 'dbus' 'ibus' "${_pinned_dependencies[@]}")
optdepends=('pulseaudio-alsa: audio via PulseAudio'
	'qt5-webengine: SSO login support'
	'picom: extra compositor needed by some window managers for screen sharing'
	'xcompmgr: extra compositor needed by some window managers for screen sharing')
options=(!strip)
source=("${pkgname}-${pkgver}_orig_x86_64.pkg.tar.xz"::"https://zoom.us/client/${_pkgver%.*}.${_build}/zoom_x86_64.pkg.tar.xz")
sha512sums=('45e9b2bb965d31741d7a52667279ae6be505a1f3870ae6ee41b292d6891154cb3fbde049cac3c95b7fee56eb9f815e15d2ce2f524480dcd5fa5b7800c8de70b8')

prepare() {
	sed -i 's/Zoom\.png/Zoom/g' "${srcdir}/usr/share/applications/Zoom.desktop"
	sed -i 's/StartupWMClass=Zoom/StartupWMClass=zoom/g' "${srcdir}/usr/share/applications/Zoom.desktop"
}

package() {
	cp -dpr --no-preserve=ownership opt usr "${pkgdir}"
}
