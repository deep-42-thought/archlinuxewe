# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname='python-scikit-image'
_pkgname="${pkgname#*-}"
pkgver=0.16.2
pkgrel=3
pkgdesc="Image processing routines for SciPy"
arch=('i686' 'pentium4' 'x86_64')
url="http://scikit-image.org/"
license=('BSD')
depends=(
  'python>=3.8'
  'python<3.9'
  'python-scipy'
  'python-matplotlib'
  'python-networkx'
  'python-pillow'
  'python-pywavelets')
makedepends=(
  'cython>=0.23')
optdepends=(
  'python-pyqt4: for imshow(x, fancy=True) and skivi'
  'freeimage: for reading various types of image file formats'
  'python-pyamg: fast cg_mg mode of random walker segmentation'
)
options=('!emptydirs')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('da499dc4e9e6c657a1c0bf5ce3c6c32c846294800142e23f8efc48863cf454fcc3e124149f00204f99f574869c51086758ad7785ec76cce395b8164bc1def561')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python setup.py install --root="${pkgdir}"/ --optimize=1
  install -D LICENSE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
