# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname='python-scikit-image'
_pkgname="${pkgname#*-}"
pkgver=0.15.0
pkgrel=3
pkgdesc="Image processing routines for SciPy"
arch=('i686' 'pentium4' 'x86_64')
url="http://scikit-image.org/"
license=('BSD')
depends=(
  'python>=3.7'
  'python<3.8'
  'python-scipy'
  'python-matplotlib'
  'python-networkx'
  'python-pillow'
  'python-pywavelets')
makedepends=(
  'cython>=0.23')
optdepends=(
  'python-pyqt4: for imshow(x, fancy=True) and skivi'
  'freeimage: for reading various types of image file formats'
  'python-pyamg: fast cg_mg mode of random walker segmentation'
)
options=('!emptydirs')
source=(https://pypi.python.org/packages/e0/46/ca035f5d7d3414124a3a5ef22cd2e75c0c5149042a668375f1d44eb69f8f/scikit-image-${pkgver}.tar.gz)
sha512sums=('9d0be54ddd564c0f28eae979b6f348011f7b38c8811f70ad460330905e706fe145dd6a30b502d15126450148fdbb3f54a271227d46e2d078030054ad00f35bc3')

build() {
  cd "${srcdir}"/scikit-image-$pkgver
  python setup.py build
}

package() {
  cd "${srcdir}"/scikit-image-$pkgver

  python setup.py install --root="${pkgdir}"/ --optimize=1
  install -D LICENSE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
