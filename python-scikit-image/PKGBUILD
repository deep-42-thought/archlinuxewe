# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname='python-scikit-image'
_pkgname="${pkgname#*-}"
pkgver=0.19.1
pkgrel=2
pkgdesc="Image processing routines for SciPy"
arch=('i686' 'pentium4' 'x86_64')
url="http://scikit-image.org/"
license=('BSD')
_pinned_dependencies=(
  'gcc-libs=11.2.0'
  'glibc>=2.31'
)
depends=(
  "${_pinned_dependencies[@]}"
  'cython>=0.29.13'
  'python>=3.10'
  'python<3.11'
  'python-cloudpickle>=0.2.1'
  'python-dask>=0.15.0'
  'python-pandas>=0.23.0'
  'python-pythran'
  'python-scikit-learn'
  'python-seaborn>=0.7.1'
  'python-wheel')
makedepends=(
  'cython>=0.23')
optdepends=(
  'python-pyqt4: for imshow(x, fancy=True) and skivi'
  'freeimage: for reading various types of image file formats'
  'python-pyamg: fast cg_mg mode of random walker segmentation'
)
options=('!emptydirs')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('643bfbed4b435ff8566a3c31eed7f63771fa5440ff4face86631f320ba8272e8ad60a957be4232107c160594feea60f21446ce7243b27468b7933d45e1c165f9')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python setup.py install --root="${pkgdir}"/ --optimize=1
  install -D LICENSE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
