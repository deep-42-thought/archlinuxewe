# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname='python-scikit-image'
_pkgname="${pkgname#*-}"
pkgver=0.18.2
pkgrel=1
pkgdesc="Image processing routines for SciPy"
arch=('i686' 'pentium4' 'x86_64')
url="http://scikit-image.org/"
license=('BSD')
_pinned_dependencies=(
  'gcc-libs=11.1.0'
  'glibc>=2.31'
)
depends=(
  "${_pinned_dependencies[@]}"
  'cython>=0.29.13'
  'python<3.10'
  'python>=3.9'
  'python-cloudpickle>=0.2.1'
  'python-dask>=0.15.0'
  'python-pandas>=0.23.0'
  'python-scikit-learn'
  'python-seaborn>=0.7.1'
  'python-wheel')
makedepends=(
  'cython>=0.23')
optdepends=(
  'python-pyqt4: for imshow(x, fancy=True) and skivi'
  'freeimage: for reading various types of image file formats'
  'python-pyamg: fast cg_mg mode of random walker segmentation'
)
options=('!emptydirs')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('a8b46d1b3042099e9bf88d8779b5b140325c531257e516d85dfb0ad967cd57bc505ad1b42badbc324539fcec25af54dcea87ebe1a0e14fb2eacd62c38ad18726')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python setup.py install --root="${pkgdir}"/ --optimize=1
  install -D LICENSE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
