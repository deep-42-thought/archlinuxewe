# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname='python-scikit-image'
_pkgname="${pkgname#*-}"
pkgver=0.17.2
pkgrel=7
pkgdesc="Image processing routines for SciPy"
arch=('i686' 'pentium4' 'x86_64')
url="http://scikit-image.org/"
license=('BSD')
_pinned_dependencies=(
  'gcc-libs=10.2.0'
  'glibc>=2.31'
)
depends=(
  "${_pinned_dependencies[@]}"
  'python>=3.9'
  'python<3.10'
  'python-imageio>=2.3.0'
  'python-matplotlib>=2.0.0'
  'python-networkx>=2.0'
  'python-numpy>=1.15.1'
  'python-pillow>=4.3.0'
  'python-pywavelets>=1.1.1'
  'python-scipy>=1.0.1'
  'python-tifffile>=2019.7.26')
makedepends=(
  'cython>=0.23')
optdepends=(
  'python-pyqt4: for imshow(x, fancy=True) and skivi'
  'freeimage: for reading various types of image file formats'
  'python-pyamg: fast cg_mg mode of random walker segmentation'
)
options=('!emptydirs')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('3e57d8bea2fd41270eb17e8489c477f58ed98e56cef0192b0b2d455f27f7a2d705161afd61c7bfd556ec938a9a2bf5663ec1aa7b79609a8e5946806356364e82')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python setup.py install --root="${pkgdir}"/ --optimize=1
  install -D LICENSE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
