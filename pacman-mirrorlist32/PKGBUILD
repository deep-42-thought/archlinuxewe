# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname=pacman-mirrorlist32
pkgver=20171115
pkgrel=1
pkgdesc=$(printf '%s' 'Arch Linux mirror list for use by pacman for i68' '6 packages on an x86' '_64 host')
arch=('any')
url="https://www.archlinux32.org/"
license=('GPL')
backup=(etc/pacman.d/mirrorlist32)
source=(archlinux32-packages::git+https://github.com/archlinux32/packages.git)
sha512sums=('SKIP')

pkgver() {
  git -C "$srcdir/archlinux32-packages" archive HEAD -- 'core/pacman-mirrorlist/PKGBUILD' | \
    tar -Ox | \
    grep '^pkgver=' | \
    cut -d= -f2
}

package() {
  mkdir -p "$pkgdir/etc/pacman.d"
  install -m644 "$srcdir/archlinux32-packages/core/pacman-mirrorlist/mirrorlist" "$pkgdir/etc/pacman.d/mirrorlist32"
}
