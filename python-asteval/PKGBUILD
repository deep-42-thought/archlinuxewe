# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='python-asteval'
_pkgname="${pkgname#*-}"
pkgver=0.9.25
pkgrel=27
pkgdesc='Minimal Python AST Evaluator'
arch=('any')
url='https://newville.github.io/asteval/'
license=('MIT')
depends=(
  'python<3.10'
  'python>=3.9')
checkdepends=(
  'python-nose'
  'python-pytest')
makedepends=(
  'python-setuptools')
source=(
  "git+https://github.com/newville/${_pkgname}?commit=${pkgver}")
sha512sums=('SKIP')

build() {
  cd "$srcdir/${_pkgname}"
  python setup.py build
}

check() {
  cd "$srcdir/${_pkgname}"
  nosetests3 tests
}

package(){
  cd "$srcdir/${_pkgname}"
  python setup.py install --root="$pkgdir/" --optimize=1
}
