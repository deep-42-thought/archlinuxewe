# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='python-asteval'
_pkgname="${pkgname#*-}"
pkgver=0.9.23
pkgrel=1
pkgdesc='Minimal Python AST Evaluator'
arch=('any')
url='https://newville.github.io/asteval/'
license=('MIT')
depends=(

  'python<3.10'
  'python>=3.9')
checkdepends=(
  'python-nose'
  'python-pytest')
makedepends=(
  'python-setuptools')
source=(
  "${_pkgname}-${pkgver}.tar.gz::https://github.com/newville/${_pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('972ae475b7c2225953efef7dcacc441a887d5311d8482ee43b49aa7906a241f3f186f4f49a272623e04c42b6a5adfc6335036f4157a1947510973912cd255ee4')

build() {
  cd "$srcdir/${_pkgname}-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/${_pkgname}-$pkgver"
  nosetests3 tests
}

package(){
  cd "$srcdir/${_pkgname}-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}
