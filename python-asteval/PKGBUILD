# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='python-asteval'
_pkgname="${pkgname#*-}"
pkgver=0.9.22
pkgrel=1
pkgdesc='Minimal Python AST Evaluator'
arch=('any')
url='https://newville.github.io/asteval/'
license=('MIT')
depends=(
  'python<3.10'
  'python>=3.9')
checkdepends=(
  'python-nose'
  'python-pytest')
makedepends=(
  'python-setuptools')
source=(
  "${_pkgname}-${pkgver}.tar.gz::https://github.com/newville/${_pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('9d55a7c58038802f117192a826f3c4531c5161714cc607ed59d17c9e60130d43011eb955e03f55dd4ea47d0f50524368fb2ea3db6cc93a17e95f777f00d55ecc')

build() {
  cd "$srcdir/${_pkgname}-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/${_pkgname}-$pkgver"
  nosetests3 tests
}

package(){
  cd "$srcdir/${_pkgname}-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}
