# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgname='python-asteval'
_pkgname="${pkgname#*-}"
pkgver=0.9.19
pkgrel=1
pkgdesc='Minimal Python AST Evaluator'
arch=('any')
url='https://newville.github.io/asteval/'
license=('MIT')
checkdepends=(
  'python-nose'
  'python-pytest')
makedepends=(
  'python-setuptools')
depends=(

  'python>=3.8'
  'python<3.9')
source=(
  "${_pkgname}-${pkgver}.tar.gz::https://github.com/newville/${_pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('5d5bd7cca7503f48c433b46996c5d23a7d13164bab8e26d3eb4bd721e7720fd43905dbe9a5eb19cb38fc8296aacf3fc7fa508ff80977ace2ed2eef16e5bbb977')

build() {
  cd "$srcdir/${_pkgname}-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/${_pkgname}-$pkgver"
  nosetests3 tests
}

package(){
  cd "$srcdir/${_pkgname}-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}
