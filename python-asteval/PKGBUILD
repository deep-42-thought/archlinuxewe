# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Erich Eckner <arch at eckner dot net>

pkgbase=python-asteval
pkgname=('python-asteval' 'python2-asteval')
pkgver=0.9.13
pkgrel=1
pkgdesc='Minimal Python AST Evaluator'
arch=('any')
url='https://newville.github.io/asteval/'
license=('MIT')
_deppy2=(
  'python2>=2.7'
  'python2<2.8'
)
_deppy=(
  'python>=3.7'
  'python<3.8'
)
_depends=('python' 'python-numpy>=1.6' 'python-six')
_makedepends=('python-setuptools')
_checkdepends=('python-nose' 'python-pytest')
makedepends=(
  "${_deppy[@]}"
  "${_deppy2[@]}"
  "${_depends[@]}" "${_depends[@]/python/python2}"
  "${_makedepends[@]}" "${_makedepends[@]/python/python2}"
  )
checkdepends=(
  "${_checkdepends[@]}" "${_checkdepends[@]/python/python2}"
  )
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/newville/asteval/archive/${pkgver}.tar.gz")
sha512sums=('f58d80ee67d35cc20a1627e320af696cc9045ee3fa1678d571b2ab304e8c98be30f7844574b6c1c2af0a2454909f58924af53f46ead76fd4d44008342eae4bde')

prepare() {
  mv "asteval-${pkgver}" "asteval-py-${pkgver}"
  cp -a "asteval-py-${pkgver}" "asteval-py2-${pkgver}"
}

build() {
  cd "$srcdir/asteval-py-$pkgver"
  python setup.py build
  
  cd "$srcdir/asteval-py2-$pkgver"
  python2 setup.py build
}

check() {
  cd "$srcdir/asteval-py-$pkgver"
  nosetests3 tests

  cd "$srcdir/asteval-py2-$pkgver"
  nosetests2 tests
}

package_python-asteval(){
  depends=(
    "${_deppy[@]}"
    "${_depends[@]}"
  )
  cd "$srcdir/asteval-py-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-asteval(){
  depends=(
    "${_deppy2[@]}"
    "${_depends[@]/python/python2}"
  )
  cd "$srcdir/asteval-py2-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:ts=2:sw=2:et:
