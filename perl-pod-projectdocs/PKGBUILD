# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname='perl-pod-projectdocs'
pkgver='0.40'
pkgrel=10
pkgdesc="generates CPAN like pod pages"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=(
  'perl>=5.34'
  'perl<5.35'
  'perl-class-accessor-fast'
  'perl-class-data-inheritable'
  'perl-json>=0.991'
  'perl-mime-base64'
  'perl-pathtools'
  'perl-pod-parser>=1.32'
  'perl-readonly'
  'perl-template-toolkit'
  'perl-uri-escape')
makedepends=(
  'perl-module-install'
)
checkdepends=('perl-json' 'perl-uri-escape')
url='http://search.cpan.org/~lyokato/Pod-ProjectDocs-0.40/'
source=("http://search.mcpan.org/CPAN/authors/id/L/LY/LYOKATO/Pod-ProjectDocs-${pkgver}.tar.gz")
sha512sums=('b62156c491707aa355f2b1136b073f10c3b96ae2c86350552f7638d0ba5799a18ec30772a1121959c207934084b6343b331db0a40df2c54bbd7d3d80f1950aa3')
_distdir="Pod-ProjectDocs-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir/$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
