# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=python-fisx
_pkgname="${pkgname#*-}"
pkgver=1.2.0
pkgrel=64
pkgdesc='Enhanced C++ port of the Physics routines used by PyMca'
arch=('i686' 'pentium4' 'x86_64')
url='https://github.com/vasole/fisx'
license=('MIT License')
_pinned_dependencies=(
  'gcc-libs=12.1.0'
  'glibc>=2.31'
)
makedepends=(
  'cython>=0.17'
  'python-numpy'
  'python-setuptools')
depends=(
  "${_pinned_dependencies[@]}"
  'python>=3.10'
  'python<3.11'
  'python-numpy')
source=(
    "${_pkgname}-v${pkgver}.tar.gz::https://github.com/vasole/${_pkgname}/archive/v${pkgver}.tar.gz"
)
sha512sums=('f063437fe55cd78d06dd13df5083cf91199c18c4e3b3cf307e8ca6da07fe18a0c7753e741538d838da90932ca6c7f7977a048c4f153795777e954a336f8cce41')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --skip-build --root="${pkgdir}" --prefix=/usr
}
