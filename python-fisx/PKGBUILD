# Maintainer: Erich Eckner <arch at eckner dot net>
pkgbase=python-fisx
pkgname=(python2-fisx python-fisx)
_pkgname="${pkgbase#*-}"
pkgver=1.1.8
pkgrel=1
pkgdesc='Enhanced C++ port of the Physics routines used by PyMca'
arch=('i686' 'pentium4' 'x86_64')
url='https://github.com/vasole/fisx'
license=('MIT License')
_deppy2=(
  'python2>=2.7'
  'python2<2.8'
)
_deppy=(
  'python>=3.7'
  'python<3.8'
)
_depends=(
  'python-numpy')
_makedepends=(
  'python-setuptools'
  'cython>=0.17')
makedepends=(
  "${_deppy[@]}"
  "${_deppy2[@]}"
  "${_depends[@]}"
  "${_depends[@]/python/python2}"
  "${_makedepends[@]}"
  "${_makedepends[@]/ython/ython2}"
)
source=(
    "${_pkgname}-v${pkgver}.tar.gz::https://github.com/vasole/${_pkgname}/archive/v${pkgver}.tar.gz"
)
sha512sums=('c7facc5416b6ef7bdcad9f500c2d933a33fe72c078c5ee98a60c6875ef25c6ef6925804bc040ccdcd4ae41b9ec74218c4a673c332f4c98aa4d6f54b14047dfe0')

prepare() {
  cp -a ${_pkgname}-${pkgver}{,-py2}
}

build() {
  echo "Building python..."
  (
    cd ${_pkgname}-${pkgver}
    python setup.py build
  )
  echo "Building python2..."
  (
    cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build
  )
}

package_python2-fisx() {
  depends=(
    "${_deppy2[@]}"
    "${_depends[@]/python/python2}"
  )
  cd ${_pkgname}-${pkgver}-py2
  python2 setup.py install --skip-build --root="${pkgdir}" --prefix=/usr
  find "${pkgdir}" -type f -exec sed -si '1 s#[/ ]python\s*$# python2#' {} +
}

package_python-fisx() {
  depends=(
    "${_deppy[@]}"
    "${_depends[@]}"
  )
  cd ${_pkgname}-${pkgver}
  python setup.py install --skip-build --root="${pkgdir}" --prefix=/usr
}
