# Maintainer:  Erich Eckner  <arch at eckner dot net>
# Contributor: Evan Gates    <evan.gates@gmail.com>
# Contributor: Dag  Odenhall <dag.odenhall@gmail.com>
# Contributor: Chris Brannon <cmbrannon@cox.net>
# Contributor: William Giokas <1007380@gmail.com>
pkgname=ii
pkgver=1.9
pkgrel=1
pkgdesc="A minimalist FIFO and filesystem-based IRC client"
license=(MIT)
arch=('i686' 'pentium4' 'x86_64')
url=https://tools.suckless.org/ii
_pinned_dependencies=(
  'glibc>=2.31'
)
depends=("${_pinned_dependencies[@]}")
source=("https://dl.suckless.org/tools/$pkgname-$pkgver.tar.gz")

sha512sums=('a665a6d1e1ffe56e370da2d703e677436ef4e18e763bb3cadbbf55e01812454ce4b79e8a20eda23846fdbe5a441ec8c534c9394995b3441472ce8a74c9035279')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i "s/CFLAGS = /CFLAGS = ${CFLAGS} /" config.mk
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr MANDIR=/usr/share/man DESTDIR="$pkgdir" install
}

