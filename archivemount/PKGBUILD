# Maintainer:  Erich Eckner <arch at eckner dot net>
# Contributor: Janne He√ü <jannehess@gmail.com>

pkgname=archivemount
pkgver=0.9.0
pkgrel=1
pkgdesc="FUSE filesystem using libarchive"
url="https://www.cybernoia.de/software/archivemount/"
license=("GPL2")
depends=("fuse" "libarchive")
makedepends=("automake" "autoconf")
conflicts=("archivemount-git")
source=("https://www.cybernoia.de/software/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('99a7a6f520c00548ee29c8f54551396bdf190f32f4a8a7246e6a95365bb01826ed41b48e85822b6f22ef802dfdb70b1286139afcadeecb815f49998e4b53dd2d')
arch=("i686" "pentium4" "x86_64")

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	aclocal
	autoheader
	automake
	autoconf
	./configure
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	make PREFIX="/usr" DESTDIR="${pkgdir}/" install-man
	mv "${pkgdir}/usr/local/share" "${pkgdir}/usr"
	rmdir "${pkgdir}/usr/local"
}

