# Maintainer: Erich Eckner <arch at eckner dot net>
pkgbase=simple-pki
pkgname=(simple-pki-ca simple-pki-cb)
pkgver=0.0.6
pkgrel=1
arch=('any')
url="https://git.eckner.net/Erich/${pkgname}"
license=('GPL')
makedepends=()
source=(
    "https://git.eckner.net/Erich/${pkgbase}/snapshot/${pkgbase}-${pkgver}.tar.xz"
)
sha512sums=('0e6e655010bfa3d1d8e1269c5f691c80710336ce5b3ba403819b70f82eef6108074a40941ac38cd8b4ef829445cd3488d89f252a0574286764b13064223bce61')

build() {

  cd ${pkgbase}-${pkgver}
  make

}

package_simple-pki-ca() {

  backup=(
    "etc/${pkgbase}/ca.conf"
    "etc/${pkgbase}/ca-ssl.conf"
  )
  for suffix in '.crl.srl' '.crt.srl' '.db' '.db.attr'; do
    backup+=(
      "etc/${pkgbase}/ca/root-ca/db/root-ca${suffix}"
      "etc/${pkgbase}/ca/signing-ca/db/signing-ca${suffix}"
    )
  done
  pkgdesc="Scripts to manage a simple pki ca - including automatic renewal"
  depends=('openssl' 'rsync')

  cd ${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install-ca

}

package_simple-pki-cb() {

  backup=(
    "etc/${pkgbase}/cb.conf"
    "etc/${pkgbase}/server-ssl.conf"
  )
  pkgdesc="Scripts to manage simple pki clients - including automatic renewal"
  depends=('openssl' 'nginx')

  cd ${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install-cb

}
