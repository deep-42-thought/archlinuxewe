# Maintainer: Erich Eckner <arch at eckner dot net>
pkgbase=simple-pki
pkgname=(simple-pki-ca simple-pki-cb)
pkgver=0.2.6
pkgrel=1
arch=('any')
url="https://git.eckner.net/Erich/${pkgname}"
license=('GPL')
makedepends=()
source=(
    "https://git.eckner.net/Erich/${pkgbase}/snapshot/${pkgbase}-${pkgver}.tar.xz"
)
sha512sums=('c293e570b2f1307b3717e76fb0caddaa544d1d43d31003161fd745843a952c771412c4c2817d3c89493cf4a86c8860d6cdfdeddc998891a3d10014688ef32ba9')

build() {

  cd ${pkgbase}-${pkgver}
  make

}

package_simple-pki-ca() {

  backup=(
    "etc/${pkgbase}/ca.conf"
    "etc/${pkgbase}/ca-ssl.conf"
  )
  pkgdesc="Scripts to manage a simple pki ca - including automatic renewal"
  depends=('openssl' 'rsync')

  cd ${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install-ca

}

package_simple-pki-cb() {

  backup=(
    "etc/${pkgbase}/cb.conf"
    "etc/${pkgbase}/server-ssl.conf"
  )
  pkgdesc="Scripts to manage simple pki clients - including automatic renewal"
  depends=('openssl' 'nginx')

  cd ${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install-cb

}
