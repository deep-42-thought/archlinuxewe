# Maintainer: Erich Eckner <arch at eckner dot net>
pkgbase=simple-pki
pkgname=(simple-pki-ca simple-pki-cb)
pkgver=0.3
pkgrel=1
arch=('any')
url="https://git.eckner.net/Erich/${pkgname}"
license=('GPL')
makedepends=()
source=(
    "https://git.eckner.net/Erich/${pkgbase}/snapshot/${pkgbase}-${pkgver}.tar.xz"
)
sha512sums=('17191a01ddabcfcd9e682f6e2e3f9dea7c199d0f94674a217aedf56c8dde675712a6d451dcfaa091aa873b1409cc7c0a6f8e886ddef683c4a6479cb82bfcd045')

build() {

  cd ${pkgbase}-${pkgver}
  make

}

package_simple-pki-ca() {

  backup=(
    "etc/${pkgbase}/ca.conf"
    "etc/${pkgbase}/ca-ssl.conf"
  )
  pkgdesc="Scripts to manage a simple pki ca - including automatic renewal"
  depends=('openssl' 'rsync')

  cd ${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install-ca

}

package_simple-pki-cb() {

  backup=(
    "etc/${pkgbase}/cb.conf"
    "etc/${pkgbase}/server-ssl.conf"
  )
  pkgdesc="Scripts to manage simple pki clients - including automatic renewal"
  depends=('openssl' 'nginx')

  cd ${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install-cb

}
