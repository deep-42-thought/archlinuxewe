# Maintainer: Erich Eckner <arch at eckner dot net>
pkgbase=simple-pki
pkgname=(simple-pki-ca simple-pki-cb)
pkgver=0.2
pkgrel=1
arch=('any')
url="https://git.eckner.net/Erich/${pkgname}"
license=('GPL')
makedepends=()
source=(
    "https://git.eckner.net/Erich/${pkgbase}/snapshot/${pkgbase}-${pkgver}.tar.xz"
)
sha512sums=('354df5bd74dd0ead44b3e979182ff8d5807b63093f946473b54fa55dd0c5f5216e354075417a057d99503602a578372c5c416bebb7f96f5b8ef68d68c75389a1')

build() {

  cd ${pkgbase}-${pkgver}
  make

}

package_simple-pki-ca() {

  backup=(
    "etc/${pkgbase}/ca.conf"
    "etc/${pkgbase}/ca-ssl.conf"
  )
  pkgdesc="Scripts to manage a simple pki ca - including automatic renewal"
  depends=('openssl' 'rsync')

  cd ${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install-ca

}

package_simple-pki-cb() {

  backup=(
    "etc/${pkgbase}/cb.conf"
    "etc/${pkgbase}/server-ssl.conf"
  )
  pkgdesc="Scripts to manage simple pki clients - including automatic renewal"
  depends=('openssl' 'nginx')

  cd ${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install-cb

}
