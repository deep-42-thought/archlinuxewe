# Maintainer: Erich Eckner <arch at eckner dot net>
pkgbase=simple-pki
pkgdesc='Scripts to manage a simple pki'
pkgname=(simple-pki-ca simple-pki-cb)
pkgver=0.4
pkgrel=1
arch=('any')
url="https://git.eckner.net/Erich/${pkgname}"
license=('GPL')
makedepends=('nginx' 'openssl' 'rsync')
source=(
    "https://git.eckner.net/Erich/${pkgbase}/snapshot/${pkgbase}-${pkgver}.tar.xz"
)
sha512sums=('6f84288ba333238df008b4f30acb60abed79b8d62ad0ddc7c7b46a46e590ef9aac1502f78ea35fc9de4ee873cfb6d4e64511d3b079b01c93ab56a2b8c7cd2b2b')

build() {

  cd ${pkgbase}-${pkgver}
  make

}

package_simple-pki-ca() {

  backup=(
    "etc/${pkgbase}/ca.conf"
    "etc/${pkgbase}/ca-ssl.conf"
  )
  pkgdesc="Scripts to manage a simple pki ca - including automatic renewal"
  depends=('openssl' 'rsync')

  cd ${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install-ca

}

package_simple-pki-cb() {

  backup=(
    "etc/${pkgbase}/cb.conf"
    "etc/${pkgbase}/server-ssl.conf"
  )
  pkgdesc="Scripts to manage simple pki clients - including automatic renewal"
  depends=('openssl' 'nginx')

  cd ${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install-cb

}
