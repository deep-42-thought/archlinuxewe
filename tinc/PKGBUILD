# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: helios
# Contributor: Lothar Gesslein
# Contributor: Dominik George <nik@naturalnet.de>

pkgname=tinc
pkgver=1.0.36_1.1pre18
_pkgver=${pkgver#*_}
pkgrel=8

pkgdesc="VPN (Virtual Private Network) daemon"
url="https://www.tinc-vpn.org/"
arch=('armv6h' 'armv7h' 'aarch64' 'i486' 'i686' 'pentium4' 'x86_64')
license=('GPL')

_pinned_dependencies_i486=(
  'gcc-libs=12.1.0'
)
_pinned_dependencies_i686=(
  'gcc-libs=12.1.0'
)
_pinned_dependencies_pentium4=(
  'gcc-libs=12.1.0'
)
_pinned_dependencies=(
  'glibc>=2.31'
  'libncursesw.so=6'
  'libreadline.so=8'
  'lzo=2.10'
  'openssl=1.1.1.o'
  'zlib=1:1.2.12'
)
depends_i486=(
  "${_pinned_dependencies_i486[@]}")
depends_i686=(
  "${_pinned_dependencies_i686[@]}")
depends_pentium4=(
  "${_pinned_dependencies_pentium4[@]}")
depends=(
  "${_pinned_dependencies[@]}")

source=(https://www.tinc-vpn.org/packages/tinc-$_pkgver.tar.gz{,.sig})

sha512sums=('d8b03c78fd579df58d4c8a03f5d2241d2c95edb660ce9aa34441f6e75df09e3fff7524215c7c4b3622311e80f5bb452a6ac1205f3fd13424d56135f70b973183'
            'SKIP')
validpgpkeys=('D62BDD168EFBE48BC60E8E234A6084B9C0D71F4A')

build() {
  cd tinc-$_pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --with-systemd=/usr/lib/systemd/system
  make
}

package() {
  cd tinc-$_pkgver

  make DESTDIR="$pkgdir/" install
  install -dm755 "$pkgdir"/usr/share/tinc/examples
  cp -a doc/sample-config/. "$pkgdir"/usr/share/tinc/examples/
  find "$pkgdir"/usr/share/tinc/examples -type f -exec chmod 644 {} +
  find "$pkgdir"/usr/share/tinc/examples -type d -exec chmod 755 {} +
}
