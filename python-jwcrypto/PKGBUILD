# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Abd√≥ Roig-Maranges <abdo.roig@gmail.com>

pkgname=python-jwcrypto
_pkgname=${pkgname#*-}
pkgver=0.7.0
pkgrel=2
pkgdesc="Python implementation of JWK, JWS, JWE specifications"
arch=("any")
url="https://github.com/latchset/jwcrypto"
license=("LGPL3")
makedepends=("python-setuptools")
depends=("python-cryptography")
source=("$_pkgname-$pkgver.tar.gz::https://github.com/latchset/$_pkgname/archive/v$pkgver.tar.gz")
sha256sums=('a7e03d102bb9e5fe886bbb7048099a7adec4c3be8f149bca32c2a205cfe1dfa7')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py build
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py install --skip-build --root="${pkgdir}" --optimize=1

    rm -Rf "${pkgdir}/usr/share/doc"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
