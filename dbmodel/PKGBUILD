# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=dbmodel
pkgver=0.3.r129.6045f9a
pkgrel=2
_pkgver=${pkgver%.*.*}
_commit=${pkgver##*.}
pkgdesc='visualize a database'
arch=('i686' 'pentium4' 'x86_64')
url='https://oxygene.sk/projects/dbmodel/'
license=('GPL2')
groups=()
depends=('qt4')
makedepends=('git')
checkdepends=()
optdepends=()
provides=()
conflicts=()
replaces=()
backup=()
options=()
source=(
    "${pkgname}::git+https://bitbucket.org/lalinsky/dbmodel.git#commit=${_commit}"
    'pfeile-entwirren.patch'
)
sha512sums=('SKIP'
            '3b618b9497b3d6213700e6c5f8996903b37acd1b0d7949e6fc59a35e4f838dece5e9edef666fe0cd6dc409e982d9dec9aea362d2fd8274ea37371c37bcf0b7f1')

pkgver(){
  cd "${pkgname}"
  git fetch --all >/dev/null 2>&1
  _commit=$(
    git rev-parse master
  )
  printf '%s.r%s.%s\n' \
    "$(
      git archive "${_commit}" -- 'src/src.pro' | \
        sed -n '
          s/^VERSION = //
          T
          p
        '
    )" \
    "$(
      git rev-list --count "${_commit}"
    )" \
    "$(
      git rev-parse --short "${_commit}"
    )"
}

prepare() {

    cd "${srcdir}/${pkgname}"
    sed -i '
      s|/usr/local|/usr|g
    ' src/src.pro
    patch -p1 -i "${srcdir}/pfeile-entwirren.patch"

}

build() {

    cd "${srcdir}/${pkgname}"
    qmake-qt4
    make

}

package() {

    cd "${srcdir}/${pkgname}"
    make "INSTALL_ROOT=${pkgdir}" install

}
