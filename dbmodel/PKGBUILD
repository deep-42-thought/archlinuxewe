# Maintainer: Erich Eckner <arch at eckner dot net>
pkgname=dbmodel
pkgver=0.3.r138.c78245d
pkgrel=99
_pkgver=${pkgver%.*.*}
_commit=${pkgver##*.}
pkgdesc='visualize a database'
arch=('i686' 'pentium4' 'x86_64')
url='https://oxygene.sk/projects/dbmodel/'
license=('GPL2')
groups=()
_pinned_dependencies=(
  'gcc-libs=12.1.0'
  'glibc>=2.31'
  'qt5-base=5.15.4+kde+r164'
  'qt5-svg=5.15.4+kde+r10'
)
depends=('qt5-tools' "${_pinned_dependencies[@]}")
makedepends=('git')
checkdepends=()
optdepends=()
provides=()
conflicts=()
replaces=()
backup=()
options=()
source=(
    "${pkgname}::git+https://git.eckner.net/Erich/dbmodel#commit=${_commit}"
)
sha512sums=('SKIP')

pkgver(){
  cd "${pkgname}"
  _rev=$(
    git rev-parse --short "${_commit}"
  )
  printf '%s.r%s.%s\n' \
    "$(
      git archive "${_commit}" -- 'src/src.pro' | \
        sed -n '
          s/^VERSION = //
          T
          p
        '
    )" \
    "$(
      git rev-list --count "${_commit}"
    )" \
    "$(
      git rev-parse --short "${_commit}"
    )"
}

build() {

    cd "${srcdir}/${pkgname}"
    qmake PREFIX=/usr
    make

}

package() {

    cd "${srcdir}/${pkgname}"
    make "INSTALL_ROOT=${pkgdir}" install

}
