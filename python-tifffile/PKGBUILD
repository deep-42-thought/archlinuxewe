# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Astro Benzene <universebenzene at sina dot com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-tifffile
pkgver=2021.6.14
_pkgname=${pkgname#python-}
pkgrel=1
pkgdesc="Read and write image data from and to TIFF files"
arch=('any')
url="https://github.com/cgohlke/tifffile"
license=('BSD')
depends=(

  'python<3.10'
  'python>=3.9')
makedepends=(
  'python-setuptools')
optdepends=('python-matplotlib>=3.1: required only for plotting'
            'python-imagecodecs>=2020.2.18: required only for encoding or decoding LZW, JPEG, etc'
            'python-lxml: required only for validating and printing XML')
#checkdepends=('python-pytest')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/cgohlke/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('eb7cbc2702c0e9dacdc2146b593dd489c13b1ef354e6fe5faa6f656a08e2d2bcd36752a3a8fe8f5629a68f969d82d0cd7c428c14ccdaa392e5f85649ce4f0c5c')

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}

    python setup.py build
}

#check() {
#    cd ${srcdir}/${_pkgname}-${pkgver}
#
#    PYTHONPATH="build/lib" pytest
#}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}

    install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
    install -Dm644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
    python setup.py install --root="${pkgdir}" --optimize=1
}
