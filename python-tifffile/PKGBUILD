# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Astro Benzene <universebenzene at sina dot com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-tifffile
pkgver=2020.11.18
_pyname=${pkgname#python-}
pkgrel=1
pkgdesc="Read and write image data from and to TIFF files"
arch=('any')
url="https://github.com/cgohlke/tifffile"
license=('BSD')
makedepends=('python-setuptools')
depends=('python-numpy>=1.15.1')
optdepends=('python-matplotlib>=3.1: required only for plotting'
            'python-imagecodecs>=2020.2.18: required only for encoding or decoding LZW, JPEG, etc'
            'python-lxml: required only for validating and printing XML')
#checkdepends=('python-pytest')
source=("https://github.com/cgohlke/$_pyname/archive/v$pkgver.tar.gz")
sha512sums=('caec58374779c0009a99847b04343e89f9184447a0fc82813fda29fd68cfe9290d285724c4fbe7b4a30270c9fd605f65fc90585bc6e4e5b30e31f1a6a989b954')

build() {
    cd ${srcdir}/${_pyname}-${pkgver}

    python setup.py build
}

#check() {
#    cd ${srcdir}/${_pyname}-${pkgver}
#
#    PYTHONPATH="build/lib" pytest
#}

package() {
    cd ${srcdir}/${_pyname}-${pkgver}

    install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
    install -Dm644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
    python setup.py install --root="${pkgdir}" --optimize=1
}
