# Maintainer: Erich Eckner <arch at eckner dot net>
# Contributor: Astro Benzene <universebenzene at sina dot com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-tifffile
pkgver=2021.10.12
_pkgname=${pkgname#python-}
pkgrel=1
pkgdesc="Read and write image data from and to TIFF files"
arch=('any')
url="https://github.com/cgohlke/tifffile"
license=('BSD')
depends=(

  'python<3.10'
  'python>=3.9')
makedepends=(
  'python-setuptools')
optdepends=('python-matplotlib>=3.1: required only for plotting'
            'python-imagecodecs>=2020.2.18: required only for encoding or decoding LZW, JPEG, etc'
            'python-lxml: required only for validating and printing XML')
#checkdepends=('python-pytest')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/cgohlke/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('1d877e766b6c228088b6b0a5cf72eeaeecaadaaa5e94a63c3bde128c803d45bebe43ca9faa3ce462d89fbaa874230147d55e59219b191d8a77560eb22d8b2e5e')

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}

    python setup.py build
}

#check() {
#    cd ${srcdir}/${_pkgname}-${pkgver}
#
#    PYTHONPATH="build/lib" pytest
#}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}

    install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
    install -Dm644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
    python setup.py install --root="${pkgdir}" --optimize=1
}
